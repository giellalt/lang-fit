!! # Meänkieli noun morphology

!! This file documents [`affixes/nouns.lexc`, the file for Meänkieli noun morphology](http://github.com/giellalt/lang-fit/blob/main/src/fst/affixes/nouns.lexc)  


!! ## This is an overview of the continuation lexicon types. 

!! ### Vowel stems
!! * n0 = 1syll  nouns: maa, suu, tie
!! * n1 = 2syll ordinary nouns: talo
!! * n2 = e-nouns: liike, säe
!! * n3 = odd-syllabic: kanava
!! * n4 = i:e nouns: veri

!! ### Consonant stems

!! * n_uus = vajavuus
!! * nc = cvc
!! * 3nc = cvcvc, jiddish
!! * nen = nainen paradigm
!! * 3nen = hevonen paradigm
!! * 3n_ks = keskus - keskuksen
!! * 3n_ue = lakeus - lakeude
!! * 3n_ime = puhelin - puhelime
!! * 3n_lnr = taival, taimen, manner
!! * nas = tehas - tehtaan
!! * 3mies = mies

!! ### Lexica for unassigned words



LEXICON nx
!! **LEXICON @LEXNAME@** pointing to *n1*.
n1 ; ! nx

LEXICON nc ! romani chib
!! **LEXICON @LEXNAME@** for consonant-final nouns, structure CVC
 +N+Sg+Nom: K ;
 +N:i  x1_sg_oblique ;
 +N:i x1_pl ;

!! ### Lexica for regular nounse

LEXICON n0
!! **LEXICON @LEXNAME@** for 1-syllabic: *maa, suu, tie, ...*
 +N: x0 ;
 +N+Nom: R ;
 +N+Sg+Gen:%>n R ;

LEXICON n0_pl ! häät
!! **LEXICON @LEXNAME@** for plurals of the same: *häät*
 +N: x1_pl ;

LEXICON n1
!! **LEXICON @LEXNAME@** for 2-syll ordinary nouns (*talo*)
 +N: x1 ;
 +N+Nom: R ;
 +N+Sg+Gen:^WG%>n R ;

LEXICON n1_pl ! 
!! **LEXICON @LEXNAME@** for the same plural words (*urut*)
 +N: x1_pl ;

LEXICON x0
!! **LEXICON @LEXNAME@** splitting to sg and pl
x0_sg ;
x0_pl ;

LEXICON x0_sg 
!! **LEXICON @LEXNAME@** sg forms x0 point here
 +Sg+Nom:       PxK ; !!≈ * @CODE@ points to Px for noun
 x0_sg_oblique ; !!≈ *  @CODE@ points to oblique

LEXICON x0_sg_oblique
!! **LEXICON @LEXNAME@** for oblique case forms in sg
 +Sg+Gen:%>n PxK ;
 +Sg+Par:%>t^A PxK ;
 +Sg+Ill:%>h^V n_PxK ; ! maahan
 +Sg+Ine:%>ss^A PxK ;      ! maassa
 +Sg+Ela:%>st^A PxK ;      ! maasta
 +Sg+All:%>le PxxK ;        ! maale
 +Sg+Ade:%>l^A PxK ;       ! maala
 +Sg+Abl:%>lt^A PxK ;      ! maalta
 +Sg+Tra: TRA ;          ! maaksi
 +Sg+Ess:%>n^A PxK ;          ! maana
 +Sg+Abe:%>tt^A PxK ;      ! maatta

LEXICON x0_pl 
!! **LEXICON @LEXNAME@** for plural case forms
 +Pl+Nom:%>t PxK ;
 +Pl+Gen:%>i2tten PxK ;
 +Pl+Par:%>i2t^A PxK ;
 +Pl+Ill:%>i2hi n_PxK ;  ! maihin
 +Pl+Ine:%>i2ss^A PxK ;
 +Pl+Ela:%>i2st^A PxK ;
 +Pl+All:%>i2le PxxK ;
 +Pl+Ade:%>i2l^A PxK ;
 +Pl+Abl:%>i2lt^A PxK ;
 +Pl+Tra:%>i2 TRA ;
 +Pl+Ess:%>i2n^A PxK ;
 +Pl+Abe:%>i2tt^A PxK ;




LEXICON x1
!! **LEXICON @LEXNAME@** for the bisyallbic, pointing to sg, pl
x1_sg ;
x1_pl ;

LEXICON x1_sg 
!! **LEXICON @LEXNAME@** bisyllabic sg
 +Sg+Nom:^E2I       PxK ; !!≈ * @CODE@ nominative separate
 x1_sg_oblique ; !!≈ * @CODE@  the rest

LEXICON x1_sg_oblique
!! **LEXICON @LEXNAME@** gives the rest
 +Sg+Gen:^WG%>n PxK ; !!≈ * @CODE@ 
 +Sg+Par:%>^A PxK ; !!≈ * @CODE@ 
 +Sg+Ill:^HMETA%>h^V^V n_PxK ; !!≈ * @CODE@ talhoon
 +Sg+Ine:^WG%>ss^A PxK ;      !!≈ * @CODE@  etc.
 +Sg+Ela:^WG%>st^A PxK ;      ! talosta
 +Sg+All:^WG%>le PxxK ;        ! talole
 +Sg+Ade:^WG%>l^A PxK ;       ! talola
 +Sg+Abl:^WG%>lt^A PxK ;      ! talolta
 +Sg+Tra:^WG TRA ;          ! taloksi
 +Sg+Ess:%>n^A PxK ;          ! talona
 +Sg+Abe:^WG%>tt^A PxK ;      ! talotta

LEXICON x1_pl 
!! **LEXICON @LEXNAME@** the pl forms
 +Pl+Nom:^WG%>t PxK ;
 +Pl+Gen:^IDEL%>i2tten PxK ;
 +Pl+Gen+Err/Orth:%>i2en PxK ;
 +Pl+Par:%>i2^A PxK ;
 +Pl+Ill:^HMETA%>h^Vi n_PxK ;  ! talhoin
 +Pl+Ine:^WG%>i2ss^A PxK ;
 +Pl+Ela:^WG%>i2st^A PxK ;
 +Pl+All:^WG%>i2le PxxK ;
 +Pl+Ade:^WG%>i2l^A PxK ;
 +Pl+Abl:^WG%>i2lt^A PxK ;
 +Pl+Tra:^WG%>i2 TRA ;
 +Pl+Ess:%>i2n^A PxK ;
 +Pl+Abe:^WG%>i2tt^A PxK ;

LEXICON n2 
!! **LEXICON @LEXNAME@**  vene
 +N: x2 ;
 +N+Nom: R ;
 +N+Gen:he%>en R ;

LEXICON n2_pl 
!! **LEXICON @LEXNAME@**  vehkheet
 +N: x2_pl ;
 
LEXICON x2
!! **LEXICON @LEXNAME@** splits in sg and pl
 x2_sg ;
 x2_pl ;

LEXICON x2_sg 
!! **LEXICON @LEXNAME@** the sg
 +Sg+Nom:^WG      KK ; ! liike, liikkekki
 +Sg+Nom+Err/Orth:^WGt      KK ; ! liiket, liikkekki
 +Sg+Gen:he%>en    PxK ; ! liikheen, säkheen fix liikkehen
 +Sg+Par:^WG%>tt^A PxK ; ! liikettä, säettä
 +Sg+Ill:h%>ee     n_PxK ; ! liikheen, säkheen
 +Sg+Ine:%>ess^A   PxK ; ! liikheessä, säkheessä
 +Sg+Ela:%>est^A   PxK ; ! liikheessä, säkheessä
 +Sg+All:%>ele    PxxK ; ! liikheele, liihkeelekki
 +Sg+Ade:%>el^A    PxK ; ! liikheelä, säkheelä
 +Sg+Abl:%>elt^A   PxK ; ! liikheeltä, säkheeltä
 +Sg+Tra:%>e TRA ;
 +Sg+Ess:%>en^A PxK ;
 +Sg+Abe:%>ett^A PxK ;

LEXICON x2_pl 
!! **LEXICON @LEXNAME@** the pl
 +Pl+Nom:h%>eet     K ;
 +Pl+Nom:h%>ee      Px ;
 +Pl+Gen:h%>eitten   PxK ;
 +Pl+Gen:h%>ejen   PxK ;
 +Pl+Par:h%>ej^A   PxK ;
 +Pl+Ill:%>e     n_PxK ; ! liikheen, säkheen
 +Pl+Ine:%>ess^A   PxK ; ! liikheessä, säkheessä
 +Pl+Ela:%>est^A   PxK ; ! liikheessä, säkheessä
 +Pl+Ade:%>ei2le    PxxK ; ! liikheelä, säkheelä
 +Pl+All:%>ei2l^A    PxK ; ! liikheelä, säkheelä
 +Pl+Abl:%>ei2lt^A    PxK ; ! liikheelä, säkheelä
 +Pl+Tra:%>ei2 TRA ;
 +Pl+Ess:%>ei2n^A PxK ;
 +Pl+Abe:%>ei2tt^A PxK ;

LEXICON 3nc
!! **LEXICON @LEXNAME@** 
+N: xnc ;

LEXICON xnc
!! **LEXICON @LEXNAME@** 
 +Sg+Nom: PxK ;      ! jiddisch
 :i x3_oblique_sg ; ! jiddischin
 :i x3_pl ;         ! jiddischit
 
LEXICON n3 
!! **LEXICON @LEXNAME@**  kanava
 +N: x3 ;
 +N+Nom:^E2I R ;
 +N+Gen:^WG%>n R ;

LEXICON n3_pl 
!! **LEXICON @LEXNAME@**  haalarit
 +N: x3_pl ;
 
LEXICON x3
!! **LEXICON @LEXNAME@** 
 x3_sg ;
 x3_pl ;

LEXICON x3_oblique
!! **LEXICON @LEXNAME@** 
x3_oblique_sg ;
x3_pl ;

LEXICON x3_sg 
!! **LEXICON @LEXNAME@** 
 +Sg+Nom:^E2I       PxK ;
 x3_oblique_sg ;

LEXICON x3_oblique_sg
!! **LEXICON @LEXNAME@** 
 +Sg+Gen:^WG%>n PxK ;
 +Sg+Par:%>^A PxK ;
 +Sg+Ill:^HMETA%>h^V^V n_PxK ;
 +Sg+Ine:^WG%>ss^A PxK ;
 +Sg+Ela:^WG%>st^A PxK ;
 +Sg+All:^WG%>lle PxxK ;
 +Sg+Ade:^WG%>ll^A PxK ;
 +Sg+Abl:^WG%>lt^A PxK ;
 +Sg+Ess:%>nn^A PxK ;
 +Sg+Tra:^WG%> TRA ;
 +Sg+Abe:^WG%>tt^A PxK ;

LEXICON x3_pl
!! **LEXICON @LEXNAME@** 
 +Pl+Nom:^WG%>t PxK ;
 +Pl+Gen:%>i2tten PxK ;
 +Pl+Par:%>j^A PxK ;
 +Pl+Ill:^HMETA%>h^Vi n_PxK ;
 +Pl+Ine:^WG%>i2ss^A PxK ;
 +Pl+Ela:^WG%>i2st^A PxK ;
 +Pl+Ade:^WG%>i2ll^A PxK ;
 +Pl+Ade:^WG%>i2ll^A PxK ;
 +Pl+Abl:^WG%>i2lt^A PxK ;
 +Pl+Tra:%>ei2 TRA ;
 +Pl+Ess:%>ei2n^A PxK ;
 +Pl+Abe:%>ei2tt^A PxK ;



LEXICON n4
!! **LEXICON @LEXNAME@** 
 +N: x4 ;
 +N+Nom:^E2I R ;
 +N+Gen:^WG%>n R ;

LEXICON n4_pl
!! **LEXICON @LEXNAME@** 
 +N: x4_pl ;
 
LEXICON x4 
!! **LEXICON @LEXNAME@**  veri
x4_sg ;
x4_pl ;

LEXICON x4_sg
 +Sg+Nom:^E2I       PxK ; ! veri
 +Sg+Gen:^WG%>n     PxK ; ! veren
 +Sg+Par:%>t^A      PxK ; ! verta
 +Sg+Ill:^HMETA%>h^V^V n_PxK ;
 +Sg+Ine:^WG%>ss^A PxK ;
 +Sg+Ela:^WG%>st^A PxK ;
 +Sg+All:^WG%>le PxxK ;
 +Sg+Ade:^WG%>l^A PxK ;
 +Sg+Abl:^WG%>lt^A PxK ;
 +Sg+Ess:%>n^A PxK ;
 +Sg+Tra:^WG TRA ;

LEXICON x4_pl
!! **LEXICON @LEXNAME@** 
 +Pl+Nom:^WG%>t PxK ;
 +Pl+Gen:%>ten PxK ;
 +Pl+Par:%>j^A PxK ;
 +Pl+Ill:^HMETA%>hii n_PxK ;
 +Pl+Ine:^WG%>i2ss^A PxK ;
 +Pl+Ela:^WG%>i2st^A PxK ;
 +Pl+All:^WG%>i2le PxxK ;
 +Pl+Ade:^WG%>i2l^A PxK ;
 +Pl+Abl:^WG%>i2lt^A PxK ;
 +Pl+Ess:%>i2n^A PxK ;
 +Pl+Tra:^WG%>i2 TRA ;


LEXICON n4_lapsi
!! **LEXICON @LEXNAME@** 
+N+Sg+Nom:psi R ;
+N+Sg+Gen:pse%>n R ;
+N+Sg+Nom:psi       PxK ; ! veri
+N+Sg+Gen:pse%>n     PxK ; ! veren
+N+Sg+Par:s%>t^A      PxK ; ! verta
+N+Sg+Ill:ps%>hee n_PxK ;
+N+Sg+Ine:pse%>ss^A PxK ;
+N+Sg+Ela:pse%>st^A PxK ;
+N+Sg+All:pse%>le PxxK ;
+N+Sg+Ade:pse%>l^A PxK ;
+N+Sg+Abl:pse%>lt^A PxK ;
+N+Sg+Ess:pse%>n^A PxK ;
+N+Sg+Tra:pse TRA ;
 
+N+Pl+Nom:pse%>t PxK ;
+N+Pl+Gen:s%>ten PxK ;
+N+Pl+Par:ps%>i^A PxK ;
+N+Pl+Ill:ps%>hii n_PxK ;
+N+Pl+Ine:%>i2ss^A PxK ;
+N+Pl+Ela:ps%>ist^A PxK ;
+N+Pl+All:ps%>ile PxxK ;
+N+Pl+Ade:ps%>il^A PxK ;
+N+Pl+Abl:ps%>ilt^A PxK ;
+N+Pl+Ess:ps%>in^A PxK ;
+N+Pl+Tra:ps TRA ;




!! #### The nainen (nen) and hevonen (3nen) family

LEXICON nen
!! **LEXICON @LEXNAME@**
+N: xnen ; 
+N+Sg+Nom:s PxK ;
+N+Sg+Gen:s PxK ;
+N+Sg+Gen:s R ;

LEXICON xnen
!! **LEXICON @LEXNAME@**
xnen_sg ;
xnen_pl ;

LEXICON xnen_sg
!! **LEXICON @LEXNAME@**
+Sg:se%> 2cases ; !!≈ @CODE@ for  Ade, All, Ess lla, lle, nna
xnen_common_sg ;

LEXICON xnen_pl
!! **LEXICON @LEXNAME@**
+Pl:se%>i2%> 2cases ;  
 xnen_common_pl ; ! all other cases

LEXICON 3nen
!! **LEXICON @LEXNAME@** 
+N: x3nen ;
+N+Sg+Gen:s R ;

LEXICON x3nen
!! **LEXICON @LEXNAME@** 
x3nen_sg ;
x3nen_pl ;

LEXICON x3nen_sg
!! **LEXICON @LEXNAME@** 
+Sg:se%> 3cases ; ! Ade, All, Ess lla, lle, nna
xnen_common_sg ;

LEXICON x3nen_pl
!! **LEXICON @LEXNAME@** 
+Pl:se%>i2%> 3cases ;  
 xnen_common_pl ; ! all other cases

LEXICON xnen_common_sg
!! **LEXICON @LEXNAME@** 
 +Sg+Nom:nen       PxK ;
 +Sg+Gen:se^WG%>n PxK ;
 +Sg+Par:s%>t^A PxK ;
 +Sg+Ill:shee n_PxK ;    ! liikheen, säkheen
 +Sg+Ine:se%>ss^A PxK ;
 +Sg+Ela:se%>st^A PxK ;
 +Sg+Abl:se%>lt^A PxK ;
 +Sg+Abe:se%>tt^A PxK ;
 +Sg+Tra:se TRA ;

LEXICON xnen_common_pl
!! **LEXICON @LEXNAME@** 
 +Pl+Nom:se^WG%>t PxK ;
 +Pl+Gen:s%>itten PxK ;
 +Pl+Gen+Err/Orth:s%>ten PxK ;
 +Pl+Par:s%>i^A PxK ;
 +Pl+Ill:shii   n_PxK ;    !
 +Pl+Ine:s^WG%>i2ss^A PxK ;
 +Pl+Ela:s%>i2st^A PxK ;
 +Pl+Abl:s%>i2lt^A PxK ;
 +Pl+Abe:s%>i2tt^A PxK ;

LEXICON 3cases
!! **LEXICON @LEXNAME@** 
+Ade:%>ll^A PxK ;
+All:%>lle PxxK ;
+Ess:%>nn^A PxK ;
+Ess+Err/Orth:%>n^A PxK ;

LEXICON 2cases
!! **LEXICON @LEXNAME@** 
+Ade:%>l^A PxK ;
+All:%>le PxxK ;
+Ess:%>n^A PxK ;


LEXICON 3n_ks
!! **LEXICON @LEXNAME@** 
+N: xn_ks ;
+N+Sg+Nom:s R ;
+N+Sg+Gen:kse%>n R ;

LEXICON xn_ks
!! **LEXICON @LEXNAME@** 
xn_ks_sg ;
xn_ks_pl ;

LEXICON xn_ks_sg
!! **LEXICON @LEXNAME@** 
 +Sg+Nom:s       PxK ;
 +Sg+Gen:kse%>n PxK ;
 +Sg+Par:s%>t^A PxK ;
 +Sg+Ill:kse^HMETA%>h^V^V n_PxK ;
 +Sg+Ine:kse%>ss^A PxK ;
 +Sg+Ela:kse%>st^A PxK ;
 +Sg+All:kse%>lle PxxK ;
 +Sg+Ade:kse%>ll^A PxK ;
 +Sg+Abl:kse%>lt^A PxK ;
 +Sg+Abe:kse%>tt^A PxK ;
 +Sg+Tra:kse TRA ;


LEXICON xn_ks_pl
!! **LEXICON @LEXNAME@** 
 +Pl+Nom:kse%>t PxK ;
 +Pl+Gen:s%>ten PxK ;
 +Pl+Par:kse%>i2^A PxK ;
 +Pl+Ine:ks%>iss^A PxK ;
 +Pl+Ill:kse^HMETA%>h^Vi n_PxK ;
 +Pl+Ela:ks%>ist^A PxK ;
 +Pl+All:ks%>i2lle PxxK ;
 +Pl+Ade:ks%>ill^A PxK ;
 +Pl+Abl:ks%>i2lt^A PxK ;
 +Pl+Abe:ks%>i2tt^A PxK ;
 +Pl+Tra:ks%>i2 TRA ;

LEXICON 3n_ue
!! **LEXICON @LEXNAME@** 
3x_ue ;
+N+Sg+Nom:s R ;
+N+Sg+Nom:de%>n R ;

LEXICON 3x_ue
!! **LEXICON @LEXNAME@** 
+N: 3x_ue_sg ;
+N: 3x_ue_pl ;

LEXICON 3x_ue_sg
!! **LEXICON @LEXNAME@** 
 +Sg+Nom:s PxK ;
 +Sg+Gen:e%>n PxK ;
 +Sg+Par:t%>t^A PxK ;
 +Sg+Ill:t%>hee n_PxK ;
 +Sg+Ine:e%>ss^A PxK ;
 +Sg+Ela:e%>st^A PxK ;
 +Sg+All:e%>lle PxxK ;
 +Sg+Ade:e%>ll^A PxK ;
 +Sg+Abl:e%>ll^A PxK ;
 +Sg+Abe:e%>tt^A PxK ;
 +Sg+Ess:te%>n^A PxK ;
 +Sg+Tra:e TRA ;

LEXICON 3x_ue_pl
!! **LEXICON @LEXNAME@** 
 +Pl+Nom:e%>t PxK ;
 +Pl+Gen:t%>ten PxK ;
 +Pl+Par:ks%>i^A PxK ;
 +Pl+Ill:t%>hii n_PxK ;
 +Pl+Ine:%>i2ss^A PxK ;
 +Pl+Ela:%>i2st^A PxK ;
 +Pl+All:%>i2lle PxxK ;
 +Pl+Ade:%>i2ll^A PxK ;
 +Pl+Abl:%>i2lt^A PxK ;
 +Pl+Abe:%>i2tt^A PxK ;
 +Pl+Tra:%>i2 TRA ;
 
LEXICON 3n_ime
!! **LEXICON @LEXNAME@** 
 +N+Sg+Nom:^WGn       R ;
 +N+Sg+Gen:me%>n R ;
 +N: 3n_ime_sg ;
 +N: 3n_ime_pl ;

LEXICON 3n_ime_sg 
!! **LEXICON @LEXNAME@** 
 +Sg+Nom:^WGn       PxK ;
 +Sg+Gen:me%>n PxK ;
 +Sg+Par:^WGn%>t^A PxK ;
 +Sg+Ill:im%>hee n_PxK ;
 +Sg+Ine:me%>ss^A PxK ;
 +Sg+Ela:me%>st^A PxK ;
 +Sg+All:me%>lle PxxK ;
 +Sg+Ade:me%>ll^A PxK ;
 +Sg+Abl:me%>ll^A PxK ;
 +Sg+Abe:me%>tt^A PxK ;
 +Sg+Ess:me%>n^A PxK ;
 +Sg+Tra:me TRA ;

LEXICON 3n_ime_pl
 +Pl+Nom:me%>t PxK ;
 +Pl+Gen:n%>ten PxK ;
 +Pl+Par:m%>i^A PxK ;
 +Pl+Ill:m%>hii n_PxK ;
 +Pl+Ine:m%>i2ss^A PxK ;
 +Pl+Ela:m%>i2st^A PxK ;
 +Pl+Ade:m%>i2lle PxxK ;
 +Pl+Ade:m%>i2ll^A PxK ;
 +Pl+Abl:m%>i2lt^A PxK ;


LEXICON nas ! tehas
+N: xnas ;
 +N+Sg+Nom:^WGs R ;
 +N+Sg+Gen:%>^Vn R ;

LEXICON xnas
 +Sg+Nom:^WGs       PxK ;
 +Sg+Gen:%>^Vn PxK ;
 +Sg+Par:^WGs%>t^A PxK ;
 +Sg+Ill:^HMETA%>h^V^V n_PxK ;
 +Sg+Ine:%>^Vss^A PxK ;
 +Sg+Ela:%>^Vst^A PxK ;
 +Sg+All:%>^Vlle PxxK ;
 +Sg+Ade:%>^Vll^A PxK ;
 +Sg+Abl:%>^Vll^A PxK ;
 +Sg+Abe:%>^Vtt^A PxK ;
 +Sg+Ess:%>^Vn^A PxK ;
 +Sg+Tra:%>^V TRA ;

 +Pl+Nom:%>^Vt PxK ;
 +Pl+Gen:%>^Vi2den PxK ;
 +Pl+Par:%>^Vi2t^A PxK ;
 +Pl+Ill:%>^V^HMETA%>h^Vi n_PxK ;
 +Pl+Ine:%>^Vi2ss^A PxK ;
 +Pl+Ela:%>^Vi2st^A PxK ;
 +Pl+All:%>^Vi2lle PxxK ;
 +Pl+Ade:%>^Vi2ll^A PxK ;
 +Pl+Abl:%>^Vi2lt^A PxK ;


LEXICON 3mies
 +N+Sg+Nom:s       PxK ;
 +N+Sg+Gen:he%>n PxK ;
 +N+Sg+Par:s%>t^A PxK ;
 +N+Sg+Ill:he%>e n_PxK ;
 +N+Sg+Ine:he%>ss^A PxK ;
 +N+Sg+Ela:he%>st^A PxK ;
 +N+Sg+All:he%>le PxxK ;
 +N+Sg+Ade:he%>l^A PxK ;
 +N+Sg+Abl:he%>lt^A PxK ;
 +N+Sg+Abe:he%>tt^A PxK ;
 +N+Sg+Ess:he%>n^A PxK ;
 +N+Sg+Tra:he TRA ;

 +N+Pl+Nom:he%>t PxK ;
 +N+Pl+Gen:h%>ien PxK ;
 +N+Pl+Par:h%>i^A PxK ;
 +N+Pl+Ill:hii n_PxK ;
 +N+Pl+Ine:he%>i2ss^A PxK ;
 +N+Pl+Ela:he%>i2st^A PxK ;
 +N+Pl+All:he%>i2le PxxK ;
 +N+Pl+Ade:he%>i2l^A PxK ;
 +N+Pl+Abl:he%>i2lt^A PxK ;
 +N+Pl+Abe:he%>i2tt^A PxK ;
 +N+Pl+Ess:he%>i2n^A PxK ;
 +N+Pl+Tra:he TRA ;

LEXICON n_uus
 +N+Sg+Nom:uus     R ;
 +N+Sg+Gen:uue%>n R ;
 +N+Sg+Nom:uus     PxK ;
 +N+Sg+Gen:uue%>n PxK ;
 +N+Sg+Par:uut%>t^A PxK ;
 +N+Sg+Ill:uut^HMETA%>h^V^V n_PxK ;
 +N+Sg+Ine:uu%>ess^A PxK ;
 +N+Sg+Ela:uu%>est^A PxK ;
 +N+Sg+All:uu%>elle PxxK ;
 +N+Sg+Ade:uu%>ell^A PxK ;
 +N+Sg+Abl:uu%>ell^A PxK ;
 +N+Sg+Abe:uu%>ett^A PxK ;
 +N+Sg+Ess:uut%>en^A PxK ;
 +N+Sg+Tra:uu TRA ;

 +N+Pl+Nom:uu%>et PxK ;
 +N+Pl+Gen:uuks%>i2en PxK ;
 +N+Pl+Par:uuks%>i2^A PxK ;
 +N+Pl+Ill:uuks%>hii n_PxK ;
 +N+Pl+Ine:uuks%>i2ss^A PxK ;
 +N+Pl+Ela:uuks%>i2st^A PxK ;
 +N+Pl+Ade:uuks%>i2lle PxxK ;
 +N+Pl+Ade:uuks%>i2ll^A PxK ;
 +N+Pl+Abl:uuks%>i2lt^A PxK ;
 +N+Pl+Abe:uuks%>i2tt^A PxK ;
 +N+Pl+Tra:uuks TRA ;
 +N+Pl+Ess:uuks%>i2n^A PxK ;
 
LEXICON 3n_lnr
 +N+Sg+Nom:^WG  R ;
 +N+Sg+Gen:e%>n R ;
 +N+Sg+Nom:^WG     PxK ;
 +N+Sg+Gen:e%>n PxK ;
 +N+Sg+Par:^WG%>t^A PxK ;
 +N+Sg+Ill:%>hee n_PxK ;
 +N+Sg+Ine:%>ess^A PxK ;
 +N+Sg+Ela:%>est^A PxK ;
 +N+Sg+All:%>elle PxxK ;
 +N+Sg+Ade:%>ell^A PxK ;
 +N+Sg+Abl:%>ell^A PxK ;
 +N+Sg+Abe:%>ett^A PxK ;
 +N+Sg+Ess:%>en^A PxK ;
 +N+Sg+Tra: TRA ;

 +N+Pl+Nom:e%>t PxK ;
 +N+Pl+Gen:^WG%>ten PxK ;
 +N+Pl+Par:e%>it^A PxK ;
 +N+Pl+Ill:%>hii n_PxK ;
 +N+Pl+Ine:%>iss^A PxK ;
 +N+Pl+Ela:%>ist^A PxK ;
 +N+Pl+Ade:%>ille PxxK ;
 +N+Pl+Ade:%>ill^A PxK ;
 +N+Pl+Abl:%>ilt^A PxK ;
 +N+Pl+Abe:%>itt^A PxK ;
 +N+Pl+Tra: TRA ;
 +N+Pl+Ess:%>in^A PxK ;




LEXICON 4n_ks
+N: x4n_ks ;
+N+Sg+Nom:s R ;
+N+Sg+Gen:kse%>n R ;

LEXICON x4n_ks
x4n_ks_sg ;
x4n_ks_pl ;

LEXICON x4n_ks_sg
 +Sg+Nom:s       PxK ;
 +Sg+Gen:kse%>n PxK ;
 +Sg+Par:s%>t^A PxK ;
 +Sg+Ill:kse^HMETA%>h^V^V n_PxK ;
 +Sg+Ine:kse%>ss^A PxK ;
 +Sg+Ela:kse%>st^A PxK ;
 +Sg+All:kse%>le PxxK ;
 +Sg+Ade:kse%>l^A PxK ;
 +Sg+Abl:kse%>lt^A PxK ;
 +Sg+Abe:kse%>tt^A PxK ;
 +Sg+Tra:kse TRA ;


LEXICON x4n_ks_pl
 +Pl+Nom:kse%>t PxK ;
 +Pl+Gen:s%>ten PxK ;
 +Pl+Par:kse%>i2^A PxK ;
 +Pl+Ine:kse%>i2ss^A PxK ;
 +Pl+Ill:ks%>hii n_PxK ;
 +Pl+Ela:kse%>i2st^A PxK ;
 +Pl+All:kse%>i2le PxxK ;
 +Pl+Ade:kse%>i2l^A PxK ;
 +Pl+Abl:kse%>i2lt^A PxK ;
 +Pl+Abe:kse%>i2tt^A PxK ;





!! ### Sublexica for cases

LEXICON TRA
:%>ks K ;
:%>ks i_PxK ; 

!! ### Sublexica for possessive suffixes
! ---------------------------------

!! Px is now not in use, with one exception, comitative.

LEXICON n_PxK  !!≈ @CODE@ has either -n or goes to Px
  :n K  ;
     Px ;

LEXICON i_PxK  !!≈ @CODE@ Tra: -i or -e and goes to Px
  :i K  ;
  :e Px ;

LEXICON PxK !!≈ @CODE@ has only -nsA, and is currently not in use. Check
   K  ;
  Px ;

LEXICON PxxK  !!≈ @CODE@ has also -Vn, thus both .. llensa and ..lleen.
  KK    ; ! All, so KK not K
  Px    ;
  Px-Vn ;

!LEXICON Pxx ! nsA and Vn
!  Px    ;
!  Px-Vn ;

LEXICON Px
 +PxSg1:%>ni    K ;
 +PxSg2:%>sti   K ;
 +PxSg3:%>ns%^A K ;
 +PxPl1:%>mm^A  K ; ! NB! -m^A ei hoidettu
 +PxPl2:%>nn^A  K ; ! NB! -n^A ei hoidettu
 +PxPl3:%>ns%^A K ;


LEXICON Px-Vn
 +PxSg3:%>%^Vn K ;
 +PxPl3:%>%^Vn K ;



