!! # Documenting Meänkieli adjective morphology


!! Most lexica here (*a1, a2, ...*) add **+A**, and thereafter redirect to the 
!! corresponding **x1, x2, ...** lexicon in `affixes/nouns.lexc` for case inflection.
!! In addition, it also points to comparative and superlative lexica.

!! ## Unassigned

LEXICON ax
!! **LEXICON @LEXNAME@** pointing to **a1**
a1 ; ! ax

LEXICON a1 
!! **LEXICON @LEXNAME@** adding **+A** and sending to **x1**, and to *3comp, 3sup*.
+A: x1 ; !!≈ * @CODE@
+A+Comp: 3comp ; !!≈ * @CODE@
+A+Superl:ime 3sup ; !!≈ * @CODE@


LEXICON a2
!! **LEXICON @LEXNAME@** gets +A and goes to x2.
+A: x2 ; !!≈ * @CODE@
+A+Comp: 4comp ; !!≈ * @CODE@
+A+Superl:ime 4sup; !!≈ * @CODE@

LEXICON a3 
!! **LEXICON @LEXNAME@** 
+A: x3 ;  !!≈ * @CODE@
+A+Comp: 4comp ; !!≈ * @CODE@
+A+Superl:ime 4sup; !!≈ * @CODE@

LEXICON a4
!! **LEXICON @LEXNAME@** 
+A: x4 ; !!≈ * @CODE@

LEXICON anen
!! **LEXICON @LEXNAME@** 
+A: xnen ; !!≈ * @CODE@

LEXICON aas
!! **LEXICON @LEXNAME@** 
+A: xnas ;

LEXICON a1_ton
!! **LEXICON @LEXNAME@** 
+A: x1_ton ;

LEXICON x1_ton
!! **LEXICON @LEXNAME@** 
 +Sg+Nom:%>^On       K ;
 +Sg+Gen:%>t^Om^An K ;
 +Sg+Par:%>^Ont^A K ;
 +Sg+Ill:t%>h^V^Vn K ; ! talhoon
 +Sg+Ine:^WG%>ss^A K ;      ! talossa
 +Sg+Ela:^WG%>st^A K ;      ! talosta
 +Sg+All:^WG%>le K ;        ! talole
 +Sg+Ade:^WG%>l^A K ;       ! talola
 +Sg+Abl:^WG%>lt^A K ;      ! talolta
 +Sg+Tra:^WG%>ksi K ;       ! taloksi
 +Sg+Tra:^WG%>ks K ;       ! taloksi
 +Sg+Ess:%>n^A K ;          ! talona
 +Sg+Abe:^WG%>tt^A K ;      ! talotta

 +Pl+Nom:^WG%>t K ;
 +Pl+Gen:%>i2en K ;
 +Pl+Par:%>i2^A K ;
 +Pl+Ill:^HMETA%>h^Vin K ;  ! talhoin
 +Pl+Ine:^WG%>i2ss^A K ;
 +Pl+Ela:^WG%>i2st^A K ;
 +Pl+All:^WG%>i2le K ;
 +Pl+Ade:^WG%>i2l^A K ;
 +Pl+Abl:^WG%>i2lt^A K ;
 +Pl+Tra:^WG%>i2ksi K ;
 +Pl+Tra:^WG%>i2ks K ;
 +Pl+Ess:%>i2n^A K ;
 +Pl+Abe:^WG%>i2tt^A K ;




!! ## Comparative inflection
!  ----------------------

LEXICON 3comp  !!≈ * @CODE@ 2syll adj, 3syll comparative
+Sg+Ess:%>mp^Ann^A K ;
+Sg+All:%>m^Alle K ;
+Sg+Ade:%>m^All^A K ;
+Pl+Ess:%>mpinn^A K ;
+Pl+All:%>mille K ;
+Pl+Ade:%>mill^A K ;
xcomp ; ! common cases from here

LEXICON 4comp  !!≈ * @CODE@ 3syll adj, 4syll comparative
+Sg+Ess:%>mp^An^A K ;
+Sg+All:%>m^Ale K ;
+Sg+Ade:%>m^Al^A K ;
+Pl+Ess:%>mpin^A K ;
+Pl+All:%>mile K ;
+Pl+Ade:%>mil^A K ;
xcomp ; ! common cases from here




LEXICON xcomp  !!≈ * @CODE@ common for 2syll and 3syll
+Sg+Nom:%>mpi # ;
+Sg+Gen:%>ma%>n K ;
+Sg+Par:%>mp^A^A K ;
+Sg+Tra:%>m^Aksi K ;
+Sg+Ill:%>mph^A^An K ;
+Sg+Ine:%>m^Ass^A K ;
+Sg+Ela:%>m^Ast^A K ;
+Sg+Abl:%>m^Alt^A K ;
+Pl+Nom:%>m^At # ;
+Pl+Gen:%>mitten K ;
+Pl+Par:%>mpi^A K ;
+Pl+Tra:%>miksi K ;
+Pl+Ill:%>mphiin K ;
+Pl+Ine:%>miss^A K ;
+Pl+Ela:%>mist^A K ;
+Sg+Abl:%>milt^A K ;





!! ### Superlative inflection
!  ----------------------

LEXICON 3sup  !!≈ * @CODE@ 2syll adj, 3syll superlative
+Sg+Ess:%>i5i5mp^Ann^A K ;
+Sg+All:%>i5m^Alle K ;
+Sg+Ade:%>i5m^All^A K ;
+Pl+Ess:%>i5mpinn^A K ;
+Pl+All:%>i5mille K ;
+Pl+Ade:%>i5mill^A K ;
xsup ; ! common cases from here

LEXICON 4sup  !!≈ * @CODE@ 3syll adj, 4syll superlative
+Sg+Ess:%>i5mp^An^A K ;
+Sg+All:%>i5m^Ale K ;
+Sg+Ade:%>i5m^Al^A K ;
+Pl+Ess:%>i5mpin^A K ;
+Pl+All:%>i5mile K ;
+Pl+Ade:%>i5mil^A K ;
xsup ; ! common cases from here




LEXICON xsup  !!≈ * @CODE@ common for 2syll and 3syll
+Sg+Nom:%>i5mpi # ;
+Sg+Gen:%>i5man K ;
+Sg+Par:%>i5mp^A^A K ;
+Sg+Tra:%>i5m^Aksi K ;
+Sg+Ill:%>i5mph^A^An K ;
+Sg+Ine:%>i5m^Ass^A K ;
+Sg+Ela:%>i5m^Ast^A K ;
+Sg+Abl:%>i5m^Alt^A K ;
+Pl+Nom:%>i5m^At # ;
+Pl+Gen:%>i5mitten K ;
+Pl+Par:%>i5mpi^A K ;
+Pl+Tra:%>i5miksi K ;
+Pl+Ill:%>i5mphiin K ;
+Pl+Ine:%>i5miss^A K ;
+Pl+Ela:%>i5mist^A K ;
+Sg+Abl:%>i5milt^A K ;
