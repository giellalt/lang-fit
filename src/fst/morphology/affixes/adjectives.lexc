!! # Documenting the file for  *Meänkieli adjective morphology*

!! This file documents  the file *affixes/adjectives.lexc* for Meänkieli adjective morphology.

!! Most lexica here (*a1, a2, ...*) add **+A**, and thereafter redirect to the 
!! corresponding **x1, x2, ...** lexicon in `affixes/nouns.lexc` for case inflection.
!! The lexicon numbers correspond to the ones for nouns.

!! In addition, each lexicon also points to comparative and superlative sublexica.

!! ## Unassigned

LEXICON ax
!! **LEXICON @LEXNAME@** pointing to **a1**. It is for adjectives that have still not been classified.
a1 ; ! ax

!! ## Regular lexica

LEXICON a1 
!! **LEXICON @LEXNAME@** adding **+A** and sending to **x1**, and to *3comp, 3sup*.
+A: x1 ; !!≈ * @CODE@
+A+Comp:%^AE 3comp ; !!≈ * @CODE@ 
!+A+Superl:ime 3sup ; !!≈ * @CODE@
+A+Superl: 3sup ; !!≈ * @CODE@

!LEXICON a1_e 
!!! **LEXICON @LEXNAME@** adding **+A** and sending to **x1**, and to *3comp, 3sup*.
!+A: x1 ; !!≈ * @CODE@
!+A+Comp:%^AE 3comp ; !!≈ * @CODE@ !TEST jan
!!+A+Superl:ime 3sup ; !!≈ * @CODE@
!+A+Superl: 3sup ; !!≈ * @CODE@

LEXICON a1_ee 
!! **LEXICON @LEXNAME@** adding **+A** and sending to **x1**, and to *3comp, 3sup*.
+A: x1 ; !!≈ * @CODE@
+A+Comp:%^AEe 3comp ; !!≈ * @CODE@ !TEST jan
!+A+Superl:ime 3sup ; !!≈ * @CODE@
+A+Superl: 3sup ; !!≈ * @CODE@

LEXICON a2
!! **LEXICON @LEXNAME@** gets +A and goes to x2.
+A: x2 ; !!≈ * @CODE@
+A+Comp: 4comp ; !!≈ * @CODE@
+A+Superl:ime 4sup; !!≈ * @CODE@

LEXICON a3 
!! **LEXICON @LEXNAME@** gets +A and points to x3
+A: x3 ;  !!≈ * @CODE@
+A+Comp: 4comp ; !!≈ * @CODE@
+A+Superl:ime 4sup; !!≈ * @CODE@

LEXICON a4
!! **LEXICON @LEXNAME@** has no comparative or superlative , just points to x4
+A: x4 ; !!≈ * @CODE@
 +A+Sg+Nom:^E2I       PxK ; ! s
 +A+Sg+Par:%>^A      PxK ; ! verta

LEXICON a5
! **LEXICON @LEXNAME@** has no comparative or superlative , what is the difference from a4?
 a4 ; !!≈ * @CODE@


LEXICON anen
!! **LEXICON @LEXNAME@**   has no comparative or superlative , just points to xnen
+A: xnen ; !!≈ * @CODE@

LEXICON aas  
!! **LEXICON @LEXNAME@**  has no comparative or superlative , just points to xnas
+A: xnas ; !!≈ * @CODE@

LEXICON a_suuri
!! **LEXICON @LEXNAME@** has no comparative or superlative , just points to x4
+A: x5_sg ; !!≈ * @CODE@
+A: x5_pl ; 
+A+Sg+Nom:^E2I       PxK ; ! suuri
+A+Pl+Ill:^E2I%>in      PxK ; ! 
+A+Comp: 4comp ; !CHECK!

LEXICON a1_ton
!! **LEXICON @LEXNAME@** 
+A: x1_ton ; !!≈ * @CODE@

LEXICON x1_ton
!! **LEXICON @LEXNAME@** 
 +Sg+Nom:%>^On       K ; !!≈ * @CODE@
 +Sg+Gen:t%>^Om^An K ; !!≈ * @CODE@
 +Sg+Par:%>^Ont^A K ; !!≈ * @CODE@
 +Sg+Ill:t%>h^Om^A^An K ;  !!≈ * @CODE@
 +Sg+Ine:t%>^Om^Ass^A K ;       !!≈ * @CODE@ etc.
 +Sg+Ela:t%>^Om^Ast^A K ;      ! talosta
 +Sg+All:t%>^Om^Ale K ;        ! talole
 +Sg+Ade:t%>^Om^Al^A K ;       ! talola
 +Sg+Abl:t%>^Om^Alt^A K ;      ! talolta
 +Sg+Tra:t%>^Om^Aksi K ;       ! taloksi
 +Sg+Tra:t%>^Om^Aks K ;       ! taloksi
 +Sg+Ess:t%>^Om^An^A K ;          ! talona
 +Sg+Abe:t%>^Om^Att^A K ;      ! talotta

 +Pl+Nom:t%>^Om^At K ;
 +Pl+Gen:t%>^Omitten K ;
 +Pl+Par:t%>^Omi2^A K ;
 +Pl+Ill:t%>^Omhiin K ;  ! talhoin
 +Sg+Ine:t%>^Omi2ss^A K ;      ! talossa
 +Sg+Ela:t%>^Omi2st^A K ;      ! talosta
 +Sg+All:t%>^Omi2le K ;        ! talole
 +Sg+Ade:t%>^Omi2l^A K ;       ! talola
 +Sg+Abl:t%>^Omi2lt^A K ;      ! talolta
 +Sg+Tra:t%>^Omi2ksi K ;       ! taloksi
 +Sg+Tra:t%>^Omi2ks K ;       ! taloksi
 +Sg+Ess:t%>^Omi2n^A K ;          ! talona
 +Sg+Abe:t%>^Omi2tt^A K ;      ! talotta




!! ## Comparative inflection
!  ----------------------

LEXICON 3comp  
!! **LEXICON @LEXNAME@**  2syll adj, 3syll comparative
+Sg+Ess:%>mp^Ann^A K ; !!≈ * @CODE@
+Sg+All:%>m^Alle K ; !!≈ * @CODE@ etc.
+Sg+Ade:%>m^All^A K ;
+Pl+Ess:%>mpinn^A K ;
+Pl+All:%>mille K ;
+Pl+Ade:%>mill^A K ;
xcomp ; ! common cases from here

LEXICON 4comp  
!! **LEXICON @LEXNAME@**   3syll adj, 4syll comparative
+Sg+Ess:%>mp^An^A K ; !!≈ * @CODE@
+Sg+All:%>m^Ale K ; !!≈ * @CODE@ etc.
+Sg+Ade:%>m^Al^A K ;
+Pl+Ess:%>mpin^A K ;
+Pl+All:%>mile K ;
+Pl+Ade:%>mil^A K ;
xcomp ; ! common cases from here




LEXICON xcomp  
!! **LEXICON @LEXNAME@**   common for 2syll and 3syll
+Sg+Nom:%>mpi # ; !!≈ * @CODE@
+Sg+Gen:%>ma%>n K ; !!≈ * @CODE@ etc.
+Sg+Par:%>mp^A^A K ;
+Sg+Tra:%>m^Aksi K ;
+Sg+Ill:%>mph^A^An K ;
+Sg+Ine:%>m^Ass^A K ;
+Sg+Ela:%>m^Ast^A K ;
+Sg+Abl:%>m^Alt^A K ;
+Pl+Nom:%>m^At # ;
+Pl+Gen:%>mitten K ;
+Pl+Par:%>mpi^A K ;
+Pl+Tra:%>miksi K ;
+Pl+Ill:%>mphiin K ;
+Pl+Ine:%>miss^A K ;
+Pl+Ela:%>mist^A K ;
+Pl+Abl:%>milt^A K ;





!! ### Superlative inflection
!  ----------------------

LEXICON 3sup  
!! **LEXICON @LEXNAME@**   2syll adj, 3syll superlative
+Sg+Ess:%>i5i5mp^Ann^A K ; !!≈ * @CODE@
+Sg+All:%>i5m^Alle K ; !!≈ * @CODE@ etc.
+Sg+Ade:%>i5m^All^A K ;
+Pl+Ess:%>i5mpinn^A K ;
+Pl+All:%>i5mille K ;
+Pl+Ade:%>i5mill^A K ;
xsup ; ! common cases from here

LEXICON 4sup 
!! **LEXICON @LEXNAME@**  3syll adj, 4syll superlative
+Sg+Ess:%>i5mp^An^A K ; !!≈ * @CODE@
+Sg+All:%>i5m^Ale K ; !!≈ * @CODE@ etc.
+Sg+Ade:%>i5m^Al^A K ;
+Pl+Ess:%>i5mpin^A K ;
+Pl+All:%>i5mile K ;
+Pl+Ade:%>i5mil^A K ;
xsup ; ! common cases from here




LEXICON xsup 
!! **LEXICON @LEXNAME@**   common for 2syll and 3syll
!+Sg+Nom:%>i5mpi # ; !!≈ * @CODE@
+Sg+Nom:^WG%>i5n # ; !!≈ * @CODE@
+Sg+Gen:%>i5man K ; !!≈ * @CODE@ etc.
+Sg+Par:%>i5mp^A^A K ;
+Sg+Tra:%>i5m^Aksi K ;
+Sg+Ill:%>i5mph^A^An K ;
+Sg+Ine:%>i5m^Ass^A K ;
+Sg+Ela:%>i5m^Ast^A K ;
+Sg+Abl:%>i5m^Alt^A K ;
+Pl+Nom:%>i5m^At # ;
+Pl+Gen:%>i5mitten K ;
+Pl+Par:%>i5mpi^A K ;
+Pl+Tra:%>i5miksi K ;
+Pl+Ill:%>i5mphiin K ;
+Pl+Ine:%>i5miss^A K ;
+Pl+Ela:%>i5mist^A K ;
+Sg+Abl:%>i5milt^A K ;
