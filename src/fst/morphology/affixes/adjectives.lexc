!! # Documenting the file for  *Meänkieli adjective morphology*

!! This file documents  the file *affixes/adjectives.lexc* for Meänkieli adjective morphology.

!! Most lexica here (*a1, a_e, ...*) add **+A**, and thereafter redirect to the 
!! corresponding **x1, x_e, ...** lexicon in `affixes/nouns.lexc` for case inflection.
!! The lexicon numbers correspond to the ones for nouns.

!! In addition, each lexicon also points to comparative and superlative sublexica.

!! * a1: pointing to x1, even syllable adjectives; kova, iso
!! * a1_ta: as a1 plus plural partitive -ta: paksuita

!! ## Unassigned

LEXICON ax
!! **LEXICON @LEXNAME@** pointing to **a1**. It is for adjectives that have still not been classified.
a1 ; ! ax

!! ## Regular lexica

LEXICON a1 
!! **LEXICON @LEXNAME@** adding **+A** and sending to **x1**, and to *3comp, 3sup*.
+A: x1 ; 
+A+Pl+Gen:^WG^IDEL%>i2e n_PxK ; ! även kovien
+A+Pl+Ill+Err/Orth:^HMETA%>^Vi2hi n_PxK ; !Check should this be Err/Orth?
+A+Comp:%^AE 3comp ; !!≈ * @CODE@ 
+A+Superl: 3sup ; !!≈ * @CODE@

LEXICON a1_ta 
!! **LEXICON @LEXNAME@** vanhaa !TODO!
a1 ;
+A+Pl+Par:^WG^IDEL%>i2ta PxK ; ! även paksuita TODO: dela upp, vilka ska ha -ta-variant?

LEXICON a1_aa
!! **LEXICON @LEXNAME@** vanhaa !TODO!
a1 ;
+A+Sg+Par:%>t^A K ;
+A+Sg+All:%>le K ; 
+A+Sg+Ade:%>l^A K ; 

LEXICON a1_kova 
!! **LEXICON @LEXNAME@** Like a1 but: 
+A: x1 ; 
+A+Pl+Gen:^WG^IDEL%>i2e n_PxK ; ! även kovien
+A+Pl+Ill+Err/Orth:^HMETA%>^Vi2hi n_PxK ; !Check should this be Err/Orth?
+A+Comp:%^AE 3comp ; 
+A+Comp+Pl+Gen:%^AE%>mpi5en K ;
+A+Superl: 3sup ; 
+A+Superl:%>i5 3sup ; 
+A+Superl+Pl+Gen:%>i5%>i5mpien K ;

LEXICON a1_e 
!! **LEXICON @LEXNAME@** vanha, which has Err/Orth vanhee-, otherwise like a1
a1 ;
+A+Pl+Ill:^HMETA%>^Vi2shii n_PxK ; !för vilka?? tagit bort för paksu
+A+Pl+Ill:^HMETA%>^Vi2ssii n_PxK ; !för vilka?? tagit bort för paksu
+A+Pl+Par:^WG^IDEL%>i2t^A PxK ; ! även paksuita
+A+Comp+Pl+Gen:%^AE%>mpi2en K ;
+A+Superl+Sg+Nom:^WG%>i5in K ; 
+A+Superl+Pl+Gen:^WG%>i5mpien K ; 

+A+Comp+Sg+Nom+Err/Orth:%^AEe%>mpi K ;
+A+Comp+Sg+Gen+Err/Orth:%^AEe%>man K ;
+A+Comp+Sg+Ade+Err/Orth:%^AEe%>malla K ;
+A+Comp+Sg+Abl+Err/Orth:%^AEe%>malta K ;
+A+Comp+Sg+Tra+Err/Orth:%^AEe%>maksi K ;
+A+Comp+Sg+Tra+Err/Orth:%^AEe%>maks K ;

+A+Comp+Pl+Gen+Err/Orth:%^AEe%>mi2tten K ;
+A+Comp+Pl+Par+Err/Orth:%^AEe%>mpi2a K ;
+A+Comp+Pl+Ill+Err/Orth:%^AEe%>mphiin K ;
+A+Comp+Pl+Ade+Err/Orth:%^AEe%>milla K ;


LEXICON a_vasen
!! **LEXICON @LEXNAME@** adding **+A** and sending to **x1**, and to *3comp, 3sup*.
+A+Sg+Nom:%>n       K ; 
+A+Sg+Gen:%>m^An K ; 
+A+Sg+Par:%>nt^A K ; 
+A+Sg+Ill:%>mph^A^An K ;  
+A+Sg+Ine:%>m^Ass^A K ;       
+A+Sg+Ela:%>m^Ast^A K ;       
+A+Sg+All:%>m^Alle K ;       
+A+Sg+Ade:%>m^All^A K ;      
+A+Sg+Abl:%>m^Alt^A K ;       
+A+Sg+Tra:%>m^Aksi K ;        
+A+Sg+Ess:%>mp^An^A K ;
+A+Sg+Ess:%>mp^Ann^A K ;        
+A+Sg+Abe:%>m^Att^A K ;      

+A+Pl+Nom:%>m^At K ;
+A+Pl+Gen:%>mitten K ;
+A+Pl+Gen:%>nten K ;
+A+Pl+Par:%>mpi2^A K ;
+A+Pl+Ill:%>mphiin K ;  
+A+Pl+Ine:%>mi2ss^A K ; 
+A+Pl+Ela:%>mi2st^A K ; 
+A+Pl+All:%>mi2lle K ;  
+A+Pl+Ade:%>mi2ll^A K ; 
+A+Pl+Abl:%>mi2lt^A K ; 
+A+Pl+Tra:%>mi2ksi K ;  
+A+Pl+Ess:%>mpi2n^A K ; 
+A+Pl+Abe:%>mi2tt^A K ; 

!+A+Comp:%^AE 3comp ;
+A+Comp: 3comp ;
+A+Comp+Sg+Ess:%>mp^An^A K ;
+A+Comp+Pl+Gen:%>mpien K ; 
+A+Comp+Pl+Ess:%>mpin^A K ;
!+A+Superl:ime 3sup ; !!≈ * @CODE@
+A+Superl:mmais 3sup ; 
!+Sg+Nom:^WG%>i5n # ;

LEXICON a_e
!! **LEXICON @LEXNAME@** gets +A and goes to x_e.
+A: x_e ; !!≈ * @CODE@
+A+Comp: 4comp ; !!≈ * @CODE@
+A+Superl:ime 4sup; !!≈ * @CODE@

LEXICON a3 
!! **LEXICON @LEXNAME@** kamala gets +A and points to x3
+A: x3_sg ;  !!≈ * @CODE@
+A+Sg+Par+Err/Orth:%>t^A PxK ; !Kolla med Elina
+A+Pl+Nom:^WG%> t_PxK ;
+A+Pl+Gen:%>i9tte n_PxK ; 
+A+Pl+Gen:%>i9e n_PxK ;
+A+Pl+Par:%>i9^A PxK ; 
+A+Pl+Ill:^HMETA%>h^Vi9 n_PxK ; 
+A+Pl+Ill:^HMET2%>h^Vi9 n_PxK ;
+A+Pl+Ine:^WG%>i9ss^A PxK ;
+A+Pl+Ela:^WG%>i9st^A PxK ;
+A+Pl+All:^WG%>i9lle PxK ; 
+A+Pl+Ade:^WG%>i9ll^A PxK ;
+A+Pl+Abl:^WG%>i9lt^A PxK ;
+A+Pl+Tra:%>i9 TRA ;
+A+Pl+Ess:%>i9n^A PxK ;
+A+Pl+Abe:%>i9tt^A PxK ;

+A+Comp: 4comp ; !!≈ * @CODE@
!+A+Superl:ime 4sup; !!≈ * @CODE@
!FRÅN 4sup och xsup
+A+Superl+Sg+Ess:%>i5mp^An^A K ; !!≈ * @CODE@
+A+Superl+Sg+All:%>i5m^Ale K ; !!≈ * @CODE@ etc.
+A+Superl+Sg+Ade:%>i5m^Al^A K ;

+A+Superl+Sg+Nom:^WG%>i5n # ; 
+A+Superl+Sg+Gen:%>i5m^An K ; 
+A+Superl+Sg+Par:%>i5mp^A^A K ;
+A+Superl+Sg+Tra:%>i5m^Aksi K ;
+A+Superl+Sg+Ill:%>i5mph^A^An K ;
+A+Superl+Sg+Ine:%>i5m^Ass^A K ;
+A+Superl+Sg+Ela:%>i5m^Ast^A K ;
+A+Superl+Sg+Abl:%>i5m^Alt^A K ;
+A+Superl+Pl+Nom:%>i5m^At # ;
+A+Superl+Pl+Gen:%>i5mitten K ;
+A+Superl+Pl+Par:%>i5mpi^A K ;
+A+Superl+Pl+Tra:%>i5miksi K ;
+A+Superl+Pl+Ill:%>i5mphiin K ;
+A+Superl+Pl+Ine:%>i5miss^A K ;
+A+Superl+Pl+Ela:%>i5mist^A K ;
+A+Superl+Pl+Abl:%>i5milt^A K ;

+A+Superl+Pl+Ess:%>i5mpin^A K ;
+A+Superl+Pl+All:%>i5mile K ;
+A+Superl+Pl+Ade:%>i5mil^A K ;


LEXICON a4
!! **LEXICON @LEXNAME@** has no comparative or superlative , just points to x4
+A: x4 ; !!≈ * @CODE@
 +A+Comp: 3comp ; !CHECK!
 +A+Superl: 3sup ; !CHECK! !TODO e-delete in nuorin etc (not nuorein)

LEXICON a5_moni
! **LEXICON @LEXNAME@** has no comparative or superlative , what is the difference from a4?
 :e a4 ; 
 +A+Sg+Nom:i       PxK ; ! s
 +A+Sg+Par:%>t^A      PxK ; 
 +A+Pl+Gen:ien PxK ; !monien

LEXICON a5_uusi
! **LEXICON @LEXNAME@** has no comparative or superlative , what is the difference from a4?
 :te a4 ; 
 +A+Sg+Nom:si       PxK ; ! s
 +A+Sg+Par:%>tt^A      PxK ; 
 +A+Pl+Gen:sien PxK ; !

 LEXICON a5_pieni
! **LEXICON @LEXNAME@** has no comparative or superlative , what is the difference from a4?
 :e a4 ; 
 +A+Sg+Nom:i       PxK ; 
 +A+Sg+Par:%>t^A      PxK ; 
 +A+Pl+Gen:%>ten PxK ; 
 !+A+Comp:i 3comp ; !Finns redan comp i a4!
 +A+Comp+Pl+Gen:e%>mpi5en K ;
 !+A+Superl:i 3sup ;
 +A+Superl:i 3sup ; !variant pieniin, pieniimistä etc
 +A+Superl+Pl+Gen:i%>mpi5en K ;
 +A+Superl+Pl+Gen:ii%>mpi5en K ;


LEXICON a5_auki
! **LEXICON @LEXNAME@** has no comparative or superlative , what is the difference from a4?
 :e a4 ; 
 +A+Sg+Nom:i       PxK ; ! 
 +A+Sg+Par:e%>^A      PxK ; ! aukea

LEXICON anen
!! **LEXICON @LEXNAME@**   has no comparative or superlative , just points to xnen
+A: xnen ; !!≈ * @CODE@ #TODO: komparera!
+A+Comp+Sg+Nom:%>sempi PxK ;
+A+Comp:se 3comp ; !CHECK!
+A+Superl:s 3sup ; !CHECK! 


LEXICON aas  
!! **LEXICON @LEXNAME@**  has no comparative or superlative , just points to xnas
+A: xnas ; 
+A+Sg+Nom:^WGs R ; !needed?
+A+Sg+Gen:%>^Vn R ; !needed?

LEXICON a_suuri
!! **LEXICON @LEXNAME@** has no comparative or superlative , just points to x4
+A:e x5_sg ; !!≈ * @CODE@
+A:e x5_pl ; 
+A+Sg+Nom:i  PxK ; ! suuri
!+A+Pl+Ill:^E2I%>in      PxK ; ! 
+A+Comp:e 3comp ; 
+A+Comp+Pl+Gen:e%>mpien K ;
+A+Comp+Sg+Ess:e%>mp^An^A K ; !även suurempana
+A+Comp+Pl+Ess:e%>mpin^A K ; !även suurempana
+A+Superl: 3sup ; 
+A+Superl:i 3sup ; !TEST extra i hela paradigmet
+A+Superl+Sg+Ess:%>i5mp^An^A K ; !även suurimpina
+A+Superl+Sg+Ess:i%>i5mp^An^A K ; !även suuriimpina
+A+Superl+Pl+Ess:%>i5mpin^A K ; !även suurimpina
+A+Superl+Pl+Ess:i%>i5mpin^A K ; !även suuriimpina
+A+Superl+Pl+Gen:%>i5mpien K ; !även suurimpien
+A+Superl+Pl+Gen:i%>i5mpien K ; !även suuriimpien

!+A+Superl+Sg+Nom:%>i5in # ; 
!+A+Superl+Pl+Nom:%>i5im^At # ; 


LEXICON a1_ton
!! **LEXICON @LEXNAME@** 
+A: x1_ton ; !!≈ * @CODE@

LEXICON x1_ton
!! **LEXICON @LEXNAME@** 
 +Sg+Nom:%>^On       K ; !!≈ * @CODE@
 +Sg+Gen:t%>^Om^An K ; !!≈ * @CODE@
 +Sg+Par:%>^Ont^A K ; !!≈ * @CODE@
 +Sg+Ill:t%>h^Om^A^An K ;  !!≈ * @CODE@
 +Sg+Ine:t%>^Om^Ass^A K ;       !!≈ * @CODE@ etc.
 +Sg+Ela:t%>^Om^Ast^A K ;      
 +Sg+All:t%>^Om^Ale K ;        
 +Sg+Ade:t%>^Om^Al^A K ;       
 +Sg+Abl:t%>^Om^Alt^A K ;      
 +Sg+Tra:t%>^Om^Aksi K ;       
 +Sg+Tra:t%>^Om^Aks K ;       
 +Sg+Ess:t%>^Om^An^A K ;  
 +Sg+Ess:t%>^Om^Ann^A K ;          
 +Sg+Abe:t%>^Om^Att^A K ;     

 +Pl+Nom:t%>^Om^At K ;
 +Pl+Gen:t%>^Omitten K ;
 +Pl+Par:t%>^Omi2^A K ;
 +Pl+Ill:t%>^Omhiin K ;  ! talhoin
 +Pl+Ine:t%>^Omi2ss^A K ;      ! talossa
 +Pl+Ela:t%>^Omi2st^A K ;      ! talosta
 +Pl+All:t%>^Omi2le K ;        ! talole
 +Pl+Ade:t%>^Omi2l^A K ;       ! talola
 +Pl+Abl:t%>^Omi2lt^A K ;      ! talolta
 +Pl+Tra:t%>^Omi2ksi K ;       ! taloksi
 +Pl+Tra:t%>^Omi2ks K ;       ! taloksi
 +Pl+Ess:t%>^Omi2n^A K ;          ! talona
 +Pl+Abe:t%>^Omi2tt^A K ;      ! talotta

+Comp:t%>^Om 3comp ; !!≈ * @CODE@ 
+Superl: 3sup ; !!≈ * @CODE@


!! ## Comparative inflection
!  ----------------------

LEXICON 3comp  
!! **LEXICON @LEXNAME@**  2syll adj, 3syll comparative
+Sg+Ess:%>mp^Ann^A K ; 
+Sg+All:%>m^Alle K ; 
+Sg+Ade:%>m^All^A K ;
+Pl+Ess:%>mpinn^A K ;
+Pl+All:%>mille K ;
+Pl+Ade:%>mill^A K ;
xcomp ; ! common cases from here

LEXICON 4comp  
!! **LEXICON @LEXNAME@**   3syll adj, 4syll comparative
+Sg+Ess:%>mp^An^A K ; !!≈ * @CODE@
+Sg+All:%>m^Ale K ; !!≈ * @CODE@ etc.
+Sg+Ade:%>m^Al^A K ;
+Pl+Ess:%>mpin^A K ;
+Pl+All:%>mile K ;
+Pl+Ade:%>mil^A K ;
xcomp ; ! common cases from here




LEXICON xcomp  
!! **LEXICON @LEXNAME@**   common for 2syll and 3syll
+Sg+Nom:^WG%>mpi # ; !!≈ * @CODE@
+Sg+Gen:%>m^A%>n K ; !!≈ * @CODE@ etc.
+Sg+Par:%>mp^A^A K ;
+Sg+Tra:%>m^Aksi K ;
+Sg+Ill:%>mph^A^An K ;
+Sg+Ine:%>m^Ass^A K ;
+Sg+Ela:%>m^Ast^A K ;
+Sg+Abl:%>m^Alt^A K ;
+Pl+Nom:%>m^At # ;
+Pl+Gen:%>mitten K ;
+Pl+Par:%>mpi^A K ;
+Pl+Tra:%>miksi K ;
+Pl+Ill:%>mphiin K ;
+Pl+Ine:%>miss^A K ;
+Pl+Ela:%>mist^A K ;
+Pl+Abl:%>milt^A K ;





!! ### Superlative inflection
!  ----------------------

LEXICON 3sup  
!! **LEXICON @LEXNAME@**   2syll adj, 3syll superlative
+Sg+Ess:%>i5mp^Ann^A K ; !!≈ * @CODE@
+Sg+All:%>i5m^Alle K ; !!≈ * @CODE@ etc.
+Sg+Ade:%>i5m^All^A K ;
+Pl+Ess:%>i5mpinn^A K ;
+Pl+All:%>i5mille K ;
+Pl+Ade:%>i5mill^A K ;
xsup ; ! common cases from here

LEXICON 4sup 
!! **LEXICON @LEXNAME@**  3syll adj, 4syll superlative
+Sg+Ess:%>i5mp^An^A K ; !!≈ * @CODE@
+Sg+All:%>i5m^Ale K ; !!≈ * @CODE@ etc.
+Sg+Ade:%>i5m^Al^A K ;
+Pl+Ess:%>i5mpin^A K ;
+Pl+All:%>i5mile K ;
+Pl+Ade:%>i5mil^A K ;
xsup ; ! common cases from here

LEXICON xsup 
!! **LEXICON @LEXNAME@**   common for 2syll and 3syll
!+Sg+Nom:%>i5mpi # ; 
+Sg+Nom:^WG%>i5n # ; 
+Sg+Gen:%>i5m^An K ; 
+Sg+Par:%>i5mp^A^A K ;
+Sg+Tra:%>i5m^Aksi K ;
+Sg+Ill:%>i5mph^A^An K ;
+Sg+Ine:%>i5m^Ass^A K ;
+Sg+Ela:%>i5m^Ast^A K ;
+Sg+Abl:%>i5m^Alt^A K ;
+Pl+Nom:%>i5m^At # ;
+Pl+Gen:%>i5mitten K ;
+Pl+Par:%>i5mpi^A K ;
+Pl+Tra:%>i5miksi K ;
+Pl+Ill:%>i5mphiin K ;
+Pl+Ine:%>i5miss^A K ;
+Pl+Ela:%>i5mist^A K ;
+Pl+Abl:%>i5milt^A K ;
