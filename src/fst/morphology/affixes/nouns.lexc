!! # Meänkieli noun morphology

!! This file documents [`affixes/nouns.lexc`, the file for Meänkieli noun morphology](http://github.com/giellalt/lang-fit/blob/main/src/fst/affixes/nouns.lexc)


!! ## This is an overview of the continuation lexicon types.

!! Special stems
!! * n_nomorph = uninflected nouns: covid-19
!! * nc = for consonant-final nouns, structure CVC (romani chib)
!! * 3nc = for triple-consonant-final (jiddisch)

!! ### Vowel stems
!! * n0 = 1syll  nouns: maa, suu, tie
!! * n1 = 2syll ordinary nouns: talo
!! * n2 = e-nouns; liike, säe, including odd-syll e-nouns karpalheila (not -lla after two vow)
!! * n3 = odd-syllabic ordinary nouns: hopea, ulvonta (ulvonnalle>ulvonnoile? MEN käräjälle>käräjille)

!! ### Consonant stems

!! * n4 = i:e nouns: suomi, kivi (2syll)
!! * n5 = i:e nouns, cns stems  kieli : kielen : kieltä (2syll)
!! * n5_lumi = lumi : lumen : lunta (2syll)
!! * n5_loimi = som n5_lumi PLUS partitiv loimea
!! * n5_vuosi = som n5_kasi PLUS former utan -o-: Sg+Gen vuen/vuoen etc
!! * n5_kasi = käsi : käden : kättä (2syll)
!! * n5_lapsi = lapsi : lapsen : lasta
!! * n5_ie = 2syll nouns ending with i and -e- only in plural: tunti, äiti>äitile
!! * n5_ie_odd = odd-syll nouns ending with i and -e- only in plural: alttari>alttarille (men alttareile)

!! * n_uus = vajavuus
!! * n_uus_odd = miehuus (miehuuele men miehuksille?)
!! * nc = cvc
!! * 3nc = cvcvc, jiddish
!! * nen = nainen paradigm
!! * 3nen = hevonen paradigm
!! * 3n_ks = keskus - keskuksen
!! * 4n_ks = even variants of 3n_ks; morahus - morahuksele
!! * n_äes = identical to 3n_ks except N+Sg+Nom (äes:äke)
!! * 3n_ue = lakeus - lakeude
!! * 3n_ime = puhelin - puhelime
!! * 3n_lnr = taimen, höyhen 
!! * 30n_lnr = sammal, askel (like 3n_lnr except Gen Ill Ine Ess) (2syll)
!! * nas = stam VVs; ankerias-ankerihaala, kauppias-kauppihaale (pga lång vokal+l), taivas-taihvaale
!! * 3mies = mies
!! * n_ien = ien
!! * n1_troppi = -i-nouns UTAN -e- i plural; troppi, kursi (>kursile) etc !CHECK needed? 
!! * n1_troppi_odd = -i-nouns UTAN -e- i plural; kontrolli (>kontrollille) etc

!! ### Lexica for unassigned words
LEXICON nx
!! **LEXICON @LEXNAME@** pointing to *n1*.
n1 ; ! nx

LEXICON n_nomorph
!! **LEXICON @LEXNAME@** for uninflected nouns
+N: K ; 

LEXICON nc ! romani chib
!! **LEXICON @LEXNAME@** for consonant-final nouns, structure CVC
+N: xc_sg ;
+N: xc_pl ;
+N+Pl+Ill:i^HMETA%>h^Vi2 n_PxK ;
+N+Pl+Ill:i^HMET2%>h^Vi2 n_PxK ;

LEXICON xc_sg
!! **LEXICON @LEXNAME@**
+Sg+Nom: K ;
      :i x1_sg_oblique ;

LEXICON xc_pl
!! **LEXICON @LEXNAME@**
 x1_pl ;




!! ### Lexica for regular nouns

LEXICON n0
!! **LEXICON @LEXNAME@** for 1-syllabic: *maa, suu, tie, ...*
 +N: x0 ;
 +N+Nom: R ;
 +N+Sg+Gen:%>n R ;

LEXICON n0_pl ! häät
!! **LEXICON @LEXNAME@** for plurals of the same: *häät*
 +N: x0_pl ;

LEXICON x0
 !! **LEXICON @LEXNAME@** splitting to sg and pl
 x0_sg ;
 x0_pl ;

LEXICON x0_sg
 !! **LEXICON @LEXNAME@** sg forms x0 point here
  +Sg+Nom:       PxK ; !!≈ * @CODE@ points to Px for noun
  x0_sg_oblique ; !!≈ *  @CODE@ points to oblique

LEXICON x0_sg_oblique
 !! **LEXICON @LEXNAME@** for oblique case forms in sg
  +Sg+Gen:%> n_PxK ;
  +Sg+Par:%>t^A PxK ;
  +Sg+Ill:%>h^V n_PxK ; ! maahan
  +Sg+Ine:%>ss^A PxK ;      ! maassa
  +Sg+Ela:%>st^A PxK ;      ! maasta
  +Sg+All:%>le PxxK ;        ! maale
  +Sg+Ade:%>l^A PxK ;       ! maala
  +Sg+Abl:%>lt^A PxK ;      ! maalta
  +Sg+Tra: TRA ;          ! maaksi
  +Sg+Ess:%>n^A PxK ;          ! maana
  +Sg+Abe:%>tt^A PxK ;      ! maatta

LEXICON x0_pl
 !! **LEXICON @LEXNAME@** for plural case forms
  +Pl+Nom:%> t_PxK ;
  +Pl+Gen:%>i2tte n_PxK ;
  +Pl+Par:%>i2t^A PxK ;
  +Pl+Ill:%>i2hi n_PxK ;  ! maihin
  +Pl+Ine:%>i2ss^A PxK ;
  +Pl+Ela:%>i2st^A PxK ;
  +Pl+All:%>i2le PxxK ;
  +Pl+Ade:%>i2l^A PxK ;
  +Pl+Abl:%>i2lt^A PxK ;
  +Pl+Tra:%>i2 TRA ;
  +Pl+Ess:%>i2n^A PxK ;
  +Pl+Abe:%>i2tt^A PxK ;



LEXICON n1
!! **LEXICON @LEXNAME@** for 2-syll ordinary nouns (*talo*)
 +N: x1 ;
 +N+Sg+Nom:       PxK ; !!≈ * @CODE@ Px separate
 +N+Nom: R ;
 +N+Sg+Gen:^WG%>n R ;

LEXICON n1_pl !
!! **LEXICON @LEXNAME@** for the same plural words (*urut*)
 +N: x1_pl ;


LEXICON x1
!! **LEXICON @LEXNAME@** for the bisyallbic, pointing to sg, pl
x1_sg ;
x1_pl ;

LEXICON x1_sg
!! **LEXICON @LEXNAME@** bisyllabic sg
+Sg+Nom: K ;
 x1_sg_oblique ; !!≈ * @CODE@  the rest

LEXICON x1_sg_oblique
!! **LEXICON @LEXNAME@** gives the rest
 +Sg+Gen:^WG%> n_PxK ; !!≈ * @CODE@ ! lagt till underscore
 +Sg+Par:%>^A PxK ; !!≈ * @CODE@
 +Sg+Ill:^HMETA%>h^V^V n_PxK ; !!≈ * @CODE@ saunhaan 
 +Sg+Ill:^HMET2%>h^V^V n_PxK ; !!≈ * @CODE@ talhoon, sauhnaan 
 +Sg+Ine:^WG%>ss^A PxK ;      !!≈ * @CODE@  etc.
 +Sg+Ela:^WG%>st^A PxK ;      ! talosta
 +Sg+All:^WG%>le PxxK ;        ! talole
 +Sg+Ade:^WG%>l^A PxK ;       ! talola
 +Sg+Abl:^WG%>lt^A PxK ;      ! talolta
 +Sg+Tra:^WG TRA ;          ! taloksi
 +Sg+Ess:%>n^A PxK ;          ! talona
 +Sg+Abe:^WG%>tt^A PxK ;      ! talotta

LEXICON x1_pl
!! **LEXICON @LEXNAME@** the pl forms
 +Pl+Nom:^WG%> t_PxK ;
 +Pl+Gen:^WG^IDEL%>i2tte n_PxK ;
 +Pl+Gen+Err/Orth:^WG%>i2e n_PxK ;
 +Pl+Par:%>i2^A PxK ; 
 +Pl+Ill:^HMETA%>h^Vi2 n_PxK ;  ! saunhoin 
 +Pl+Ill:^HMET2%>h^Vi2 n_PxK ;  ! sauhnoin 
 +Pl+Ine:^WG%>i2ss^A PxK ;
 +Pl+Ela:^WG%>i2st^A PxK ;
 +Pl+All:^WG%>i2le PxxK ;
 +Pl+Ade:^WG%>i2l^A PxK ;
 +Pl+Abl:^WG%>i2lt^A PxK ;
 +Pl+Tra:^WG%>i2 TRA ;
 +Pl+Ess:%>i2n^A PxK ; 
 +Pl+Abe:^WG%>i2tt^A PxK ;



LEXICON n2
!! **LEXICON @LEXNAME@**  vene, liike, säe
 +N: x2 ;
 +N+Nom: R ;
 +N+Gen:he%>en R ;

LEXICON n2_pl
!! **LEXICON @LEXNAME@**  vehkheet
 +N: x2_pl ;

LEXICON x2
!! **LEXICON @LEXNAME@** splits in sg and pl
 x2_sg ;
 x2_pl ;

LEXICON x2_sg ! nyare lånord, tex "nalle" böjs -heen i illativ
!! **LEXICON @LEXNAME@** the sg
 +Sg+Nom:^WG     PxK ; ! liike, liikkekki
 +Sg+Nom+Err/Orth:^WGt      PxK ; ! liiket, liikkekki
 +Sg+Gen:h%>ee n_PxK ; ! liikheen, säkheen, ainheen !TEST stryka WG före h?
 +Sg+Gen:^HMET2%>hee n_PxK ; ! aihneen, liihkeen
 +Sg+Par:^WG%>tt^A PxK ; ! liikettä, säettä 
 +Sg+Ill:h%>eeshee n_PxK ; ! liikheesheen !TEST stryka WG före h?
 +Sg+Ill:^HMET2%>heeshee n_PxK ; ! aihneesheen
 +Sg+Ine:h%>eess^A   PxK ; ! liikheessä, säkheessä, ainheessa !TEST stryka WG före h?
 +Sg+Ine:^HMET2%>heess^A   PxK ; ! NY aihneessa 
 +Sg+Ela:h%>eest^A   PxK ; ! liikheestä, säkheestä !TEST stryka WG före h?
 +Sg+Ela:^HMET2%>heest^A   PxK ; ! NY aihneesta
 +Sg+All:h%>eele    PxxK ; ! liikheele, liihkeelekki !TEST stryka WG före h?
 +Sg+All:^HMET2%>heele    PxxK ; ! NY
 +Sg+Ade:h%>eel^A    PxK ; ! liikheelä, säkheelä !TEST stryka WG före h?
 +Sg+Ade:^HMET2%>heel^A    PxK ; ! NY
 +Sg+Abl:h%>eelt^A   PxK ; ! liikheeltä, säkheeltä !TEST stryka WG före h?
 +Sg+Abl:^HMET2%>heelt^A   PxK ; ! NY
 +Sg+Tra:h%>ee TRA ; !TEST stryka WG före h?
 +Sg+Tra:^HMET2%>hee TRA ; ! NY
 +Sg+Ess:h%>een^A PxK ; !TEST stryka WG före h?
 +Sg+Ess:^HMET2%>heen^A PxK ; ! NY
 +Sg+Abe:%>ett^A PxK ;

LEXICON x2_pl
!! **LEXICON @LEXNAME@** the pl
 +Pl+Nom:h%>ee t_PxK ; !
 +Pl+Gen:h%>eitte n_PxK ; !
 +Pl+Gen:h%>eje n_PxK ; !
 +Pl+Par:h%>ej^A   PxK ; !
 +Pl+Par:h%>eit^A   PxK ; !
 +Pl+Ill:h%>ei2shii n_PxK ; !
 +Pl+Ill:^HMET2%>hei2shii n_PxK ; 
 +Pl+Ine:h%>ei2ss^A   PxK ; ! liikheissä !
 +Pl+Ine:^HMET2%>hei2ss^A   PxK ;
 +Pl+Ela:h%>ei2st^A   PxK ; ! liikheessä, säkheessä !
 +Pl+Ela:^HMET2%>hei2st^A   PxK ; 
 +Pl+All:h%>ei2le    PxxK ; ! liikheelä, säkheelä!
 +Pl+All:^HMET2%>hei2le    PxxK ;
 +Pl+Ade:h%>ei2l^A    PxK ; ! liikheelä, säkheelä!
 +Pl+Ade:^HMET2%>hei2l^A    PxK ; 
 +Pl+Abl:h%>ei2lt^A    PxK ; ! liikheelä, säkheelä!
 +Pl+Abl:^HMET2%>hei2lt^A    PxK ;
 +Pl+Tra:h%>ei2 TRA ; !
 +Pl+Tra:^HMET2%>hei2 TRA ;
 +Pl+Ess:h%>ei2n^A PxK ; !
 +Pl+Ess:^HMET2%>hei2n^A PxK ;
 +Pl+Abe:h%>ei2tt^A PxK ; !
 +Pl+Abe:^HMET2%>hei2tt^A PxK ;


!LEXICON n2_odd
!! **LEXICON @LEXNAME@**  urvakke etc, n2-ord med -lle/-lla 
 !+N: x2_odd ;
 !+N+Nom: R ;
 !+N+Gen:he%>en R ;

!LEXICON x2_odd
!! **LEXICON @LEXNAME@** splits in sg and pl
 !x2_odd_sg ;
 !x2_odd_pl ;

!LEXICON x2_odd_sg ! urvakke etc, n2-ord med -lle/-lla 
!! **LEXICON @LEXNAME@** the sg
 !+Sg+Nom:^WG      KK ; ! eläke
 !+Sg+Nom+Err/Orth:^WGt      KK ; ! 
 !+Sg+Gen:^WGh%>een    PxK ; ! 
 !+Sg+Gen:^HMET2%>heen    PxK ; ! 
 !+Sg+Par:^WG%>tt^A PxK ; ! 
 !+Sg+Ill:^WGh%>eeshee     n_PxK ; ! 
 !+Sg+Ill:^HMET2%>heeshee     n_PxK ; ! 
 !+Sg+Ine:^WGh%>eess^A   PxK ; ! 
 !+Sg+Ine:^HMET2%>heess^A   PxK ; ! 
 !+Sg+Ela:^WGh%>eest^A   PxK ; ! 
 !+Sg+Ela:^HMET2%>heest^A   PxK ; ! 
 !+Sg+All:^WGh%>eele    PxxK ; ! 
 !+Sg+All:^HMET2%>heele    PxxK ; ! 
 !+Sg+Ade:^WGh%>eel^A    PxK ; ! 
 !+Sg+Ade:^HMET2%>heel^A    PxK ; ! 
 !+Sg+Abl:^WGh%>eelt^A   PxK ; ! 
 !+Sg+Abl:^HMET2%>heelt^A   PxK ; ! 
 !+Sg+Tra:^WGh%>ee TRA ;
 !+Sg+Tra:^HMET2%>hee TRA ; ! 
 !+Sg+Ess:^WGh%>een^A PxK ;
 !+Sg+Ess:^HMET2%>heen^A PxK ; ! 
 !+Sg+Abe:%>ett^A PxK ;

!LEXICON x2_odd_pl
!! **LEXICON @LEXNAME@** the pl
 !+Pl+Nom:^WGh%>eet     K ;
 !+Pl+Nom:^WGh%>ee      Px ;
 !+Pl+Gen:^WGh%>eitten   PxK ;
 !+Pl+Gen:^WGh%>ejen   PxK ;
 !+Pl+Par:^WGh%>ej^A   PxK ;
 !+Pl+Ill:%>e     n_PxK ; ! liikheen, säkheen
 !+Pl+Ine:%>ess^A   PxK ; ! liikheessä, säkheessä
 !+Pl+Ela:%>est^A   PxK ; ! liikheessä, säkheessä
 !+Pl+Ade:%>ei2le    PxxK ; ! liikheelä, säkheelä
 !+Pl+All:%>ei2l^A    PxK ; ! liikheelä, säkheelä
 !+Pl+Abl:%>ei2lt^A    PxK ; ! liikheelä, säkheelä
 !+Pl+Tra:%>ei2 TRA ;
 !+Pl+Ess:%>ei2n^A PxK ;
 !+Pl+Abe:%>ei2tt^A PxK ;



LEXICON n3 
!! **LEXICON @LEXNAME@**  odd-syllabic: kanava
 +N: x3 ;
 +N+Nom:^E2I R ;
 +N+Gen:^WG%>n R ;

LEXICON n3_pl
!! **LEXICON @LEXNAME@**  haalarit
 +N: x3_pl ;

LEXICON x3
!! **LEXICON @LEXNAME@**
 x3_sg ;
 x3_pl ;

LEXICON x3_oblique
!! **LEXICON @LEXNAME@**
x3_oblique_sg ;
x3_pl ;

LEXICON x3_sg
!! **LEXICON @LEXNAME@**
 +Sg+Nom:^E2I       PxK ;
 x3_oblique_sg ;

LEXICON x3_oblique_sg
!! **LEXICON @LEXNAME@**
 +Sg+Gen:^WG%> n_PxK ;
 +Sg+Par:%>^A PxK ;
 +Sg+Par+Err/Orth:%>t^A PxK ; !asiata
 +Sg+Ill:^HMETA%>h^V^V n_PxK ;
 +Sg+Ill:^HMET2%>h^V^V n_PxK ;
 +Sg+Ine:^WG%>ss^A PxK ;
 +Sg+Ela:^WG%>st^A PxK ;
 +Sg+All:^WG%>lle PxxK ;
 +Sg+Ade:^WG%>ll^A PxK ;
 +Sg+Abl:^WG%>lt^A PxK ;
 +Sg+Ess:%>nn^A PxK ;
 +Sg+Tra:^WG TRA ;
 +Sg+Abe:^WG%>tt^A PxK ;

LEXICON x3_pl
!! **LEXICON @LEXNAME@**
 +Pl+Nom:^WG%> t_PxK ;
 +Pl+Gen:%>i2tte n_PxK ;
 +Pl+Par:%>i2t^A PxK ; !asioita
 +Pl+Ill:^HMETA%>h^Vi2 n_PxK ;
 +Pl+Ill:^HMET2%>h^Vi2 n_PxK ;
 +Pl+Ine:^WG%>i2ss^A PxK ;
 +Pl+Ela:^WG%>i2st^A PxK ;
 +Pl+All:^WG%>i2lle PxK ;
 +Pl+Ade:^WG%>i2ll^A PxK ;
 +Pl+Abl:^WG%>i2lt^A PxK ;
 +Pl+Tra:%>i2 TRA ;
 +Pl+Ess:%>i2n^A PxK ;
 +Pl+Abe:%>i2tt^A PxK ;

LEXICON 3nc ! jiddisch
!! **LEXICON @LEXNAME@**
+N: xnc ;

LEXICON xnc
!! **LEXICON @LEXNAME@**
 +Sg+Nom: PxK ;      ! jiddisch
 :i x3_oblique_sg ; ! jiddischin
  x3_pl ;         ! jiddischit
 +Pl+Ill:i^HMETA%>h^Vi2 n_PxK ;       ! 



!! #### The i>e-family; kivi, kieli, käsi, lumi etc

LEXICON n4
!! **LEXICON @LEXNAME@** kivi, stem kive
 +N: x4 ;
 +N+Sg+Nom:^E2I   K ; ! kivi
 +N+Sg+Nom:       Px ; ! kivensä
 +N+Sg+Par:%>^A   PxK ; ! kiveä
 +N+Nom:^E2I      R ;
 +N+Sg+Gen:^WG%>n R ;

LEXICON x4
!! **LEXICON @LEXNAME@**  veri
x4_sg ;
x4_pl ;

LEXICON n4_pl
!! **LEXICON @LEXNAME@**
 +N: x4_pl ;

LEXICON x4_sg
!! **LEXICON @LEXNAME@** shared lexica for n4, n5, n5_lumi/loimi/lapsi EXCEPT SgNom, SgPar
+Sg+Gen:^WG%> n_PxK ; ! veren
 +Sg+Ill:^HMETA%>h^V^V n_PxK ;
 +Sg+Ill:^HMET2%>h^V^V n_PxK ;
 +Sg+Ine:^WG%>ss^A PxK ;
 +Sg+Ela:^WG%>st^A PxK ;
 +Sg+All:^WG%>le PxxK ;
 +Sg+Ade:^WG%>l^A PxK ;
 +Sg+Abl:^WG%>lt^A PxK ;
 +Sg+Ess:%>n^A PxK ;
 +Sg+Tra:^WG TRA ;
 +Sg+Abe:^WG%>tt^A PxK;

LEXICON x4_pl
!! **LEXICON @LEXNAME@**
 +Pl+Nom:^WG%> t_PxK ;
 +Pl+Gen:^E2I%>tte n_PxK ;
 +Pl+Gen:^E2Ie%> n_PxK ;
 +Pl+Par:^E2I%>^A PxK ;
 +Pl+Ill:^HMETA%>hii n_PxK ;
 +Pl+Ill:^HMET2%>hii n_PxK ;
 +Pl+Ine:^WG^E2I%>ss^A PxK ; !NY ändr neråt
 +Pl+Ela:^WG^E2I%>st^A PxK ;
 +Pl+All:^WG^E2I%>le PxxK ;
 +Pl+Ade:^WG^E2I%>l^A PxK ;
 +Pl+Abl:^WG^E2I%>lt^A PxK ;
 +Pl+Ess:^E2I%>n^A PxK ;
 +Pl+Tra:^WG^E2I%> TRA ;
 +Pl+Abe:^E2I%>tt^A PxK;


LEXICON n5
!! **LEXICON @LEXNAME@** kieli, stem kiele
 +N: x5_sg ;
 +N: x5_pl ;

 LEXICON x5_sg
!! **LEXICON @LEXNAME@** kieli, stem kiele
 x4_sg ;
 +Sg+Nom:^E2I       PxK ; ! kieli
 +Sg+Par:%>t^A      PxK ; ! kieltä
 +Nom:^E2I R ;
 +Gen:^WG%>n R ;

LEXICON x5_pl
x4_pl ;

LEXICON n5_lumi
!! **LEXICON @LEXNAME@** lumi, stem lu
 +N:me x4 ;
 +N+Sg+Nom:mi       PxK ; ! lumi
 +N+Sg+Par:ne%>t^A      PxK ; ! lunta
 +N+Nom:mi R ;
 +N+Gen:me%>n R ;

LEXICON n5_loimi 
!! **LEXICON @LEXNAME@** loimi, stem loi, som n5_lumi PLUS partitiv loimea
n5_lumi ;
+N+Sg+Par:me%>^A      PxK ; ! loimea


LEXICON n5_vuosi
!! **LEXICON @LEXNAME@** vuosi> vuoessa/vuessa, stem ELLER vu
 +N+Nom:osi R ;
 +N:ote x5_kasi ;
 +N+Sg+Nom:osi       PxK ; ! vuosi
 +N+Sg+Par:ot%>t^A      PxK ; ! vuotta
 +N+Sg+Ine:^WG%>ess^A PxK ; !
 +N+Sg+Ela:^WG%>est^A PxK ;
 +N+Sg+Ess:o%>n^A PxK ;
 +N+Sg+Gen:e%> n_PxK ;
 +N+Sg+Tra:^WG%>e TRA ;

LEXICON n5_kasi
!! **LEXICON @LEXNAME@** käsi, stem kä
 +N:te x5_kasi ;
 +N+Sg+Nom:si       PxK ; ! käsi
 +N+Sg+Par:t%>t^A      PxK ; ! kättä
 +N+Nom:si R ;
 +N+Gen:e%>n R ;

LEXICON n5_kasi_pl
!! **LEXICON @LEXNAME@**  continuation for kasi_pl
+N: x5_kasi_pl ;

LEXICON x5_kasi
!! **LEXICON @LEXNAME@**  veri
x4_sg ;
x5_kasi_pl ;

LEXICON x5_kasi_pl
!! **LEXICON @LEXNAME@**
 +Pl+Nom:^WG%> t_PxK ;
 +Pl+Gen:^E2I%>tte n_PxK ;
 +Pl+Gen:^E2Ie%> n_PxK ;
 +Pl+Par:^E2I%>^A PxK ;
 +Pl+Ill:^HMETA%>hii n_PxK ;
 +Pl+Ill:^HMET2%>hii n_PxK ;
 +Pl+Ine:^E2I^WG%>ss^A PxK ; !NY ändr neråt
 +Pl+Ela:^E2I^WG%>st^A PxK ;
 +Pl+All:^E2I^WG%>le PxxK ;
 +Pl+Ade:^E2I^WG%>l^A PxK ;
 +Pl+Abl:^E2I^WG%>lt^A PxK ;
 +Pl+Ess:^E2I%>n^A PxK ;
 +Pl+Tra:^E2I^WG%> TRA ;
 +Pl+Abe:^E2I%>tt^A PxK;


LEXICON n5_lapsi
!! **LEXICON @LEXNAME@**
+N+Sg+Nom:psi R ;
+N+Sg+Gen:pse%>n R ;
+N+Sg+Nom:psi       PxK ; ! lapsi
+N+Sg+Gen:pse%> n_PxK ; ! lapsen
+N+Sg+Par:s%>t^A      PxK ; ! lasta
+N+Sg+Ill:ps%>hee n_PxK ;
+N+Sg+Ine:pse%>ss^A PxK ;
+N+Sg+Ela:pse%>st^A PxK ;
+N+Sg+All:pse%>le PxxK ;
+N+Sg+Ade:pse%>l^A PxK ;
+N+Sg+Abl:pse%>lt^A PxK ;
+N+Sg+Ess:pse%>n^A PxK ;
+N+Sg+Tra:pse TRA ;
+N+Sg+Abe:pse%>tt^A PxK ;

+N+Pl+Nom:pse%> t_PxK ;
+N+Pl+Gen:s%>te n_PxK ;
+N+Pl+Par:ps%>i^A PxK ;
+N+Pl+Ill:ps%>hii n_PxK ;
+N+Pl+Ine:ps%>i2ss^A PxK ;
+N+Pl+Ela:ps%>ist^A PxK ;
+N+Pl+All:ps%>ile PxxK ;
+N+Pl+Ade:ps%>il^A PxK ;
+N+Pl+Abl:ps%>ilt^A PxK ;
+N+Pl+Ess:ps%>in^A PxK ;
+N+Pl+Tra:psi TRA ;
+N+Pl+Abe:ps%>itt^A PxK ;

LEXICON n5_ie
!! **LEXICON @LEXNAME@** for 2-syll ordinary nouns ending with -i; malmi, äiti, poiting to x5_i
 +N: x5_i ;
 +N+Sg+Nom:i   PxK ; 
 +N+Nom:i R ;
 +N+Sg+Gen:i^WG%>n R ;
 +N+Pl+Par:i%>^A PxK ; !malmia, äitiä 

LEXICON n5_takki
!! **LEXICON @LEXNAME@** same as n5_ie except Pl+Part: takki>takkeja 
 +N: x5_i ;
 +N+Sg+Nom:i   PxK ; 
 +N+Nom:i R ;
 +N+Sg+Gen:i^WG%>n R ;
 +N+Pl+Par:ej%>^A PxK ; !takkeja

LEXICON x5_i
!! **LEXICON @LEXNAME@** cont lexica for type n1-words ending with -i
:i x1_sg ;
+Pl+Nom:i^WG%> t_PxK ;
+Pl+Gen:i%>e n_PxK ;
+Pl+Gen:i^WG%>tte n_PxK ;
+Pl+Ill:e^HMETA%>h^Vi n_PxK ;  ! saunhoin 
+Pl+Ill:e^HMET2%>h^Vi n_PxK ;  ! sauhnoin 
+Pl+Ine:e^WG%>iss^A PxK ;
+Pl+Ela:e^WG%>ist^A PxK ;
+Pl+All:e^WG%>ile PxxK ;
+Pl+Ade:e^WG%>il^A PxK ;
+Pl+Abl:e^WG%>ilt^A PxK ;
+Pl+Tra:e^WG%>i TRA ; 
+Pl+Ess:e%>in^A PxK ; !malmina, äitinä
+Pl+Abe:e^WG%>itt^A PxK ;


LEXICON n5_ie_odd
!! **LEXICON @LEXNAME@**
+N+Sg+Nom:^E2I R ;
+N+Sg+Nom:^E2I PxK ;
+N+Sg+Gen:^E2I%> n_PxK ; !
+N+Sg+Gen:^E2I%>n     R ; ! 
+N+Sg+Par:^E2I%>^A      PxK ; ! alttaria
+N+Sg+Ill:^HMETA%>hii n_PxK ;
+N+Sg+Ill:^HMET2%>hii n_PxK ;
+N+Sg+Ine:^E2I%>ss^A PxK ;
+N+Sg+Ela:^E2I%>st^A PxK ;
+N+Sg+All:^E2I%>lle PxxK ;
+N+Sg+Ade:^E2I%>ll^A PxK ;
+N+Sg+Abl:^E2I%>lt^A PxK ;
+N+Sg+Ess:^E2I%>nn^A PxK ;
+N+Sg+Tra:^E2I TRA ;
+N+Sg+Abe:^E2I%>tt^A PxK;

+N+Pl+Nom:^E2I%> t_PxK ;
+N+Pl+Gen:%>itte n_PxK ;
+N+Pl+Gen:^E2I%>tten PxK ;
+N+Pl+Par:%>it^A PxK ;
+N+Pl+Ill:^HMETA%>hii n_PxK ;
+N+Pl+Ill:^HMET2%>hii n_PxK ;
+N+Pl+Ine:%>iss^A PxK ; !NY ändr neråt
+N+Pl+Ela:%>ist^A PxK ;
+N+Pl+All:%>ile PxxK ;
+N+Pl+Ade:%>il^A PxK ;
+N+Pl+Abl:%>ilt^A PxK ;
+N+Pl+Ess:%>in^A PxK ;
+N+Pl+Tra:%>i TRA ;
+N+Pl+Abe:%>itt^A PxK;


!! #### The nainen (nen) and hevonen (3nen) family

LEXICON nen
!! **LEXICON @LEXNAME@** bisyllabic nainen stem nai
nen_sg ;
nen_pl ;

LEXICON nen_sg
!! **LEXICON @LEXNAME@**
+N: xnen_sg ;
!+N+Sg+Nom:s PxK ; #?
!+N+Sg+Gen:s PxK ; #?
+N+Sg+Gen:s R ;

LEXICON nen_pl
!! **LEXICON @LEXNAME@**
+N: xnen_pl ;

LEXICON xnen
!! **LEXICON @LEXNAME@**
xnen_sg ;
xnen_pl ;

LEXICON xnen_sg
!! **LEXICON @LEXNAME@**
+Sg:se 2cases ; !!≈ @CODE@ for  Ade, All, Ess lla, lle, nna
xnen_common_sg ;

LEXICON xnen_pl
!! **LEXICON @LEXNAME@**
+Pl:se%>i2 2cases ;
 xnen_common_pl ; ! all other cases

LEXICON 3nen
!! **LEXICON @LEXNAME@** odd-syllabic hevonen stem hevose
+N: x3nen ;
+N+Sg+Gen:s R ;

LEXICON x3nen
!! **LEXICON @LEXNAME@**
x3nen_sg ;
x3nen_pl ;

LEXICON x3nen_sg
!! **LEXICON @LEXNAME@**
+Sg:se 3cases ; ! Ade, All, Ess lla, lle, nna
xnen_common_sg ;

LEXICON x3nen_pl
!! **LEXICON @LEXNAME@**
+Pl:se%>i2 3cases ;
 xnen_common_pl ; ! all other cases

LEXICON xnen_common_sg
!! **LEXICON @LEXNAME@**
 +Sg+Nom:nen       PxK ;
 +Sg+Gen:se^WG%> n_PxK ;
 +Sg+Par:s%>t^A PxK ;
 +Sg+Ill:shee n_PxK ;    ! liikheen, säkheen
 +Sg+Ine:se%>ss^A PxK ;
 +Sg+Ela:se%>st^A PxK ;
 +Sg+Abl:se%>lt^A PxK ;
 +Sg+Abe:se%>tt^A PxK ;
 +Sg+Tra:se TRA ;

LEXICON xnen_common_pl
!! **LEXICON @LEXNAME@**
 +Pl+Nom:se^WG%> t_PxK ;
 +Pl+Gen:s%>itte n_PxK ;
 +Pl+Gen+Err/Orth:s%>te n_PxK ;
 +Pl+Par:s%>i^A PxK ;
 +Pl+Ill:shii n_PxK ;    !
 +Pl+Ine:s^WG%>i2ss^A PxK ;
 +Pl+Ela:s%>i2st^A PxK ;
 +Pl+Abl:s%>i2lt^A PxK ;
 +Pl+Abe:s%>i2tt^A PxK ;

LEXICON 3cases
!! **LEXICON @LEXNAME@**
+Ade:%>ll^A PxK ;
+All:%>lle PxxK ;
+Ess:%>nn^A PxK ;
+Ess:%>n^A PxK ;
!+Ess+Err/Orth:%>n^A PxK ;

LEXICON 2cases
!! **LEXICON @LEXNAME@**
+Ade:%>l^A PxK ;
+All:%>le PxxK ;
+Ess:%>n^A PxK ;
+Ess:%>nn^A PxK ;

LEXICON 3n_ks
!! **LEXICON @LEXNAME@**
+N: xn_ks ;
+N+Sg+Nom:^WGs R ;
+N+Sg+Gen:^WGkse%>n R ;

LEXICON 3n_ks_pl
!! **LEXICON @LEXNAME@**
+N: xn_ks_pl ;

LEXICON xn_ks
!! **LEXICON @LEXNAME@**
xn_ks_sg ;
xn_ks_pl ;

LEXICON xn_ks_sg
!! **LEXICON @LEXNAME@**
 +Sg+Nom:s       PxK ; 
 +Sg+Gen:kse%> n_PxK ;
 +Sg+Par:s%>t^A PxK ;
 +Sg+Ill:kse^HMETA%>h^V^V n_PxK ;
 +Sg+Ine:kse%>ss^A PxK ;
 +Sg+Ela:kse%>st^A PxK ;
 +Sg+All:kse%>lle PxxK ;
 +Sg+Ade:kse%>ll^A PxK ;
 +Sg+Abl:kse%>lt^A PxK ;
 +Sg+Ess:kse%>n^A PxK ;
 +Sg+Abe:kse%>tt^A PxK ;
 +Sg+Tra:kse TRA ;


LEXICON xn_ks_pl
!! **LEXICON @LEXNAME@**
 +Pl+Nom:kse%> t_PxK ;
 +Pl+Gen:s%>te n_PxK ;
 +Pl+Par:kse%>i2^A PxK ;
 +Pl+Ine:ks%>iss^A PxK ;
 +Pl+Ill:kse^HMETA%>hii n_PxK ;
 +Pl+Ela:ks%>ist^A PxK ;
 +Pl+All:ks%>i2lle PxxK ;
 +Pl+Ade:ks%>ill^A PxK ;
 +Pl+Abl:ks%>i2lt^A PxK ;
 +Pl+Ess:ks%>in^A PxK ;
 +Pl+Abe:ks%>i2tt^A PxK ;
 +Pl+Tra:ks%>i2 TRA ;


LEXICON n_äes !!identical to n_ks except N+Sg+Nom 
!! **LEXICON @LEXNAME@**
+N: x_äes ;
+N+Sg+Nom:^WGs R ;
+N+Sg+Gen:^WGkse%>n R ;

LEXICON x_äes !!xn_ks except N+Sg+Nom 
!! **LEXICON @LEXNAME@**
 +Sg+Nom:^WGs       PxK ; 
 +Sg+Gen:kse%> n_PxK ;
 +Sg+Par:s%>t^A PxK ;
 +Sg+Ill:kse^HMETA%>h^V^V n_PxK ;
 +Sg+Ine:kse%>ss^A PxK ;
 +Sg+Ela:kse%>st^A PxK ;
 +Sg+All:kse%>lle PxxK ;
 +Sg+Ade:kse%>ll^A PxK ;
 +Sg+Abl:kse%>lt^A PxK ;
 +Sg+Ess:kse%>n^A PxK ;
 +Sg+Abe:kse%>tt^A PxK ;
 +Sg+Tra:kse TRA ;

 +Pl+Nom:kse%> t_PxK ;
 +Pl+Gen:s%>te n_PxK ;
 +Pl+Par:kse%>i2^A PxK ;
 +Pl+Ine:ks%>iss^A PxK ;
 +Pl+Ill:kse^HMETA%>hii n_PxK ;
 +Pl+Ela:ks%>ist^A PxK ;
 +Pl+All:ks%>i2lle PxxK ;
 +Pl+Ade:ks%>ill^A PxK ;
 +Pl+Abl:ks%>i2lt^A PxK ;
 +Pl+Ess:ks%>in^A PxK ;
 +Pl+Abe:ks%>i2tt^A PxK ;
 +Pl+Tra:ks%>i2 TRA ;


LEXICON 3n_ue
!! **LEXICON @LEXNAME@**
3x_ue ;
+N+Sg+Nom:s R ;
+N+Sg+Nom:de%>n R ;

LEXICON 3x_ue
!! **LEXICON @LEXNAME@**
+N: 3x_ue_sg ;
+N: 3x_ue_pl ;

LEXICON 3x_ue_sg
!! **LEXICON @LEXNAME@**
 +Sg+Nom:s PxK ;
 +Sg+Gen:e%> n_PxK ;
 +Sg+Par:t%>t^A PxK ;
 +Sg+Ill:t%>hee n_PxK ;
 +Sg+Ine:e%>ss^A PxK ;
 +Sg+Ela:e%>st^A PxK ;
 +Sg+All:e%>lle PxxK ;
 +Sg+Ade:e%>ll^A PxK ;
 +Sg+Abl:e%>lt^A PxK ;
 +Sg+Abe:e%>tt^A PxK ;
 +Sg+Ess:te%>n^A PxK ;
 +Sg+Tra:e TRA ;

LEXICON 3x_ue_pl
!! **LEXICON @LEXNAME@**
 +Pl+Nom:e%> t_PxK ;
 +Pl+Gen:ks%>i2e n_PxK ;
 +Pl+Par:ks%>i2^A PxK ;
 +Pl+Ill:ks%>hii n_PxK ;
 +Pl+Ine:ks%>i2ss^A PxK ;
 +Pl+Ela:ks%>i2st^A PxK ;
 +Pl+All:ks%>i2lle PxxK ;
 +Pl+Ade:ks%>i2ll^A PxK ;
 +Pl+Abl:ks%>i2lt^A PxK ;
 +Pl+Abe:ks%>i2tt^A PxK ;
 +Pl+Ess:ks%>i2n^A PxK ;
 +Pl+Tra:ksi TRA ;

LEXICON 3n_ime
!! **LEXICON @LEXNAME@**
 +N+Sg+Nom:^WGn       R ;
 +N+Sg+Gen:me%>n R ;
  3n_ime_sg ;
  3n_ime_pl ;

LEXICON 3n_ime_sg 
!! **LEXICON @LEXNAME@**
+N: x_ime_sg ;

LEXICON 3n_ime_pl 
!! **LEXICON @LEXNAME@**
+N: x_ime_pl ;

LEXICON x_ime_sg
!! **LEXICON @LEXNAME@**
 +Sg+Nom:^WG n_PxK ;
 +Sg+Gen:me%> n_PxK ;
 +Sg+Par:^WGn%>t^A PxK ;
 +Sg+Ill:m%>hee n_PxK ; !kirjaimheen
 +Sg+Ine:me%>ss^A PxK ;
 +Sg+Ela:me%>st^A PxK ;
 +Sg+All:me%>lle PxxK ;
 +Sg+Ade:me%>ll^A PxK ;
 +Sg+Abl:me%>ll^A PxK ;
 +Sg+Abe:me%>tt^A PxK ;
 +Sg+Ess:me%>n^A PxK ;
 +Sg+Tra:me TRA ;

LEXICON x_ime_pl
!! **LEXICON @LEXNAME@**
 +Pl+Nom:me%> t_PxK ;
 +Pl+Gen:n%>te n_PxK ;
 +Pl+Par:m%>i^A PxK ;
 +Pl+Ill:m%>hii n_PxK ;
 +Pl+Ine:m%>i2ss^A PxK ;
 +Pl+Ela:m%>i2st^A PxK ;
 +Pl+All:m%>i2lle PxxK ;
 +Pl+Ade:m%>i2ll^A PxK ;
 +Pl+Abl:m%>i2lt^A PxK ;
 +Pl+Abe:m%>i2tt^A PxK ;
 +Pl+Ess:m%>i2n^A PxK ;
 +Pl+Tra:mi2 TRA ;

LEXICON nas ! tehas
!! **LEXICON @LEXNAME@**
 +N: xnas ;
 +N+Sg+Nom:^WGs R ;
 +N+Sg+Gen:%>^Vn R ;

LEXICON xnas
!! **LEXICON @LEXNAME@**
 xnas_sg ;
 xnas_pl ;

LEXICON xnas_sg ! ankerias:ankeria
!! **LEXICON @LEXNAME@**
 +Sg+Nom:^WGs       PxK ;
 +Sg+Gen:^HMETA%>h^V^V n_PxK ; !salmhaan
 +Sg+Gen:^HMET2%>h^V^V n_PxK ; !salhmaan
 +Sg+Par:^WGs%>t^A PxK ;
 +Sg+Ill:^HMETA%>h^V^Vshee n_PxK ;
 +Sg+Ill:^HMET2%>h^V^Vshee n_PxK ;
 +Sg+Ine:^HMETA%>h^V^Vss^A PxK ;
 +Sg+Ine:^HMET2%>h^V^Vss^A PxK ;
 +Sg+Ela:^HMETA%>h^V^Vst^A PxK ;
 +Sg+Ela:^HMET2%>h^V^Vst^A PxK ;
 +Sg+All:^HMETA%>h^V^Vle PxxK ;
 +Sg+All:^HMET2%>h^V^Vle PxxK ; !maitosalhmaala
 +Sg+Ade:^HMETA%>h^V^Vl^A PxK ; !maitosalmhaala
 +Sg+Ade:^HMET2%>h^V^Vl^A PxK ;
 +Sg+Abl:^HMETA%>h^V^Vlt^A PxK ;
 +Sg+Abl:^HMET2%>h^V^Vlt^A PxK ;
 +Sg+Abe:^HMETA%>h^V^Vtt^A PxK ;
 +Sg+Abe:^HMET2%>h^V^Vtt^A PxK ;
 +Sg+Ess:^HMETA%>h^V^Vn^A PxK ;
 +Sg+Ess:^HMET2%>h^V^Vn^A PxK ;
 +Sg+Tra:^HMETA%>h^V^V TRA ;
 +Sg+Tra:^HMET2%>h^V^V TRA ;

LEXICON xnas_pl
!! **LEXICON @LEXNAME@**
 +Pl+Nom:%>^V t_PxK ;
 +Pl+Gen:^HMETA%>h^Vitte n_PxK ; !maitosalmhoitten !TEST
 +Pl+Gen:^HMET2%>h^Vitte n_PxK ; !maitosalhmoitten
 +Pl+Par:^HMETA%>h^Vit^A PxK ;
 +Pl+Par:^HMET2%>h^Vit^A PxK ;
 +Pl+Ill:^HMETA%>h^Vi n_PxK ;
 +Pl+Ill:^HMET2%>h^Vi n_PxK ;
 +Pl+Ine:^HMETA%>h^Viss^A PxK ;
 +Pl+Ine:^HMET2%>h^Viss^A PxK ;
 +Pl+Ela:^HMETA%>h^Vist^A PxK ;
 +Pl+Ela:^HMET2%>h^Vist^A PxK ;
 +Pl+All:^HMETA%>h^Vile PxxK ;
 +Pl+All:^HMET2%>h^Vile PxxK ;
 +Pl+Ade:^HMETA%>h^Vil^A PxK ;
 +Pl+Ade:^HMET2%>h^Vil^A PxK ;
 +Pl+Abl:^HMETA%>h^Vilt^A PxK ;
 +Pl+Abl:^HMET2%>h^Vilt^A PxK ;
 +Pl+Ess:^HMETA%>h^Vin^A PxK ;
 +Pl+Ess:^HMET2%>h^Vin^A PxK ;
 +Pl+Tra:^HMETA%>h^Vi TRA ;
 +Pl+Tra:^HMET2%>h^Vi TRA ;
 +Pl+Abe:^HMETA%>h^Vitt^A PxK ;
 +Pl+Abe:^HMET2%>h^Vitt^A PxK ;

 LEXICON nas_h ! saappas !CHECK ELINA ser nas rätt ut? Fler såna här?
!! **LEXICON @LEXNAME@**
 nas_h_sg ;
 nas_h_pl ;
 
 LEXICON nas_h_sg ! saaphaat !CHECK ELINA ser nas rätt ut? Fler såna här?
!! **LEXICON @LEXNAME@**
 +N: xnas_sg ;
 +N+Sg+Nom:^WGs R ;
 +N+Sg+Gen:%>^Vn R ;

LEXICON nas_h_pl ! saaphaat !CHECK ELINA ser nas rätt ut? Fler såna här?
!! **LEXICON @LEXNAME@**
 +N: xnas_pl ;
 +N+Pl+Nom:^HMETA%>h^V^V t_PxK ;
 


LEXICON 3mies
!! **LEXICON @LEXNAME@**
 +N+Sg+Nom:s       PxK ;
 +N+Sg+Gen:he%> n_PxK ;
 +N+Sg+Par:s%>t^A PxK ;
 +N+Sg+Ill:hhe%>e n_PxK ;
 +N+Sg+Ine:he%>ss^A PxK ;
 +N+Sg+Ela:he%>st^A PxK ;
 +N+Sg+All:he%>le PxxK ;
 +N+Sg+Ade:he%>l^A PxK ;
 +N+Sg+Abl:he%>lt^A PxK ;
 +N+Sg+Abe:he%>tt^A PxK ;
 +N+Sg+Ess:he%>n^A PxK ;
 +N+Sg+Tra:he TRA ;

 +N+Pl+Nom:he%> t_PxK ;
 +N+Pl+Gen:h%>ie n_PxK ;
 +N+Pl+Par:h%>i^A PxK ;
 +N+Pl+Ill:hhii n_PxK ;
 +N+Pl+Ine:he%>i2ss^A PxK ;
 +N+Pl+Ela:he%>i2st^A PxK ;
 +N+Pl+All:he%>i2le PxxK ;
 +N+Pl+Ade:he%>i2l^A PxK ;
 +N+Pl+Abl:he%>i2lt^A PxK ;
 +N+Pl+Abe:he%>i2tt^A PxK ;
 +N+Pl+Ess:he%>i2n^A PxK ;
 +N+Pl+Tra:he TRA ;

LEXICON n_ien
!! **LEXICON @LEXNAME@**
 n_ien_sg ;
 n_ien_pl ;

LEXICON n_ien_sg
!! **LEXICON @LEXNAME@**
 +N+Sg+Nom:^WGn       PxK ;
 +N+Sg+Gen:ne%> n_PxK ;
 +N+Sg+Par:^WGn%>t^A PxK ;
 +N+Sg+Ill:n^HMETA%>hee n_PxK ;
 +N+Sg+Ill:hnee n_PxK ;
 +N+Sg+Ine:ne%>ss^A PxK ;
 +N+Sg+Ela:ne%>st^A PxK ;
 +N+Sg+All:ne%>lle PxxK ;
 +N+Sg+Ade:ne%>ll^A PxK ;
 +N+Sg+Abl:ne%>lt^A PxK ;
 +N+Sg+Abe:ne%>tt^A PxK ;
 +N+Sg+Ess:ne%>nn^A PxK ;
 +N+Sg+Tra:he TRA ;

LEXICON n_ien_pl !FIX!!
 +N+Pl+Nom:ne%> t_PxK ;
 +N+Pl+Nom:n^HMETA%>hee t_PxK ;
 +N+Pl+Nom:h%>nee t_PxK ;
 +N+Pl+Gen:n%>ie n_PxK ;
 +N+Pl+Par:n%>i^A PxK ;
 +N+Pl+Ill:n^HMETA%>hii n_PxK ;
 +N+Pl+Ill:hnii n_PxK ;
 +N+Pl+Ine:n%>hei2ss^A PxK ;
 +N+Pl+Ine:h%>nei2ss^A PxK ;
 +N+Pl+Ela:n%>hei2st^A PxK ;
 +N+Pl+Ela:h%>nei2st^A PxK ;
 +N+Pl+All:n%>hei2le PxxK ;
 +N+Pl+All:h%>nei2le PxxK ;
 +N+Pl+Ade:n%>hei2l^A PxK ;
 +N+Pl+Ade:h%>nei2l^A PxK ;
 +N+Pl+Abl:n%>hei2lt^A PxK ;
 +N+Pl+Abl:h%>nei2lt^A PxK ;
 +N+Pl+Abe:n%>hei2tt^A PxK ;
 +N+Pl+Abe:h%>nei2tt^A PxK ;
 +N+Pl+Ess:n%>hei2nn^A PxK ;
 +N+Pl+Ess:h%>nei2nn^A PxK ;
 +N+Pl+Tra:hei2 TRA ;


LEXICON n_uus  !vajavuus
!! **LEXICON @LEXNAME@**
 +N+Sg+Nom:s     R ;
 +N+Sg+Gen:e%>n R ;
 +N+Sg+Nom:s     PxK ;
 +N+Sg+Gen:e%> n_PxK ;
 +N+Sg+Par:t%>t^A PxK ;
 +N+Sg+Ill:t^HMETA%>hee n_PxK ;
 +N+Sg+Ine:%>ess^A PxK ;
 +N+Sg+Ela:%>est^A PxK ;
 +N+Sg+All:%>ele PxxK ;
 +N+Sg+Ade:%>el^A PxK ;
 +N+Sg+Abl:%>elt^A PxK ;
 +N+Sg+Abe:%>ett^A PxK ;
 +N+Sg+Ess:t%>en^A PxK ;
 +N+Sg+Tra: TRA ;

 +N+Pl+Nom:%>e t_PxK ;
 +N+Pl+Gen:ks%>i2e n_PxK ;
 +N+Pl+Par:ks%>i2^A PxK ;
 +N+Pl+Ill:ks%>hii n_PxK ;
 +N+Pl+Ine:ks%>i2ss^A PxK ;
 +N+Pl+Ela:ks%>i2st^A PxK ;
 +N+Pl+All:ks%>i2le PxxK ;
 +N+Pl+Ade:ks%>i2l^A PxK ;
 +N+Pl+Abl:ks%>i2lt^A PxK ;
 +N+Pl+Abe:ks%>i2tt^A PxK ;
 +N+Pl+Tra:ks TRA ;
 +N+Pl+Ess:ks%>i2n^A PxK ;

LEXICON n_uus_odd ! for odd syllabel-stems: suuruue->(suuruuelle pga lång vokal men) suuruuksille 
!! **LEXICON @LEXNAME@**
 +N+Sg+Nom:s     R ;
 +N+Sg+Gen:e%>n R ;
 +N+Sg+Nom:s     PxK ;
 +N+Sg+Gen:e%> n_PxK ;
 +N+Sg+Par:t%>t^A PxK ;
 +N+Sg+Ill:t^HMETA%>hee n_PxK ;
 +N+Sg+Ine:%>ess^A PxK ;
 +N+Sg+Ela:%>est^A PxK ;
 +N+Sg+All:%>elle PxxK ;
 +N+Sg+Ade:%>ell^A PxK ;
 +N+Sg+Abl:%>elt^A PxK ;
 +N+Sg+Abe:%>ett^A PxK ;
 +N+Sg+Ess:t%>en^A PxK ;
 +N+Sg+Tra: TRA ;

 +N+Pl+Nom:%>e t_PxK ;
 +N+Pl+Gen:ks%>i2e n_PxK ;
 +N+Pl+Par:ks%>i2^A PxK ;
 +N+Pl+Ill:ks%>hii n_PxK ;
 +N+Pl+Ine:ks%>i2ss^A PxK ;
 +N+Pl+Ela:ks%>i2st^A PxK ;
 +N+Pl+All:ks%>i2lle PxxK ;
 +N+Pl+Ade:ks%>i2ll^A PxK ;
 +N+Pl+Abl:ks%>i2lt^A PxK ;
 +N+Pl+Abe:ks%>i2tt^A PxK ;
 +N+Pl+Tra: TRA ;
 +N+Pl+Ess:ks%>i2n^A PxK ;

 
!! ### 2-syllabic LNR final stems

LEXICON 3n_lnr 
!! **LEXICON @LEXNAME@** ahven - ahvenheen
+N+Sg+Gen:e%>n R ;
+N+Sg+Gen:e%> n_PxK ;
+N+Sg+Ill:%>hee n_PxK ;
+N+Sg+Ill:^HMET2%>hee n_PxK ;
+N+Sg+Ine:%>ess^A PxK ;
+N+Sg+Ess:%>en^A PxK ;
3n_lnr_inteill ; 

LEXICON 30n_lnr
!! **LEXICON @LEXNAME@** askel - askelheesheen
 +N+Sg+Gen:%>hee n_PxK ;
 +N+Sg+Gen:^HMET2%>hee n_PxK ;
 +N+Sg+Gen:e%> n_PxK ; !två genetivformer? askelen, askelheen
 !+N+Sg+Gen:^HMETA%>hee n_PxK ; askehleen? 
 +N+Sg+Ill:%>heeshee n_PxK ;
 +N+Sg+Ill:^HMET2%>heeshee n_PxK ;
 +N+Sg+Ine:%>heess^A PxK ;
 +N+Sg+Ine:^HMET2%>heess^A PxK ;
 +N+Sg+Ess:%>heen^A PxK ;
 +N+Sg+Ess:^HMET2%>heen^A PxK ;
 3n_lnr_inteill ; 

LEXICON TYÄR
+N+Sg+Nom:tyär K ; 
+N+Sg+Par:tytärtä K ;
+N+Sg+Par:tyärtä K ;
         :tyttär 3xn_lnr ;

LEXICON n_kasuven !!FIXA 
!! **LEXICON @LEXNAME@** 
+N+Sg+Nom:ve n_PxK ; 
+N+Sg+Ill:%>hvii n_PxK ;
+N+Sg+Ela:ven%>st^A PxK ;

+N+Pl+Ill:ve^HMETA%>hii n_PxK ;
+N+Pl+Ill:ve^HMET2%>hii n_PxK ;
+N+Pl+All:v%>i2lle PxK ;

LEXICON 3xn_lnr
!! **LEXICON @LEXNAME@** tyär, kort och lång Ill
 +N+Sg+Gen:e%>n R ;
 +N+Sg+Gen:e%> n_PxK ;
 +N+Sg+Ill+Use/NG:%>heeshee n_PxK ;
 +N+Sg+Ill+Use/NG:^HMET2%>heeshee n_PxK ;
 +N+Sg+Ill:%>hee n_PxK ;
 +N+Sg+Ill:^HMET2%>hee n_PxK ;
 +N+Sg+Ine:%>ess^A PxK ;
 +N+Sg+Ess:%>en^A PxK ;
 3n_lnr_inteill ; 

LEXICON 3n_lnr_inteill 
!! **LEXICON @LEXNAME@** inte Ill, Ine, Ess men alla andra
+N+Sg+Nom:^WG  R ;
 +N+Sg+Nom:^WG PxK ;
 !+N+Sg+Gen:e%>n R ;
 !+N+Sg+Gen:e%>n PxK ;
 +N+Sg+Par:^WG%>t^A PxK ;
 +N+Sg+Ela:%>est^A PxK ;
 +N+Sg+All:%>elle PxxK ;
 +N+Sg+Ade:%>ell^A PxK ;
 +N+Sg+Abl:%>elt^A PxK ;
 +N+Sg+Abe:%>ett^A PxK ;
 +N+Sg+Tra:e TRA ;

 +N+Pl+Nom:e%> t_PxK ;
 +N+Pl+Gen:^WG%>te n_PxK ;
 +N+Pl+Par:e%>it^A PxK ;
 +N+Pl+Ill:%>hii n_PxK ;
 +N+Pl+Ill:^HMET2%>hii n_PxK ;
 +N+Pl+Ine:%>iss^A PxK ;
 +N+Pl+Ela:%>ist^A PxK ;
 +N+Pl+All:%>ille PxxK ;
 +N+Pl+Ade:%>ill^A PxK ;
 +N+Pl+Abl:%>ilt^A PxK ;
 +N+Pl+Abe:%>itt^A PxK ;
 +N+Pl+Tra:%>i TRA ;
 +N+Pl+Ess:%>in^A PxK ;



LEXICON 4n_ks
!! **LEXICON @LEXNAME@**
 +N: x4n_ks ;
 +N+Sg+Nom:s R ;
 +N+Sg+Gen:kse%>n R ;

LEXICON x4n_ks
!! **LEXICON @LEXNAME@**
 x4n_ks_sg ;
 x4n_ks_pl ;

LEXICON x4n_ks_sg
!! **LEXICON @LEXNAME@**
 +Sg+Nom:s       PxK ;
 +Sg+Gen:kse%> n_PxK ;
 +Sg+Par:s%>t^A PxK ;
 +Sg+Ill:kse^HMETA%>h^V^V n_PxK ;
 +Sg+Ine:kse%>ss^A PxK ;
 +Sg+Ela:kse%>st^A PxK ;
 +Sg+All:kse%>le PxxK ;
 +Sg+Ade:kse%>l^A PxK ;
 +Sg+Abl:kse%>lt^A PxK ;
 +Sg+Ess:kse%>n^A PxK ;
 +Sg+Abe:kse%>tt^A PxK ;
 +Sg+Tra:kse TRA ;


LEXICON x4n_ks_pl
!! **LEXICON @LEXNAME@**
 +Pl+Nom:kse%> t_PxK ;
 +Pl+Gen:s%>te n_PxK ;
 +Pl+Par:kse%>i2^A PxK ;
 +Pl+Ine:kse%>i2ss^A PxK ;
 +Pl+Ill:ks%>hii n_PxK ;
 +Pl+Ela:kse%>i2st^A PxK ;
 +Pl+All:kse%>i2le PxxK ;
 +Pl+Ade:kse%>i2l^A PxK ;
 +Pl+Abl:kse%>i2lt^A PxK ;
 +Pl+Ess:kse%>i2n^A PxK ;
 +Pl+Abe:kse%>i2tt^A PxK ;
 +Pl+Tra:ksi TRA ;





!! ### Sublexica for cases

LEXICON TRA
!! **LEXICON @LEXNAME@**
+Err/Orth:%>ks K ; ! VetEj
 :%>ks i_PxK ;

!! ### Sublexica for possessive suffixes
! ---------------------------------

!! Px is now not in use, with one exception, comitative.

LEXICON n_PxK  !!≈ @CODE@ has either -n or goes to Px
!! **LEXICON @LEXNAME@**
  :n K  ;
     Px ;

LEXICON t_PxK  !!≈ @CODE@ has either -t or goes to Px
!! **LEXICON @LEXNAME@**
  :t K  ;
     Px ;

LEXICON i_PxK  !!≈ @CODE@ Tra: -i or -e and goes to Px
!! **LEXICON @LEXNAME@**
  :i K  ;
  :e Px ;

LEXICON PxK !!≈ @CODE@ has only -nsA, compare PxxK
!! **LEXICON @LEXNAME@**
  K  ;
  Px ;

LEXICON PxxK  !!≈ @CODE@ has also -Vn, thus both .. llensa and ..lleen.
!! **LEXICON @LEXNAME@**
  KK    ; ! All, so KK not K
  Px    ;
  Px-Vn ;

!LEXICON Pxx ! nsA and Vn
!  Px    ;
!  Px-Vn ;

LEXICON Px
!! **LEXICON @LEXNAME@**
 +PxSg1:%>ni    K ;
 +PxSg2:%>sti   K ;
 +PxSg3:%>ns%^A K ; !ns%^A varför %?
 +PxPl1:%>mm^A  K ; ! NB! -m^A ei hoidettu
 +PxPl2:%>nn^A  K ; ! NB! -n^A ei hoidettu
 +PxPl3:%>ns%^A K ; !ns%^A 


LEXICON Px-Vn
!! **LEXICON @LEXNAME@**
 +PxSg3:%>%^Vn K ;
 +PxPl3:%>%^Vn K ;

!! HÖGST temporärt!!! GE + LL 230208
LEXICON n1_troppi
!! **LEXICON @LEXNAME@**
+N+Sg+Nom:       PxK ; !! troppi
+N+Nom: R ;
+N+Sg+Gen:^WG%>n R ;
+N+Sg+Gen:^WG%>n PxK ; !! tropin
+N+Sg+Par:%>^A PxK ; !! troppia?
+N+Sg+Ill:^HMETA%>h^V^V n_PxK ; ! trophiin
+N+Sg+Ill:^HMET2%>h^V^V n_PxK ; ! trophiin
+N+Sg+Ine:^WG%>ss^A PxK ;       ! tropissa
+N+Sg+Ela:^WG%>st^A PxK ;       ! tropista
+N+Sg+All:^WG%>le PxxK ;        ! tropile
+N+Sg+Ade:^WG%>l^A PxK ;        ! tropila
+N+Sg+Abl:^WG%>lt^A PxK ;       ! tropilta
+N+Sg+Tra:^WG TRA ;             ! tropiksi
+N+Sg+Ess:%>n^A PxK ;           ! troppina
+N+Sg+Abe:^WG%>tt^A PxK ;       ! tropitta

+N+Pl+Nom:^WG%> t_PxK ;
+N+Pl+Gen:^WG^IDEL%>i2tten PxK ; ! nu: troppitten rätt: tropitten
+N+Pl+Gen:^IDEL%>i2en PxK ;
+N+Pl+Par:%>^A PxK ; ! nu: --- rätt (kanske samma som Sg?) :	troppia?
+N+Pl+Ill:^HMETA%>h^Vi n_PxK ;
+N+Pl+Ill:^HMET2%>h^Vi n_PxK ;  
+N+Pl+Ine:^WG%>ss^A PxK ;
+N+Pl+Ela:^WG%>st^A PxK ;
+N+Pl+All:^WG%>le PxxK ;
+N+Pl+Ade:^WG%>l^A PxK ;
+N+Pl+Abl:^WG%>lt^A PxK ;
+N+Pl+Tra:^WG%> TRA ;
+N+Pl+Ess:%>n^A PxK ;     ! nu rätt: troppina
+N+Pl+Abe:^WG%>tt^A PxK ;
 
LEXICON n1_troppi_odd
!! **LEXICON @LEXNAME@**
+N+Sg+Nom:       PxK ; !! 
+N+Nom: R ;
+N+Sg+Gen:^WG%>n R ;
+N+Sg+Gen:^WG%>n PxK ; !! 
+N+Sg+Par:%>^A PxK ; !! 
+N+Sg+Ill:^HMETA%>h^V^V n_PxK ; ! 
+N+Sg+Ill:^HMET2%>h^V^V n_PxK ; ! 
+N+Sg+Ine:^WG%>ss^A PxK ;       ! 
+N+Sg+Ela:^WG%>st^A PxK ;       ! 
+N+Sg+All:^WG%>lle PxxK ;        ! sapattille
+N+Sg+Ade:^WG%>ll^A PxK ;        ! 
+N+Sg+Abl:^WG%>lt^A PxK ;       ! 
+N+Sg+Tra:^WG TRA ;             ! 
+N+Sg+Ess:%>nn^A PxK ;           ! 
+N+Sg+Abe:^WG%>tt^A PxK ;       ! 

+N+Pl+Nom:^WG%> t_PxK ;
+N+Pl+Gen:^WG^IDEL%>i2tten PxK ; !
+N+Pl+Gen+Err/Orth:^IDEL%>i2en PxK ;
+N+Pl+Par:%>^A PxK ; ! 
+N+Pl+Ill:^HMETA%>h^Vi n_PxK ;
+N+Pl+Ill:^HMET2%>h^Vi n_PxK ;  
+N+Pl+Ine:^WG%>ss^A PxK ;
+N+Pl+Ela:^WG%>st^A PxK ;
+N+Pl+All:^WG%>lle PxxK ;
+N+Pl+Ade:^WG%>ll^A PxK ;
+N+Pl+Abl:^WG%>lt^A PxK ;
+N+Pl+Tra:^WG%> TRA ;
+N+Pl+Ess:%>nn^A PxK ;     !
+N+Pl+Abe:^WG%>tt^A PxK ;