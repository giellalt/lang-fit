!! # Meänkieli twolc file

!! This file documents the [Meänkieli twolc file](http://github.com/giellalt/lang-fit/blob/main/src/fst/phonology.twolc) (the file governing gradation, gemination, vowel harmony and other morphophonological processes).

!! The first part of the file contains definitions, the second part contains rules.

!! # Declaring the alphabet, sets and definitions

!! ## Alphabet
!! This defines all symbols (letters, archiphonemes, triggers) to be used.

Alphabet


a b c d e f g h i j k l m n o p q r s t u v w x y z å ä ö æ ø  !!= * @CODE@ = the letters
%^WG:0    !!= * @CODE@ = weak grade
%^E2I:0   !!= * @CODE@ = kiele- > kieli
%^HMETA:0 !!= * @CODE@ = vow metathesis for ill (sauna+haan > saunhaan)
%^HMET2:0 !!= * @CODE@ = vow metathesis for sauhnaan etc (sauna+haan > sauhnaan)
%^AE:0    !!= * @CODE@ = a to e in otta- > otethaan
%^IDEL:0  !!= * @CODE@ = trigger to delete i 
%>        !!= * @CODE@ = suffix boundary
h2:h      !!= * @CODE@ = clitic hAAn 
i2:i      !!= * @CODE@ = plural of nouns
i3:i      !!= * @CODE@ = past tense of verbs
i4:i      !!= * @CODE@ = i in conditional of isi
i5:i      !!= * @CODE@ = superlative i of adjectives
i9:i      !!= * @CODE@ = plural-i of adjectives (kamalissa)
i8:i      !!= * @CODE@ = past tense of verbs that drops in past antaa : anto
p2:p t2:t k2:k    !!= * @CODE@ = always p t k !mettä > mettän etc
t3:t  	  !!≈ * **@CODE@** = t participating in gradation, but not in t:s !t3 not in use?
k4:k      !!≈ * **@CODE@** = k to j in certain words
t4:t      !!= * @CODE@  goes to 0 (ie rt goes to r) in imartelee : imarella
n9:n      !!= * @CODE@ nouns that keep nn; juvanne
%^A2O:0    !!= * @CODE@ = päärynä > päärynöitä 
%^A:ä %^O:ö %^U:y !!= * @CODE@ vowel harmony archiphonemes
%^V:a   %^V:e   %^V:o   %^V:u   %^V:y   %^V:i   %^V:ä   %^V:ö !!= * @CODE@ = for vowel lenghtening
%^N:n !!= * @CODE@ for  participle -nut
'     !!= * @CODE@ 0027 aphstrophe for saa'a
’     !!= * @CODE@ 2019 right single quot mark for saa’a
ˊ     !!= * @CODE@ 02ca letter prime for saaˊa
;

!! ## Sets
!! Here we group the symbols in convenient sets.

Sets

Dummy          = %+ %^WG %^E2I %^HMETA %^HMET2 %^VDEL %^EDEL %^AE %^AO %^¤ %^IDEL %^A2O ; !!= * @CODE@
DummyBorder    = Dummy %> ;                                          !!= * @CODE@
ArchiVowel     = %^A %^O %^U ;                                       !!= * @CODE@ 
SomeVowel      = %^V ;                                               !!= * @CODE@ 
NeutralVowel   = e i               i2 i3 i4 i5 i9 i8 E I ;           !!= * @CODE@ 
FrontVowel     = e i y ä ö         ü  æ  ø     E I Y Ä Ö    Ü Æ Ø  ; !!= * @CODE@ 
BackVowel      =           a o u å                       A O U Å   ; !!= * @CODE@ 
RndVow   =     y   ö   o u å                 Y   Ö   O U Å   ; !!= * @CODE@ 
UnRndVow = e i   ä   a                   E I   Ä   A         ; !!= * @CODE@ 
VowelNotUY     = e i   ä ö a o   å ü  æ  ø     E I   Ä Ö A O Å Ü Æ Ø ; !!= * @CODE@ 
VowNotI      = e   y ä ö a o u å ü  æ  ø     E   Y Ä Ö A O U Å Ü Æ Ø ; !!= * @CODE@ 
VowNotIU     = e   y ä ö a o   å ü  æ  ø     E   Y Ä Ö A O Å Ü Æ Ø ; !!= * @CODE@ 
VowelNotEI     =     y ä ö a o u å ü  æ  ø         Y Ä Ö A O U Ü Ä Ö ; !!= * @CODE@ 
Vow            = FrontVowel BackVowel ArchiVowel SomeVowel ;         !!= * @CODE@ 
ArchiCns       = %^N ;                                               !!= * @CODE@
LNRM           = l n r m ;                                           !!= * @CODE@
SurfaceCns     = b c d đ f g h j k l m n p q r s š t v w x z ;       !!= * @CODE@
Cns            = SurfaceCns ArchiCns p2 t2 t3 t4 k2 k4 ’ ;                !!= * @CODE@ testing with ’ 2019 for saa’a.
Segment        = Vowel Cns ;                                         !!= * @CODE@
NonFront       = BackVowel ArchiVowel ArchiCns SomeVowel Cns NeutralVowel Dummy ;  !!= * @CODE@ 
NonLNR         = b c d đ f g h j k m p q s š t v w x z ;       !!= * @CODE@


!! ## Definitions
!! This defines strings used often in rules.

Definitions

WeakGrade = ([l|n|r]) (%^AE:) %^WG: ; !! `WeakGrade = ([l|n|r]) (%^AE:) %^WG:`


!! # Rules 
!! This chapter gives the rules themselves.

Rules 

!! ## Consonant rules


!! For the gradation rules, each consonant deletion or change is given its own rule. 
!! Thus, both *kk:k* and *k:0* are handled in the same **k:0* rule. This to avoid rule conflicts.
!! The  change rules (*k:g, k:j* etc.) are restricted by context (*k:g* only after *n*, etc.).


!! ### f rules

"f:0"
!! **RULE: @RULENAME@**
f:0 <=> Vow f _ Vow: [%^HMETA: |%^HMET2:] ;

!€ s o f f:0 a:0 ^HMETA:0 > h ^V:a ^V:a n


!! ### j rules
"j:0"
!! **RULE: @RULENAME@**
j:0 <=> Vow j _ Vow: [%^HMETA: |%^HMET2:] ;
            l _ e: %^E2I:0 ;


!! ### k rules

"k:g"
!! **RULE: @RULENAME@**
k:g <=> Vow n _ Vow: WeakGrade ;

!! **Tests:**

!€ k e n k:g ä ^WG:0 > n

"k:0"
!! **RULE: @RULENAME@**
k:0 <=> [Vow|l|n|r] k _ Vow:        WeakGrade ; ! Quant Grad
                 o i: _ Vow:        WeakGrade ; ! pojan
                 a i:  _ Vow:        WeakGrade ; ! vaieta
         VowelNotUY   _ Vow:        WeakGrade ; ! maun 
	    [u|y]     _ VowelNotUY: WeakGrade ; ! tuen
          Vow [k|k2:] _ e: (%^WG:)  h     %> e       ; ! kkh > kh ääkkä : ääkhään
          Vow (Cns) [k|k2:] _ Vow: (%^IDEL:) (%>) (Vow:) [%^HMETA: | %^HMET2:|h %> e] ; ! kkh > kh ääkkä : ääkhään, kirkko:kirkhoon
          Vow   [l|r] _ [VowelNotEI | a: %^AE:0]  WeakGrade ; !alkaa>aletaan
              Vow h k _ Vow         WeakGrade ;
           Vow  h _ Vow         WeakGrade ; !vehke>vehettä
!          [Vow - e] h _ Vow         WeakGrade ;
!               Vow h _ Vow         WeakGrade ; ! vehe men kehjetä !!

!! **Tests:**

!€ t a k k:0 i ^WG:0 > n
!€ j o h k:0 e ^WG:0
!€ ä ä k k:0 ä:0 ^HMETA:0 > h ^V:ä ^V:ä n
!€ v a i k:0 e ^WG:0 > t ^A:a

"k:j"
!! **RULE: @RULENAME@**
k:j <=> Vow [l|r] _ [:i|e]  WeakGrade ;   ! syljen 
        Vow [l|r] _ Vow:0  WeakGrade %> :i ; ! syljin
        ![#|.#.] r e i _ Vow: (%^WG:) %> ; !reikä, reijän FLER!
	! e h _ [e:|i:] ;

"k4:j"
!! **RULE: @RULENAME@**
k4:j <=> Vow _ Vow: %^WG: ;   ! reijän

!! **Tests:**
!€ s y l k:j e ^WG:0 > n
! k e h k:j e ^WG:0 > t ^A:a
! No rule for h k:j e ^WG. k3:j ?
!€ v e h k:0 e ^WG:0
 ! k:0?
 
"k:v"
!! **RULE: @RULENAME@**
k:v <=> [u|y] _ [y|u] WeakGrade ;

!! **Tests:**
!€ s u k:v u ^WG:0 > n


!! ### l rules
!! **RULE: @RULENAME@**
"l:0"
l:0 <=> Vow l _ Vow: [%^HMETA: |%^HMET2:| h %> e ] ; !No dubbel-cons before h
        :Vow (%^WG:) (%>) i2: (%>) l _ [%^A:|e] ; !ämmäkölle>ämmäköile, 
!       :Vow :Vow (%^WG:) %> l _ [%^A:|e] [.#.|#]; ! hopeale
        [#|.#.] (Cns) Vow+ Cns+ Vow+ _ t:l ; !uskaltaa >uskalan 
        Vow Vow _ t:l ; !huolto>huolon


!! ### m rules
"m:0"
!! **RULE: @RULENAME@**
m:0 <=> Vow m _ Vow: [%^HMETA: |%^HMET2:] ;
        [#|.#.] (Cns) Vow+ Cns+ Vow+ _ p:m ; !two syll + mm >m 
        Vow Vow _ p:m ; 





!! ### n rules
"n:0"
!! **RULE: @RULENAME@**
n:0 <=> Vow n _ Vow: [%^HMETA: |%^HMET2:] ;
        _ %> n s ; !Poss-suffix ahvensa etc
       [#|.#.] (Cns) Vow+ Cns+ Vow+ _ t:n ; !parantaa+V+Pass+Ind+Prs>paranethaan
       Vow Vow _ t:n ; !luonto>luonon



!! ### p rules

"p:0"
!! **RULE: @RULENAME@**
p:0 <=> [Vow|l|m|r] p _ Vow: WeakGrade ; ! tärppä : tärpän
                Vow p _ e: (%^WG:) h %> Vow ; !Ortografi pph>ph 
        [Vow|Cns] p _ Vow: [%^HMETA: | %^HMET2:|h %> e] ;  ! tärppä : tärphään 

!! **Tests:**

!€ a p p:0 i ^WG:0

"p:v"
!! **RULE: @RULENAME@**
p:v <=> [Vow|l]   _ Vow:  WeakGrade ;
        Vow r _ Vow WeakGrade ;

!! **Tests:**

"p:m"
!! **RULE: @RULENAME@**
p:m <=> Vow m _ Vow: WeakGrade ;


!! ### r rules
!! **RULE: @RULENAME@**
"r:0"
r:0 <=>                 Vow r _ Vow: [%^HMETA: |%^HMET2:] ;
! [#|.#.] (Cns) Vow+ Cns+ Vow+ _ t:r :Vow ; !ymmärtää>ymmärän
! [#|.#.] (Cns) Vow Vow _ t:r Vow %^WG:0 ; ! siirtyä > mie siiryn
!                     Vow Vow _ t:r :Vow WeakGrade ; !suorto>suoron

!€ y m m ä r t4:0 ä ^WG:0 > n

!! ### s rules
!! **RULE: @RULENAME@**
"s:0"
s:0 <=> Vow s _ Vow: [%^HMETA: |%^HMET2:] ;


!! ### t rules

"t:j"
!! **RULE: @RULENAME@**
t:j <=> [.#.|#:] Cns:* Vow:* i _ i     WeakGrade ;
        [.#.|#:] Cns:* Vow:* i _ [o|u] WeakGrade ;

"t4:0"
!! **RULE: @RULENAME@** where *t4* is *t* in *rt* that shall not become *rr* 
t4:0 <=> Vow:+ [l|n|r] _ [Vow: - ä:0] WeakGrade ;
         Vow   t       _ Vow:  [%^HMETA: |%^HMET2:] ;

"t4:s"
!! **RULE: @RULENAME@** where *t4* becomes *s* in *rt* that shall not become *rr* 
t4:s <=> Vow:+ [l|n|r] _ ä: (%^WG:0) %> [i3: | i8:] ; !preteritum ymmärtä>ymmärsi etc

!! **Tests:**
! i m a r t4:0 e l ^WG:0 > l ^A:a

! conflicts: t: 0/s, l/s, n/s, r/s, 

"t:0"
!! **RULE: @RULENAME@**
t:0 <=>  Vow ([l|n|r]) t _ Vow:+ [%^IDEL:0 %> e %^WG:0 |WeakGrade | (WeakGrade) %^HMET2: |%^HMETA:|h %> e] ; !
  [.#.|#] (Cns) (:Vow) i _ [a:|ä:|i2:] (%^AE:0) WeakGrade [%> |%> [:Vow - :i] | Cns: | .#.] ; ! pitä>piän, piä; haitata
          u              _ Vow       WeakGrade [%> :Vow - :i|(%>) Cns:] ; ! huutaa>huua, (kausi>)kaute>kauessa 
         [a|e|y|ä|ö] h   _ Vow:      WeakGrade [%> |%> [:Vow - :i]|Cns:] (%>) i3: ; ! för att få lähen, lähin 
                  i  h   _ e:      WeakGrade [%> :Vow - :i|Cns:] ;  ! sihata
           [VowNotIU | i e:0] (h)  _ Vow:      WeakGrade (%^IDEL:0) (%>) [%^E2I:0|i2:i|i4:i|Cns:|#|.#.] ; ! katu : kaun        tieto>tion?
         [ä|e]           _  i        WeakGrade [%> :Vow - :i|Cns:] ;
                       t _ Vow: %^HMETA: %> h [i i|%^V: %^V:|%^V: i] ; 
             Vow [t|t2:] _ e: (%^WG:)  h     %> Vow       ; ! tth > th
             Vow  (Cns)  _ [t|t2:] Vow: [%^HMETA: | %^HMET2:] ; !mettä>methään
         ![u|a] u        _   [a|e]   WeakGrade [%> :Vow - :i|Cns:] ; ! huutaa>huua, (kausi>)kaute>kauessa !ändrar till Vow för att få tauin


!! **Tests:**
!€ m o t t:0 i ^WG:0 > n
!€ t e h t:0 a ^WG:0 s
!€ p i t:0 ä ^WG:0 t
!€ t y t:0 ä r ^WG:0

"t:s"
!! **RULE: @RULENAME@**
t:s <=> [Vow - i] ([l|n|r]) _ e:0 (WeakGrade) %> [i2:|i3:] ;
        [Vow - i] ([l|n|r]) _ e: %^E2I: .#. ;
! Rest of rule from fkv:
              Vow   n       _        e:0                     %> i2: [.#.|%> Cns] ; ! fkv
              Vow ([n|l|r]) _ (:s)   e:   (%^WG:) %^E2I: ;  ! fkv: *) 
              Vow           _ (:s)   e:0                     %> i2: %> [Cns|%^V:|%^A:] ; ! **)
              Vow [n|l|r]   _        e:0 (%^WG:0)   %^E2I:   %> i2: ; ! hirsiksi, hirsi (pl: gen par) 
              Vow ([n|l|r]) _        e:0 (%^AO:0) [%^HMETA:0| %^HMET2:0] %> h :i ;    ! jok uushiin
          [i|u|y|ö] [e|u|y] _ (:s) Vow:  (%^AO:0) (%^WG:0) %> i3: ;  ! löytää:löysi (ei sousi)  
              Vow  n        _      Vow:0  [Dummy:* - %^WG:0] %> [i3:|i8:] ;! 13/11
              Vow [n|l|r]   _ (:s) Vow:0  (%^WG:0)  (%^AO:0) %> i3: ;
                          i _ k: 0: ä: %> i5:i  ; ! superlatiivi pitkä/pissiin
              ! [a] [i|h] _ (:s) Vow: (%^WG:0)   %> i3: ;  !vahtea>vahtesi, taitaa>taisi
! *) uusine, vesi: vete^E2I:vesi, vete^WG>n:vede0>n, vete^WG>i2>ll^A:vesillä
! **) [vuosiksi, etc.|vuossii(n)|uusia] ! uusine


!! **Tests:**
!€ h o i t:j o ^WG:0 > n


"t:l"  
!! **RULE: @RULENAME@** for lt:ll
t:l <=> Vow:+ l: (%>) _ :Vow WeakGrade ; 
        !Vow:+ l: (%>) _ :Vow WeakGrade [s|%> [\i2:i|\i3:i]|.#.] ; ! kulta : kullan

!! **Tests:**
!€ k u l t:l a ^WG:0 > n

"t:n" 
!! **RULE: @RULENAME@** for nt:nn 
t:n <=> (Cns:*) Vow:+  [n:|n9:n] (%>) _ :Vow  WeakGrade ; ! kanta : kannan !Changed from [:Vow|Vow:] to get tuntea>tunsin etc
        k u n _ a: WeakGrade ; !TODO kunta > kunnissa etc

!! **Tests:**

!€ k a n t:n a ^WG:0 > n

!€ k u n t:n a:0 ^WG:0 > i2:i > s t ^A:a 


	
"t:r" 
!! **RULE: @RULENAME@** for rt:rr 
t:r <=>  Vow:+ r: _ :Vow (Cns) WeakGrade ; ! kerta : kerran , jukertel - jukerrella

!! **Tests:**

!€ j u k e r t:r e l ^WG:0 > l ^A:a
!€       a r t:r e l ^WG:0

!! **Tests:**
!€ v e t:s e:i ^E2I:0

!€ v e r e:i ^E2I:0

!€ v a r t:s e:i ^E2I:0

!"t:v"
!!! **RULE: @RULENAME@**
!t:v <=> t a u _ Vow: WeakGrade ; !CHECK tauti


!! ### v rules
"v:0"
!! **RULE: @RULENAME@**
v:0 <=> Vow v _ Vow: [%^HMETA: |%^HMET2:] ;




!! ### Gemination rules

!! The gemination rules **insert** the geminated consonant (thus *0:h* if *h* to the left). 
!! There is one subrule for each vowel context, in order to avoid confilcts.

"Gemination 0:h"
!! **RULE: @RULENAME@**
0:h <=> [.#.|#] Cns:* Vow h _  :a (Dummy:) %> (h) :a ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow (Vow) h _  :e (Dummy:) %> (h) :e ;
        [.#.|#] Cns:* Vow h _  :i (Dummy:) %> (h) :i \:i ;
        [.#.|#] Cns:* Vow h _ o   (Dummy:) %> (h) :o ;
        [.#.|#] Cns:* Vow h _  :u (Dummy:) %> (h) :u ;
        [.#.|#] Cns:* Vow h _  :y (Dummy:) %> (h) :y ;
        [.#.|#] Cns:* Vow h _  :ä (Dummy:) %> (h) :ä ;
        [.#.|#] Cns:* Vow h _  :ö (Dummy:) %> (h) :ö ;
        !Rules for metathesis type two, position 1 in Kuoppa (efter två tonande Cns)
                  Vow   l   _ [j|  m|n|r|v|%^N:] Vow: %^HMET2: ; ! ilhmaa lm, lv, 
                  Vow   r   _ [j|  m|n  |v|%^N:] Vow: %^HMET2: ; ! varhmaan, Norhjaan
                  Vow   n   _ l          Vow: %^HMET2: ; ! 
                  Vow   m   _ n          Vow: %^HMET2: ; !        
        !Rules for metathesis type two, position 2 in Kuoppa/Mantila (efter syll m huvudbetoning & Vow Vow) (ingår i P3)
                !![.#.|#] (Cns) Vow Vow _ [l|m|n|r|v|%^N:] Vow: %^HMET2: %> h: ; ! puohleen, sauhnaan
                !![.#.|#] (Cns) Vow VowNotI _ j Vow: %^HMET2: %> h: ; ! luohjaan
        !Rules for methatesis type two, position 2-3 in Kuoppa (efter syll med Vow Vow)
                Vow (%^WG:) (%>) [Vow|i2:] (%>) _ [l|m|n|r|v|%^N:] (Vow:)  [%^HMET2:] (%>) h: ; !aikoihnaan, saarnaahmaan !HÄR in h
                Vow (%>) VowNotI _ j Vow: %^HMET2: (%>) h: ; ! aluohjaan !Vow (%>) VowNotI _ j Vow: %^HMET2: (%>) h: ;
        !!Rules for metathesis type two, position 4-6 (pe-se/mhaan OCH an-tah/maan öppen/sluten betonad + kort obetonad)
                Vow+ Cns+ (Cns) Vow (%>) _ [l|m|n|r|v|%^N:] (Vow:) (%^WG:) %^HMET2: (%>)  h: :Vow :Vow ; !mostly nouns; elämhään/elähmään 
                Vow+ Cns+ Vow (%>) (%^WG:) %^HMET2: (%>) _ [l|m|n|r|v|%^N:] h: Vow Vow ;!mostly verbs; tulehmaan, neuvottelehmaan
        !!Rules for metathesis type two, position 4 (efter kort bet öppen syll + kort sonorantsluten stavelse; pe-sem-hään)
                !![.#.|#] (Cns) Vow Cns Vow _ [l|m|n|r|v|%^N:] (Vow:) %^HMET2: %>  h: :Vow :Vow ; !nouns; elämhään/elähmään UTAN antahmaan
                !![.#.|#] (Cns* Vow+ Cns+ Vow+ Cns) (Cns) Vow Cns Vow (%>) %^HMET2: (%>) _ [l|m|n|r|v|%^N:] h: Vow Vow ;!verbs; tulehmaan, neuvottelehmaan, valehtelehmaan
                




!uit-mac-443:lang-fit ttr000$ cat misc/freebound.list |grep '^.m[lnrv][aeiouyäö]$'|cut -c2,3|sort|uniq

"Gemination 0:j"
!! **RULE: @RULENAME@**
0:j <=> [.#.|#] Cns:* Vow j _  :a (Dummy:) %> (h) :a ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow j _  :e (Dummy:) %> (h) :e ;
        [.#.|#] Cns:* Vow j _  :i (Dummy:) %> (h) :i ;
        [.#.|#] Cns:* Vow j _ o   (Dummy:) %> (h) :o ;
        [.#.|#] Cns:* Vow j _  :u (Dummy:) %> (h) :u ;
        [.#.|#] Cns:* Vow j _  :y (Dummy:) %> (h) :y ;
        [.#.|#] Cns:* Vow j _  :ä (Dummy:) %> (h) :ä ;
        [.#.|#] Cns:* Vow j _  :ö (Dummy:) %> (h) :ö ;

"Gemination 0:k"
!! **RULE: @RULENAME@**
0:k <=> [.#.|#] Cns:* Vow k _  :a (Dummy:) %> (h) :a ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow k _  :e (Dummy:) %> (h) :e ;
        [.#.|#] Cns:* Vow k _  :i (Dummy:) %> (h) :i ;
        [.#.|#] Cns:* Vow k _ o   (Dummy:) %> (h) :o ;
        [.#.|#] Cns:* Vow k _  :u (Dummy:) %> (h) :u ;
        [.#.|#] Cns:* Vow k _  :y (Dummy:) %> (h) :y ;
        [.#.|#] Cns:* Vow k _  :ä (Dummy:) %> (h) :ä ;
        [.#.|#] Cns:* Vow k _  :ö (Dummy:) %> (h) :ö ;

!! **Tests:**


"Gemination 0:l"
!! **RULE: @RULENAME@**
0:l <=> [.#.|#] Cns:* Vow l _  :a (Dummy:*) %> (h) :a ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow l _  :e (Dummy:) %> (h) :e ;
        [.#.|#] Cns:* Vow l _  :i (Dummy:) %> (h) :i ;
!        [.#.|#] Cns:* Vow l _ i:  (Dummy:) %>  h  :i :i ;
        [.#.|#] Cns:* Vow l _ o   (Dummy:) %> (h) :o ;
        [.#.|#] Cns:* Vow l _  :u (Dummy:) %> (h) :u ;
        [.#.|#] Cns:* Vow l _  :y (Dummy:) %> (h) :y ;
        [.#.|#] Cns:* Vow l _  :ä (Dummy:*) %> (h) :ä ;
        [.#.|#] Cns:* Vow l _  :ö (Dummy:) %> (h) :ö ;

!! **Tests:**


"Gemination 0:m"
!! **RULE: @RULENAME@**
0:m <=> [.#.|#] Cns:* Vow m _  :a (Dummy:) %> (h) :a ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow m _  :e (Dummy:) %> (h) :e ;
        [.#.|#] Cns:* Vow m _  :i (Dummy:) %> (h) :i ;
        [.#.|#] Cns:* Vow m _ o   (Dummy:) %> (h) :o ;
        [.#.|#] Cns:* Vow m _  :u (Dummy:) %> (h) :u ;
        [.#.|#] Cns:* Vow m _  :y (Dummy:) %> (h) :y ;
        [.#.|#] Cns:* Vow m _  :ä (Dummy:) %> (h) :ä ;
        [.#.|#] Cns:* Vow m _  :ö (Dummy:) %> (h) :ö ;

"Gemination 0:n"
!! **RULE: @RULENAME@**
0:n <=> [.#.|#] Cns:* Vow n _  :a (Dummy:) %> (h) :a ;   ! [:a|i2:|%^V:] ;
!     [.#.|#] Cns:* Vow n _ a:  (Dummy:) %> (h) :a :a ;  ! [:a|i2:|%^V:] ; 
        [.#.|#] Cns:* Vow n _  :e (Dummy:) %> (h) :e ;
        [.#.|#] Cns:* Vow n _  :i (Dummy:) %> (h) :i ;
        [.#.|#] Cns:* Vow n _ o   (Dummy:) %> (h) :o ;
        [.#.|#] Cns:* Vow n _  :u (Dummy:) %> (h) :u ;
        [.#.|#] Cns:* Vow n _  :y (Dummy:) %> (h) :y ;
        [.#.|#] Cns:* Vow n _  :ä (Dummy:) %> (h) :ä ;
        [.#.|#] Cns:* Vow n _  :ö (Dummy:) %> (h) :ö ;

"Gemination 0:p"
!! **RULE: @RULENAME@**
0:p <=> [.#.|#] Cns:* Vow p _  :a (Dummy:) %> (h) [:a | i5:] ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow p _  :e (Dummy:) %> (h) [:e | i5:] ;
        [.#.|#] Cns:* Vow p _  :i (Dummy:) %> (h) [:i | i5:] ;
        [.#.|#] Cns:* Vow p _ o   (Dummy:) %> (h) [:o | i5:] ;
        [.#.|#] Cns:* Vow p _  :u (Dummy:) %> (h) [:u | i5:] ;
        [.#.|#] Cns:* Vow p _  :y (Dummy:) %> (h) [:y | i5:];
        [.#.|#] Cns:* Vow p _  :ä (Dummy:) %> (h) [:ä | i5:] ;
        [.#.|#] Cns:* Vow p _  :ö (Dummy:) %> (h) [:ö | i5:] ;

"Gemination 0:s"
!! **RULE: @RULENAME@**
0:s <=> [.#.|#] Cns:* Vow s _  :a (Dummy:) %> (h) [:a | i5:] ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow s _  :e (Dummy:) %> (h) [:e | i5:] ;
        [.#.|#] Cns:* Vow s _  :i (Dummy:) %> (h) [:i | i5:] ;
        [.#.|#] Cns:* Vow s _ o   (Dummy:) %> (h) [:o | i5:] ;
        [.#.|#] Cns:* Vow s _  :u (Dummy:) %> (h) [:u | i5:] ;
        [.#.|#] Cns:* Vow s _  :y (Dummy:) %> (h) [:y | i5:] ;
        [.#.|#] Cns:* Vow s _  :ä (Dummy:) %> (h) [:ä | i5:] ;
        [.#.|#] Cns:* Vow s _  :ö (Dummy:) %> (h) [:ö | i5:] ;

!! **Tests:**




"h:0"
!! **RULE: @RULENAME@**
h:0 <=>        Vow [p|t|k]                  _ e WeakGrade ;
	[.#.|Cns] Vow s :s Vow: %^HMETA: %> _ %^V: %^V: ; ! issoon
                                      Vow h _ Vow: (%^WG:) [%^HMETA: |%^HMET2:] ; 
                                      Cns _ Vow: [%^HMETA: |%^HMET2:] %> h ; !vanha >vanhaan 
!Rules for metathesis type two, position 1 in Kuoppa/Mantila (efter två tonande Cns)
        Vow   l   :h [j|  m|n|r|v|%^N:] Vow: %^HMET2: %> _ ; ! ilhmaa lm, lv, 
        Vow   r   :h [j|  m|n  |v|%^N:] Vow: %^HMET2: %> _ ; ! Norhjaan
        Vow   n   :h  l          Vow: %^HMET2: %> _ ; ! 
        Vow   m   :h  n          Vow: %^HMET2: %> _ ; ! 
!Rules for metathesis type two, position 2 (efter syll m huvudbetoning & Vow Vow) (ingår även i P3)
        !![.#.|#] (Cns) Vow Vow :h [l|m|n|r|v|%^N:] Vow: %^HMET2: %> _ ; ! puohleen, sauhnaan
        !![.#.|#] (Cns) Vow VowNotI :h j Vow: %^HMET2: %> _ ; ! luohjaan
!Rules for metathesis type two, position 2-3 (efter syll m Vow Vow)
        Vow (%^WG:) (%>)  [Vow|i2:]  (%>) :h [l|m|n|r|v|%^N:] (Vow:) %^HMET2: %> _ ; !aikoihnaan, sairaahlaan 
        Vow (%>) VowNotI   :h j Vow: %^HMET2: %> _ ; !aluohjaan  Vow (%>) VowNotI   :h j Vow: %^HMET2: %> _ ;
!!Rules for metathesis type two, position 4-6 (pe-se/mhaan OCH an-tah/maan öppen/sluten betonad + kort obetonad)
        Vow+ Cns+ Vow (%>) :h [l|m|n|r|v|%^N:] (Vow:) (%^WG:) %^HMET2: (%>)  _ :Vow :Vow ; !mostly nouns; elämä, elämhään/elähmään
        Vow+ Cns+ Vow (%>) (%^WG:) %^HMET2: (%>) :h [l|m|n|r|v|%^N:] _ Vow Vow ; !mostly verbs; neuvotele>hmaan, tulehmaan, olehmaan
!!Rules for metathesis type two, position 4 (efter kort bet öppen syll + kort sonorantsluten syll; pe-sem-hään, ju-te-lem-haan men ej antahmaan)
        !![.#.|#] (Cns) Vow Cns Vow :h [l|m|n|r|v|%^N:] (Vow:) %^HMET2: %>  _ :Vow :Vow ; !nouns; elämä, elämhään/elähmään !endast öppna stavelser
        !![.#.|#] (Cns* Vow+ Cns+ Vow+ Cns) (Cns) Vow Cns (Cns) Vow (%>) %^HMET2: (%>) :h [l|m|n|r|v] _ Vow Vow ; !Verbs; neuvotele>hmaan, tulehmaan, olehmaan  
       


! rasta^HMETA>h^Vn

! issoon, sillhoon <== check this.


!"hm to mh for InfMa Ill after short vowel 1"
!! **RULE: @RULENAME@**
!h:m <=> Cns Vow %> _ m: %^A: %^A: ;

!"mh to hm for InfMa Ill after short vowel 2"
!! **RULE: @RULENAME@**
!m:h <=> Cns Vow %> h: _ %^A: %^A: ;

!! kasva>hm^A^An
!! kasva>mhaan

!! saarna>^A>hm^A^An
!! saarna>a>hmaan

!! tule>hm^A^An
!! tule>mhaan


"Gemination 0:t"
!! **RULE: @RULENAME@**
0:t <=> [.#.|#] Cns:* Vow t _  :a (Dummy:) %>  :a ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow t _  :e (Dummy:) %>  :e ;
        [.#.|#] Cns:* Vow t _  :i (Dummy:) %>  :i ;
        [.#.|#] Cns:* Vow t _  :u (Dummy:) %>  :u ;
        [.#.|#] Cns:* Vow t _  :y (Dummy:) %>  :y ;
        [.#.|#] Cns:* Vow t _  :ä (Dummy:) %>  :ä ;
        [.#.|#] Cns:* Vow t _  :ö (Dummy:) %>  :ö ;
        [.#.|#] Cns:* Vow t _ a:  (Dummy:) %> (h) :a :a ;  ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow t _ o   (Dummy:) %> (h) :o ;

!! **Tests:**

!€ p i t 0:t ä > ^A:ä

"Gemination 0:v"
!! **RULE: @RULENAME@**
0:v <=> [.#.|#] Cns:* Vow v _  :a (Dummy:) %> (h) [:a | i5:] ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow v _  :e (Dummy:) %> (h) [:e | i5:] ;
        [.#.|#] Cns:* Vow v _ o   (Dummy:) %> (h) [:o | i5:] ;
        [.#.|#] Cns:* Vow v _  :u (Dummy:) %> (h) [:u | i5:] ;
        [.#.|#] Cns:* Vow v _  :y (Dummy:) %> (h) [:y | i5:] ;
        [.#.|#] Cns:* Vow v _  :ä (Dummy:) %> (h) [:ä | i5:] ;
        [.#.|#] Cns:* Vow v _  :ö (Dummy:) %> (h) [:ö | i5:] ;
        ![.#.|#] Cns:* Vow v _  a: (Dummy:) %>  h   :a :a     ;
        [.#.|#] Cns:* Vow v _  a: (Dummy:) %> (h) [i2: | i5:] (%^WG:) (%>) :i ;
        ![.#.|#] Cns:* Vow v _  a: (Dummy:) %> (h) [:i | i5:] :i ;
!! **Tests:**

!€ k u v 0:v a:0 ^HMETA:0 > h i i n


!! ### Assimilation rules

!! These are assimilation rules for *n* on suffix borders of LNRS consonant stems.
!! There is also a rule **j:0** avoiding a *lji* sequence.

"Alveolar assimilation for consonant stem l"
!! **RULE: @RULENAME@**

 %^N:l <=> l: %> _ :Vow ;

!! **Tests:**

!€ t u l > ^N:l ^U:u

"Alveolar assimilation for consonant stem r"
!! **RULE: @RULENAME@**
%^N:r <=> r: %> _ ;

"Alveolar assimilation for consonant stem s in infinitives"
!! **RULE: @RULENAME@**
!%^N:t <=> s %> _ [%^A:|%^U: [.#.|%>]|| e [s s|n]] ; ! noustessa, nousten, kusta, pestä, kustu, pesty
%^N:t <=> s %> _ [%^A:| e [s s|n]] ; ! noustessa, nousten, kusta, pestä, nousta
!! **Tests:**

!€ h ä r i s > ^N:t ^A:ä

"Alveolar assimilation for consonant stem s in participles"
!! **RULE: @RULENAME@**
%^N:s <=> s %> _ %^U: (t) ; ! kussut, pessyt
 !         s %> _ e ;      ! kusseet (?)

	  


!! ## Vowel change rules: a - ä - e - i - o - ö - u - y

!! Here come the rules for stem vowel changes in front of suffix *-i-*
!! (be it plural, present, comparative or conditional).
!! Vowels are deleted or changed according to context.
!! There are also some other vowel change rules.

!! ###  a rules

"a:e"
!! **RULE: @RULENAME@** before the ^AE trigger
a:e <=> _ %^AE: ;

"a:0"
!! **RULE: @RULENAME@** before metathesis h
a:0 <=>                                                          _ [%^HMETA: | %^HMET2:] %> ;        ! 
    [.#.|#] Cns:* RndVow (Vow) Cns:+ (:Cns) (Vow:)               _ (%^WG:) (%^IDEL:)  %> [i2:|i3:|j|i8:] ;   
                                                a                _    %> [i2:|i3:|i4:|i5:] ; !shortening before i; saa:sai
 [.#.|#] Cns:* Vow:+ Cns:+ Vow:+ Cns:+ (:Cns) (i)                _ (%^WG:)  %> [i3: |i8:] ;   
         Cns:* Vow:+ Cns:+ VowNotI:+ NonLNR (i)                  _ (%^WG:)  %> i2: ; !3-syll nouns: NOT with last vow=i, not with lnr
 [.#.|#] (Cns+ Vow+) Cns:* Vow:+ Cns:+ [Vow:+ Cns:+|Cns VowNotI] _ ([%^WG:|%^HMETA:|%^HMET2:])  %> (h) i: ; !3-syll 
 [.#.|#] Cns:* Vow:+ Cns:+ Vow:+ Cns:+ (:h)                      _ ([%^WG:|%^HMETA:|%^HMET2:]) %> (h:) (Vow) i9: ; !adjectives with plural-i; kamalissa 
 [.#.|#] Cns:* RndVow (Vow) Cns:+                                _ (%^WG:)  %> i3: ;   ! huutaa>huusin
                                                                 _  (%^WG:) (i m e:) %> i5: ; !kova>kovimpiin ; adj superlativ

!! **Tests:**
!€ k u v a:0 > i2:i s s ^A:a
!€ k u v 0:v a:0 ^HMETA:0 > h ^V:a  ^V:a n
!€ s a a:0 > i3:i t

"a:o when nonrounded root vowel and before i"
!! **RULE: @RULENAME@**
a:o <=> [.#.|#] Cns:* UnRndVow:+ ([u|i|e|a]) Cns:+ (:Cns) (i) _ (%^WG:|%^A2O:) (%^IDEL:) %> [i2:|i3:|i8:|j] ; !saunoissa, vanhoitten
        [.#.|#] Cns:* UnRndVow:+ ([o|u])     Cns:+ (:Cns) (i) _ (%^WG:|%^A2O:) %> [i2:|i3:|i8:|j] ; ! see fkv
        [.#.|#] Cns:*   Vow:+       Cns:+  i Cns:+ (:Cns) (i) _ (%^WG:|%^A2O:) %> [i2:|j]; !lukijoissa, apinoissa; 3-syll with last vow=i  
        [.#.|#] Cns:* Vow:+ Cns:+ Vow    [l|n|r|Cns Cns:]     _ (%^WG:|%^A2O:) %> i2: ; !3-syll with lnr
        (Cns:+)   Vow:+ Cns:+ (Cns:) Vow:+  (Cns:) Cns: Cns: _ (%^WG:|%^HMETA:|%^HMET2:|%^A2O:) %> [i2:] ; !almunakoissa; ammunoissa (flerst.) a>o eft. 2xCns
        (Cns:+) Vow:+ Cns:+ (Cns:) Vow:+ [l|n|r]              _ (%^WG:|%^HMETA:|%^HMET2:|%^A2O:) %> [i2:] ; !tavara (flerstaviga) a>o efter lnr
        [.#.|#] Vow:+  Cns:+ (Cns:) (Vow:) i:    Cns:+ (Cns:) _ (%^WG:|%^A2O:) %> [i2:|j]; !; (flerstaviga) a>o efter i !NY alkeija
        ![.#.|#] Cns:* UnRndVow:+ ([o|u])     Cns:+ (:Cns) (i) _ (%^WG:) %> [i2:|i3:|i8:|j] ; ! see fkv

!! **Tests:**
!€ l a l t i a:o > i2:i t t e n
!€ f a n k i l a:o > i2:i s s ^A:a

!! ###  ä rules

"ä:0"
!! **RULE: @RULENAME@**
ä:0 <=>   Cns:* Vow:+ Cns:+ _ (WeakGrade) (%^IDEL:) %> [i2:|i3:|i8:|j] ; ! before i
                            _ [%^HMETA:|%^HMET2:]            ; ! before metathesis h
        ä                   _                       %> [i2:|i3:|i5:|i8:]     ; ! jäädä : jäi
                            _                       %> i5: ; !selvä>selvimpiin ; adj superlativ

!! **Tests:**

"ä:e"
!! **RULE: @RULENAME@**
ä:e <=> _ %^AE: %^WG: %> t  ; 
        _ %^AE: %> ; !adj comparative 


!! ###  e rules

"e:0"
!! **RULE: @RULENAME@** deletes *-e-* in LNR stems as well as before -i-
e:0 <=>                                                   _ (WeakGrade) h %> e ; ! säkheen del before h in e stems
                                                          _ [%^HMETA: | %^HMET2:] ;
                         [.#.|#:] Cns:* Vow:+ [h|l|n|r|t] _ %> t [%^A:|%^U:]; ! tulta, tultu
                           [.#.|#:] Cns:* Vow:+ [l|n|r] t _ %> t [%^A:|%^U:]; ! tulta, tultu
                              [.#.|#] Cns:* [Vow Vow|u] l _ %> t e n ; ! kielten, puolten
                              [.#.|#] Cns:* [Vow Vow|e] r _ %> t e n ; ! merten, puolten
[.#.|#|%-] Cns:* [i [e|i]|u ([u|o])|y y|ä ä|e (e)|a i] :n _ %> t e n ; !unten, lunten, sienten, lienten
                                                     Cns: _ (e) (WeakGrade) %> [i2:|i3:|i4:|i5:|i8:] ; !Shortening before i
                                                t i _ t:0 o ; ! to get tieto > tiot etc (CHECK: expand?)
!     k: s _ [.#.|#|t] ; ! look at this

!! **Tests:**

!€ t u l e:0 ^WG:0 > i2:i s s ^A:a

"e:i"
!! **RULE: @RULENAME@**
e:i <=> Cns:* Vow:* Cns:* _ (%^WG:) (%>) %^E2I: ; !18 nov


!! **Tests:**

!€ o v e:i ^E2I:0

"ä:ö"
!! **RULE: @RULENAME@**
ä:ö <=> Cns:+ Vow:+ Cns:+ Vow: Cns:+ _ (Dummy:) %^A2O:0 ; !TBC to get päärynä>päärynöitä 19 nov


!! **Tests:**


!! ###  i rules

"i:0"
!! **RULE: @RULENAME@**
i:0 <=>  
!Vow (:Cns) Cns _ [%^HMETA: | %^HMET2:] ;               ! before metathesis h
                  Cns   _ [e|i] (%^WG:0) %> [i2:|i3:|i4:|i5:] ; ! tie, pii, viedä
          Cns [a|o|ö|ä] _       %> [i2:|i3:|i4:] ; ! hai, koi, naida, puida, vasikoida, käpelöidä
                        _ [%^HMETA:|%^HMET2:] ;               ! keppi : kepphiin
	               Cns: _ (WeakGrade) %>  [i3:|i8:] ; ! sopia : sopi, oppia : oppi removed i2
      [#|.#.] (Cns) Vow _ %> i3: ; ! uija>uin
                  Cns: _ (%^WG:) %^IDEL: %> [i2:|e:] ; ! tukki : tukkitten 
                   ! tarkista: xxx "i to e in plural" i:e <=> Cns: _ ([Dummy: - %^HMETA:]) %> i2: ;
                   ![(^%HMETA: h: |%^HMET2: h:)] 

!! **Tests:**


"i:j"
!! **RULE: @RULENAME@**
i:j <=> [o|ö] _ k:0 :Vow ; ! pojan
        [o|ö] _ k:0 Vow:0 %^WG: %> :i ; ! pojissa
       [.#.|#] a _ k:0 a %^WG: %> ; !aika:ajan
      ! :Vow _ %> :Vow ; ! For i between vowels in suffix – paksu:paksujen
 
"i2:j"
!! **RULE: @RULENAME@**
i2:j <=> :Vow (Dummy:+) (%>) _ :Vow ;

"i8:0"
!! **RULE: @RULENAME@**
i8:0 <=> :Vow Cns:+ [:o|u|y] %> _ [.#.|%>] ; !added u for tuntua>tuntu, y for kääntyä>käänty




!! **Tests:**


"i:e"
!! **RULE: @RULENAME@**
i:e <=> Cns _ %> i2: ;



!! ###  o rules

"o:0"
!! **RULE: @RULENAME@**
o:0 <=> _ [%^HMETA: | %^HMET2:] ;  ! deletion before metathesis h
        o  _    %> [i2:|i3:|i4:|i5:] ; !shortening before i

!! **Tests:**


!! ###  ö rules
"ö:0"
!! **RULE: @RULENAME@**
!ö:0 <=> [Cns|.#.|#] Vow (Cns) Cns: _ [%^HMETA: | %^HMET2:] ; !  deletion before metathesis h
ö:0 <=>  _ [%^HMETA: | %^HMET2:] ; !  deletion before metathesis h
        ö _ %> [i2:|i3:|i4:|i5:] ; !shortening before i


!! **Tests:**



!! ###  u rules

"u:0"
!! **RULE: @RULENAME@**
u:0 <=> [Cns|.#.|#] Vow (Cns) Cns _ %^HMETA: ;           ! before metathesis h
        [Cns|.#.|#]               _ [o|u] %> [i2:|i3:|i4:|i5:] ; ! tuoda : toi, luu : luissa
                                  _ [%^HMETA: | %^HMET2:] ; !kruunu, kruuhnuun
 !       u  _    %> [i2:|i3:|i4:|i5:] ; !shortening before i

!! **Tests:**




!! ###  y rules

"y:0"
!! **RULE: @RULENAME@**
y:0 <=> [Cns|.#.|#] Vow (Cns) Cns _ %^HMETA: ;         ! deletion before h-metathesis
        _ [y|ö]  (%^WG:) %> [i2:|i3:|i4:|i|i5:] ; ! x syy, myydä, syödä,
                                  _ [%^HMETA: | %^HMET2:] ;

!! **Tests:**






!! ### Vowel copying rules

!! These are the rules connected to the Meänkieli *-h-* suffixes. 
!! The vowel must be copied from the stem to the right of the *h*
!! and also deleted in the stem (cf. *talo : talhoon*)

"a copying for h metathesis"
!! **RULE: @RULENAME@**
%^V:a <=>                   a: [%^HMETA:|%^HMET2:] %> h:     _ [:a|i] ; ! sauhnaan
                            a: [%^HMETA:|%^HMET2:] %> h:  :a _      ; ! sauhnaan
UnRndVow (Vow) (:h) Cns:+ a: [%^HMETA:|%^HMET2:] %> h: _ [:a|i] ; ! was: :i  **conflict**
                                                a  %> (h)    _ [n|t|s|l|%>|%^N:|.#.] ; !noutta

!! **Tests:**




"o copying for h metathesis"
!! **RULE: @RULENAME@**
%^V:o <=>                                                o: [%^HMETA:|%^HMET2:] %> (h:)    _ [:i|:o] ;
                                                         o: [%^HMETA:|%^HMET2:] %> (h:) :o _  ;
                                                         o                      %> (h)   _ [n|t|s|l|%>|.#.] ;
[.#.|#|Cns] [a|e|i] (Vow:) (:h) Cns:*                    a: [%^HMETA:|%^HMET2:] %>  (h:)   _ i2: ; !saunhoin/saunhoin 
[.#.|#] (Cns:*) Vow:+ Cns:+ Cns:+ Vow:+ Cns: Cns: (Cns:) a: [%^HMETA:|%^HMET2:] %> (h:)  _ i2: ; !Pl+Ill för >2-syll-nouns: a>o efter två Cns (almunakka>almunakhoin) **conflict**
[.#.|#] (Cns:*) Vow:+ Cns:+ Cns:+ Vow:+ [l|n|r] a: [%^HMETA:|%^HMET2:] %> (h:)  _ i2: ; !Pl+Ill för >2-syll-nouns: a>o efter lnr (ikkuna) **conflict**

!! **Tests:**


! *** Warning: Unresolved <= conflict with respect to '^V:a' vs. '^V:o'
!     between "a copying for h metathesis"
!         and "o copying for h metathesis"
! 
!  **** Conflicting pair(s) of contexts: 
!   UnRndVow (Vow) (:h) Cns:+ a: [^HMETA: | ^HMET2:] > h: _ :a | i ;
!   [.#. | #] (Cns:*) Vow:+ Cns:+ Cns:+ Vow:+ Cns: Cns: (Cns:) a: [^HMETA: | ^HMET2:] > (h:) _ :i | i2: ;
! Left context example:     o b b o a o b b a:0 ^HMETA:0 > h
! Right context example:    i

"i copying for h metathesis"
!! **RULE: @RULENAME@**
%^V:i <=> i: [%^HMETA: | %^HMET2:] %> (h:)    _ :i ;
          i: [%^HMETA: | %^HMET2:] %> (h:) :i _    ;
         :i           %> (h)   _ [n|t|s|l|%>|.#.] ;
         [.#.|#] Cns:* RndVow+ Cns: (Cns:) (:Cns) a: (%^WG:|%^HMETA:|%^HMET2:)  %> (h:) _ [i2:] ;  !muna>munhiin Pl+Ill h: _ [i2:] ;
         [.#.|#] Cns:* Vow+ Cns:+ (:Cns) (Cns) ä: (%^WG:|%^HMETA:|%^HMET2:)  %> (h:) _ [i2:] ;  !silmä>silmhiin Pl+Ill
         !a:0 ([%^HMETA:|%^HMET2:]) %> (h:) _ [i9:i |i2:i ]; !after adjektiv or noun plural-i !5/2
         a:0 ([%^HMETA:|%^HMET2:]) %> (h:) _ i9:i ; !after adjektiv plural-i !Testar att ta bort ovanstående för att få tillbaka seuhroin etc

!! **Tests:**



"ä copying for h metathesis"
!! **RULE: @RULENAME@**
%^V:ä <=> ä: [%^HMETA: | %^HMET2:] %> (h:)    _ [:ä|i] ; 
          ä: [%^HMETA: | %^HMET2:] %> (h:) :ä _    ;
          ä           %> (h)   _ [n|t|s|l|%>|.#.] ;
          
"e copying for h metathesis"
!! **RULE: @RULENAME@**
%^V:e <=> e: [%^HMETA: | %^HMET2:] %> (h:)    _ [:e|:i] ;
          e: [%^HMETA: | %^HMET2:] %> (h:) :e _    ;
          e         %>  (h)     _ [n|t|s|l|%>|.#.] ;


"ö copying for h metathesis"
!! **RULE: @RULENAME@**
%^V:ö <=> ö: [%^HMETA: | %^HMET2:] %> (h:)    _ [:ö|:i] ;
          ö: [%^HMETA: | %^HMET2:] %> (h:) :ö _    ;
          ö           %> (h)   _ [n|t|s|l|%>|.#.] ;
          
"y copying for h metathesis"
!! **RULE: @RULENAME@**
%^V:y <=> y: [%^HMETA: | %^HMET2:] %> (h:)    _ [:y|:i] ;
          y: [%^HMETA: | %^HMET2:] %> (h:) :y _    ;
          y           %> (h)   _ [n|t|s|l|%>|.#.] ;
          
"u copying for h metathesis"
!! **RULE: @RULENAME@**
%^V:u <=> u: [%^HMETA: | %^HMET2:] %> (h:)    _ [:u|:i] ;
          u: [%^HMETA: | %^HMET2:] %> (h:) :u _    ;
          u           %> (h)   _ [n|t|s|l|%>|.#.] ;




!! ###  Vowel harmony rule

!! All vowel harmony is taken care of with one rule.
	
"Back harmony"
!! **RULE: @RULENAME@**
 Vx:Vy <=> BackVowel: NonFront:* (:NonFront) NonFront:* %> NonFront:* ([ NonFront: | :NonFront ]) NonFront:* (%>) NonFront:* _ ;  
         where Vx in ( %^A %^O %^U )
               Vy in (   a   o   u )
         matched ;

!! **Tests:**

!!$ katto^WG
!!$ katto0



! san0a>i2ss^A
! sanno>issa ! hmm
! san0a^HMETA>h^Vin
! sann00>hoin

! Test that deliberately fails.



!t:0 
!		  :e %> _ %^A: ; ! hanhea -- rather two classes
!        [.#.|#:] Cns:* Vow:+ Cns:+ Vow:+ [l|n|r] _ Vow:+ WeakGrade ; ! imarela : imartelen
!       Vow:+ Cns:+ i t     _ Vow:+     WeakGrade ; ! esitellä
!       VowNotI (h)      _ [a|e|o|u] WeakGrade ; ! katu : kaun
!       VowNotI        t _ e: h %> e           ;
!      [VowNotI|l|n|r] t _ Vow:      WeakGrade ; ! motti Quant Grad. Note h!
