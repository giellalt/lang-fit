!! Meänkieli twolc file

!! # Alphabet
Alphabet

a b c d e f g h i j k l m n o p q r s t u v w x y z å ä ö æ ø
%^WG:0    ! weak grade
%^E2I:0   ! kiele- > kieli
%^HMETA:0 ! vow metathesis for ill
%^AE:0    ! a to e in otta- > otethaan
%>        ! suffix boundary
i2:i      ! plural of nouns
i3:i      ! past tense of verbs
i4:i      ! i in conditional of isi
i5:i      ! superlative i of adjectives
p2:p t2:t k2:k    ! always p t k.
t3:t  	  !!≈ * **@CODE@** = t participating in gradation, but not in t:s
t4:t      ! t3 goes to 0 in imartelee : imarella
%^A:ä %^O:ö %^U:y ! vowel harmony archiphonemes
%^V:a   %^V:e   %^V:o   %^V:u   %^V:y   %^V:i   %^V:ä   %^V:ö
%^N:n ! participle -nut
;

!! # Sets
Sets

Dummy          = %+ %^WG %^E2I %^HMETA %^VDEL %^EDEL %^AE %^AO %^¤ ; !!= * @CODE@
DummyBorder    = Dummy %> ;                                          !!= * @CODE@
ArchiVowel     = %^A %^O %^U ;                                       !!= * @CODE@ 
SomeVowel      = %^V ;                                               !!= * @CODE@ 
NeutralVowel   = e i               i2 i3 i4 i5 ;                     !!= * @CODE@ 
FrontVowel     = e i y ä ö         ü  æ  ø     ;                     !!= * @CODE@ 
BackVowel      =           a o u å             ;                     !!= * @CODE@ 
RoundedVowel   =     y   ö   o u å             ;                     !!= * @CODE@ 
UnroundedVowel = e i   ä   a                   ;                     !!= * @CODE@ 
VowelNotUY     = e i   ä ö a o   å ü  æ  ø     ;                     !!= * @CODE@ 
VowelNotI      = e   y ä ö a o u å ü  æ  ø     ;                     !!= * @CODE@ 
VowelNotEI     =     y ä ö a o u å ü  æ  ø     ;                     !!= * @CODE@ 
Vow            = FrontVowel BackVowel ArchiVowel SomeVowel ;         !!= * @CODE@ 
ArchiCns       = %^N ;                                               !!= * @CODE@
LNRM           = l n r m ;                                           !!= * @CODE@
SurfaceCns     = b c d đ f g h j k l m n p q r s š t v w x z ;       !!= * @CODE@
Cns            = SurfaceCns ArchiCns p2 t2 t3 t4 k2 ;                !!= * @CODE@
Segment        = Vowel Cns ;                                         !!= * @CODE@
NonFront       = BackVowel ArchiVowel ArchiCns SomeVowel Cns NeutralVowel Dummy ;  !!= * @CODE@ 


!! # Definitions
Definitions

WeakGrade = ([l|n|r]) (%^AE:) %^WG: ;


!! # Rules 
Rules 

!! ## Consonant rules

!! ### Gradation rules
!! p rules

"p:0"
p:0 <=> [Vow|l|m|r] p _ Vow: WeakGrade ; 
                Vow p _ e: h %> e ;

!!€ appi^WG
!!€ ap0i0

"p:v"
p:v <=> Vow   _ Vow:  WeakGrade ;
        Vow r _ Vow WeakGrade ;

!!€ papu^WG>n
!!€ pavu0>n

!!€ varpa^WGs
!!€ varva0s

!! k rules

"k:g"
k:g <=> Vow n _ Vow WeakGrade ;

!!€ kenkä^WG>n
!!€ kengä0>n

"k:0"
k:0 <=> [Vow|l|n|r] k _ Vow: WeakGrade      ;   ! Quant Grad
                 o i: _ Vow: WeakGrade      ;   ! pojan
         VowelNotUY   _ Vow: WeakGrade      ;   ! maun 
	    [u|y]     _ VowelNotUY: WeakGrade ; ! tuen
                Vow k _ e:   h     %> e ;       ! kkh > kh
            Vow [l|r] _ VowelNotEI WeakGrade ;
	      Vow h k _ Vow WeakGrade ;
	  [Vow - e] h _ [Vow - e:] WeakGrade ;

!!€ takki^WG>n
!!€ tak0i0>n

!!€ johke^WG
!!€ joh0e0


"k:j"
k:j <=> Vow [l|r] _ [:i|:e]  WeakGrade ;   ! syljen
        Vow [l|r] _ Vow:0  WeakGrade %> :i ; ! syljin,
	e h _ [e:|i:] ;

!!€ sylke^WG>n
!!€ sylje0>n

!!€ kehke^WG>t^A
!!€ kehje0>ta


"k:v"
k:v <=> [u|y] _ [y|u] WeakGrade ;

!!€ suku^WG>n
!!€ suvu0>n

!! t rules

"t:0"
t:0 <=> Vow ([l|n|r]) t _ Vow:+ WeakGrade ; ! new try: all tt give t
        i               _  [ä|a]    WeakGrade ; ! pitä- : piän, haitata
        VowelNotI (h)   _ Vow       WeakGrade ; ! katu : kaun
       [ä|e]            _  i        WeakGrade ; !
!        [.#.|#:] Cns:* Vow:+ Cns:+ Vow:+ [l|n|r] _ Vow:+ WeakGrade ; ! imarela : imartelen
!       Vow:+ Cns:+ i t     _ Vow:+     WeakGrade ; ! esitellä
!       VowelNotI (h)      _ [a|e|o|u] WeakGrade ; ! katu : kaun
!       VowelNotI        t _ e: h %> e           ;
!      [VowelNotI|l|n|r] t _ Vow:      WeakGrade ; ! motti Quant Grad. Note h!

!!€ motti^WG
!!€ mot0i0

!!€ tehta^WGs
!!€ teh0a0s

!!€ pitä^WG>t
!!€ pi0ä0>t

!!€ tytär^WG
!!€ ty0är0

"t4:0"
t4:0 <=> Vow:+ [l|n|r] _ Vow:+ WeakGrade ;

!!€ imart4el^WG>l^A
!!€ imar0el0>la

"t:j"
t:j <=> [.#.|#:] Cns:* Vow:* i _ i     WeakGrade ;
        [.#.|#:] Cns:* Vow:* i _ [o|u] WeakGrade ;

!!€ hoito^WG>n
!!€ hoijo0>n

"t:l" ! lt:ll 
t:l <=> Vow:+ l _ :Vow WeakGrade [s|%> [\i2:i|\i3:i]|.#.] ; ! kulta : kullan

!!€ kulta^WG>n
!!€ kulla0>n

"t:n" ! nt:nn 
t:n <=> Cns:* Vow:+  n _ :Vow WeakGrade [s|%> [\i2:i|\i3:i]|.#.] ; ! kanta : kannan

!!€ kanta^WG>n
!!€ kanna0>n

"t:r" ! rt:rr 
t:r <=>  Vow:+ r _ :Vow WeakGrade [s|%> [\i2:i|i3:i]|.#.] ; ! kerta : kerran

!!€ jukertel^WG>l^A
!!€ jukerrel0>la


"t:s"
t:s <=> [Vow - i] ([l|n|r]) _ e:0 (WeakGrade) %> [i2:|i3:] ;
        [Vow - i] ([l|n|r]) _ e: %^E2I: .#. ;
! Rest of rule from fkv:
        Vow n  _        e:0                    %> i2: [.#.|%> Cns] ; ! fkv
 Vow ([n|l|r]) _ (:s)   e:   %^E2I: ; ! fkv: uusine, vesi: vete^E2I:vesi, vete^WG>n:vede0>n, vete^WG>i2>ll^A:vesillä
 Vow           _ (:s)   e:0                    %> i2: %> [Cns|%^V:|%^A:] ; ! [vuosiksi, etc.|vuossii(n)|uusia] ! uusine
 Vow [n|l|r]   _       e:0  (%^WG:0)   %^E2I:  %> i2: ; ! hirsiksi, hirsi (pl - gen par) *)
 Vow ([n|l|r]) _        e:0 (%^AO:0) %^HMETA:0 %> h :i ;    ! jok uushiin
 [i|u|y|ö] [e|u|y] _ (:s) Vow:0      (%^AO:0)  %> i3: ;   ! huuttaat:huusi, löyttäät:löysi (ei sousi, nousi tai hoisi)
 Vow  n        _     Vow:0  [Dummy:* - %^WG:0] %> i3: ;
 Vow [n|l|r]   _ (:s) Vow:0 (%^WG:0)  (%^AO:0) %> i3: ;
             i _ k: 0: ä: %> i5:i  ; ! superlatiivi pitkä/pissiin

!!€ vete^E2I
!!€ vesi0

!!€ vere^E2I
!!€ veri0

!!€ varte^E2I
!!€ varsi0



!! ### Gemination rules

"Gemination 0:h"
0:h <=> [.#.|#] Cns:* Vow h _  :a (Dummy:) %> (h) :a ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow h _ a:  (Dummy:) %> (h) :a :a ;  ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow h _  :e (Dummy:) %> (h) :e ;
        [.#.|#] Cns:* Vow h _ e:  (Dummy:) %> h :e :e ;
        [.#.|#] Cns:* Vow h _  :i (Dummy:) %> (h) :i \:i ;
        [.#.|#] Cns:* Vow h _ i:  (Dummy:) %> h :i :i ;
        [.#.|#] Cns:* Vow h _ o   (Dummy:) %> (h) :o ;
        [.#.|#] Cns:* Vow h _ o:  %^HMETA: %> h :o :o ;
        [.#.|#] Cns:* Vow h _  :u (Dummy:) %> (h) :u ;
        [.#.|#] Cns:* Vow h _ u:  (Dummy:) %> h :u :u ;
        [.#.|#] Cns:* Vow h _  :y (Dummy:) %> (h) :y ;
        [.#.|#] Cns:* Vow h _ y:  (Dummy:) %> h :y :y ;
        [.#.|#] Cns:* Vow h _  :ä (Dummy:) %> (h) :ä ;
        [.#.|#] Cns:* Vow h _ ä:  (Dummy:) %> h :ä :ä ;
        [.#.|#] Cns:* Vow h _  :ö (Dummy:) %> (h) :ö ;
        [.#.|#] Cns:* Vow h _ ö:  (Dummy:) %> h :ö :ö ;
                    Vow Vow _ Cns Vow: %^HMETA: ;

"Gemination 0:j"
0:j <=> [.#.|#] Cns:* Vow j _  :a (Dummy:) %> (h) :a ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow j _ a:  (Dummy:) %> (h) :a :a ;  ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow j _  :e (Dummy:) %> (h) :e ;
        [.#.|#] Cns:* Vow j _ e:  (Dummy:) %> h :e :e ;
        [.#.|#] Cns:* Vow j _  :i (Dummy:) %> (h) :i ;
        [.#.|#] Cns:* Vow j _ i:  (Dummy:) %> h :i :i ;
        [.#.|#] Cns:* Vow j _ o   (Dummy:) %> (h) :o ;
        [.#.|#] Cns:* Vow j _ o:  %^HMETA: %> h :o :o ;
        [.#.|#] Cns:* Vow j _  :u (Dummy:) %> (h) :u ;
        [.#.|#] Cns:* Vow j _ u:  (Dummy:) %> h :u :u ;
        [.#.|#] Cns:* Vow j _  :y (Dummy:) %> (h) :y ;
        [.#.|#] Cns:* Vow j _ y:  (Dummy:) %> h :y :y ;
        [.#.|#] Cns:* Vow j _  :ä (Dummy:) %> (h) :ä ;
        [.#.|#] Cns:* Vow j _ ä:  (Dummy:) %> h :ä :ä ;
        [.#.|#] Cns:* Vow j _  :ö (Dummy:) %> (h) :ö ;
        [.#.|#] Cns:* Vow j _ ö:  (Dummy:) %> h :ö :ö ;

"Gemination 0:k"
0:k <=> [.#.|#] Cns:* Vow k _  :a (Dummy:) %> (h) :a ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow k _ a:  (Dummy:) %> (h) :a :a ;  ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow k _  :e (Dummy:) %> (h) :e ;
        [.#.|#] Cns:* Vow k _ e:  (Dummy:) %> h :e :e ;
        [.#.|#] Cns:* Vow k _  :i (Dummy:) %> (h) :i ;
        [.#.|#] Cns:* Vow k _ i:  (Dummy:) %> h :i :i ;
        [.#.|#] Cns:* Vow k _ o   (Dummy:) %> (h) :o ;
        [.#.|#] Cns:* Vow k _ o:  %^HMETA: %> h :o :o ;
        [.#.|#] Cns:* Vow k _  :u (Dummy:) %> (h) :u ;
        [.#.|#] Cns:* Vow k _ u:  (Dummy:) %> h :u :u ;
        [.#.|#] Cns:* Vow k _  :y (Dummy:) %> (h) :y ;
        [.#.|#] Cns:* Vow k _ y:  (Dummy:) %> h :y :y ;
        [.#.|#] Cns:* Vow k _  :ä (Dummy:) %> (h) :ä ;
        [.#.|#] Cns:* Vow k _ ä:  (Dummy:) %> h :ä :ä ;
        [.#.|#] Cns:* Vow k _  :ö (Dummy:) %> (h) :ö ;
        [.#.|#] Cns:* Vow k _ ö:  (Dummy:) %> h :ö :ö ;

!!€ tek0e>e
!!€ tekke>e

"Gemination 0:l"
0:l <=> [.#.|#] Cns:* Vow l _  :a (Dummy:) %> (h) :a ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow l _ a:  (Dummy:) %> (h) :a :a ;  ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow l _  :e (Dummy:) %> (h) :e ;
        [.#.|#] Cns:* Vow l _ e:  (Dummy:) %> h :e :e ;
        [.#.|#] Cns:* Vow l _  :i (Dummy:) %> (h) :i ;
        [.#.|#] Cns:* Vow l _ i:  (Dummy:) %> h :i :i ;
        [.#.|#] Cns:* Vow l _ o   (Dummy:) %> (h) :o ;
!        [.#.|#] Cns:* Vow l _ o:  %^HMETA: %> h :o :o ;
        [.#.|#] Cns:* Vow l _  :u (Dummy:) %> (h) :u ;
        [.#.|#] Cns:* Vow l _ u:  (Dummy:) %> h :u :u ;
        [.#.|#] Cns:* Vow l _  :y (Dummy:) %> (h) :y ;
        [.#.|#] Cns:* Vow l _ y:  (Dummy:) %> h :y :y ;
        [.#.|#] Cns:* Vow l _  :ä (Dummy:) %> (h) :ä ;
        [.#.|#] Cns:* Vow l _ ä:  (Dummy:) %> h :ä :ä ;
        [.#.|#] Cns:* Vow l _  :ö (Dummy:) %> (h) :ö ;
        [.#.|#] Cns:* Vow l _ ö:  (Dummy:) %> h :ö :ö ;

!!€ tul0e>e
!!€ tulle>e

"Gemination 0:m"
0:m <=> [.#.|#] Cns:* Vow m _  :a (Dummy:) %> (h) :a ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow m _ a:  (Dummy:) %> (h) :a :a ;  ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow m _  :e (Dummy:) %> (h) :e ;
        [.#.|#] Cns:* Vow m _ e:  (Dummy:) %> h :e :e ;
        [.#.|#] Cns:* Vow m _  :i (Dummy:) %> (h) :i ;
        [.#.|#] Cns:* Vow m _ i:  (Dummy:) %> h :i :i ;
        [.#.|#] Cns:* Vow m _ o   (Dummy:) %> (h) :o ;
        [.#.|#] Cns:* Vow m _ o:  %^HMETA: %> h :o :o ;
        [.#.|#] Cns:* Vow m _  :u (Dummy:) %> (h) :u ;
        [.#.|#] Cns:* Vow m _ u:  (Dummy:) %> h :u :u ;
        [.#.|#] Cns:* Vow m _  :y (Dummy:) %> (h) :y ;
        [.#.|#] Cns:* Vow m _ y:  (Dummy:) %> h :y :y ;
        [.#.|#] Cns:* Vow m _  :ä (Dummy:) %> (h) :ä ;
        [.#.|#] Cns:* Vow m _ ä:  (Dummy:) %> h :ä :ä ;
        [.#.|#] Cns:* Vow m _  :ö (Dummy:) %> (h) :ö ;
        [.#.|#] Cns:* Vow m _ ö:  (Dummy:) %> h :ö :ö ;

"Gemination 0:n"
0:n <=> [.#.|#] Cns:* Vow n _  :a (Dummy:) %> (h) :a ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow n _ a:  (Dummy:) %> (h) :a :a ;  ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow n _  :e (Dummy:) %> (h) :e ;
        [.#.|#] Cns:* Vow n _ e:  (Dummy:) %> h :e :e ;
        [.#.|#] Cns:* Vow n _  :i (Dummy:) %> (h) :i ;
        [.#.|#] Cns:* Vow n _ i:  (Dummy:) %> h :i :i ;
        [.#.|#] Cns:* Vow n _ o   (Dummy:) %> (h) :o ;
        [.#.|#] Cns:* Vow n _ o:  %^HMETA: %> h :o :o ;
        [.#.|#] Cns:* Vow n _  :u (Dummy:) %> (h) :u ;
        [.#.|#] Cns:* Vow n _ u:  (Dummy:) %> h :u :u ;
        [.#.|#] Cns:* Vow n _  :y (Dummy:) %> (h) :y ;
        [.#.|#] Cns:* Vow n _ y:  (Dummy:) %> h :y :y ;
        [.#.|#] Cns:* Vow n _  :ä (Dummy:) %> (h) :ä ;
        [.#.|#] Cns:* Vow n _ ä:  (Dummy:) %> h :ä :ä ;
        [.#.|#] Cns:* Vow n _  :ö (Dummy:) %> (h) :ö ;
        [.#.|#] Cns:* Vow n _ ö:  (Dummy:) %> h :ö :ö ;

"Gemination 0:p"
0:p <=> [.#.|#] Cns:* Vow p _  :a (Dummy:) %> (h) :a ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow p _ a:  (Dummy:) %> (h) :a :a ;  ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow p _  :e (Dummy:) %> (h) :e ;
        [.#.|#] Cns:* Vow p _ e:  (Dummy:) %> h :e :e ;
        [.#.|#] Cns:* Vow p _  :i (Dummy:) %> (h) :i ;
        [.#.|#] Cns:* Vow p _ i:  (Dummy:) %> h :i :i ;
        [.#.|#] Cns:* Vow p _ o   (Dummy:) %> (h) :o ;
        [.#.|#] Cns:* Vow p _ o:  %^HMETA: %> h :o :o ;
        [.#.|#] Cns:* Vow p _  :u (Dummy:) %> (h) :u ;
        [.#.|#] Cns:* Vow p _ u:  (Dummy:) %> h :u :u ;
        [.#.|#] Cns:* Vow p _  :y (Dummy:) %> (h) :y ;
        [.#.|#] Cns:* Vow p _ y:  (Dummy:) %> h :y :y ;
        [.#.|#] Cns:* Vow p _  :ä (Dummy:) %> (h) :ä ;
        [.#.|#] Cns:* Vow p _ ä:  (Dummy:) %> h :ä :ä ;
        [.#.|#] Cns:* Vow p _  :ö (Dummy:) %> (h) :ö ;
        [.#.|#] Cns:* Vow p _ ö:  (Dummy:) %> h :ö :ö ;

"Gemination 0:s"
0:s <=> [.#.|#] Cns:* Vow s _  :a (Dummy:) %> (h) :a ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow s _ a:  (Dummy:) %> (h) :a :a ;  ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow s _  :e (Dummy:) %> (h) :e ;
        [.#.|#] Cns:* Vow s _ e:  (Dummy:) %> h :e :e ;
        [.#.|#] Cns:* Vow s _  :i (Dummy:) %> (h) :i ;
        [.#.|#] Cns:* Vow s _ i:  (Dummy:) %> h :i :i ;
        [.#.|#] Cns:* Vow s _ o   (Dummy:) %> (h) :o ;
        [.#.|#] Cns:* Vow s _ o:  %^HMETA: %> h: :o :o ; ! issoon
        [.#.|#] Cns:* Vow s _  :u (Dummy:) %> (h) :u ;
        [.#.|#] Cns:* Vow s _ u:  (Dummy:) %> h :u :u ;
        [.#.|#] Cns:* Vow s _  :y (Dummy:) %> (h) :y ;
        [.#.|#] Cns:* Vow s _ y:  (Dummy:) %> h :y :y ;
        [.#.|#] Cns:* Vow s _  :ä (Dummy:) %> (h) :ä ;
        [.#.|#] Cns:* Vow s _ ä:  (Dummy:) %> h :ä :ä ;
        [.#.|#] Cns:* Vow s _  :ö (Dummy:) %> (h) :ö ;
        [.#.|#] Cns:* Vow s _ ö:  (Dummy:) %> h :ö :ö ;

!!€ is0o^HMETA>h^V^Vn
!!€ iss00>0oon

"h gradation"
h:0 <=>     Vow [p|t|k]                  _ e WeakGrade ;
        Vow Vow :h Cns Vow: %^HMETA: %> _ ;
	[.#.|Cns] Vow s :s Vow: %^HMETA: %> _ %^V: %^V: ; ! issoon
! issoon, sillhoon <== check this.

"Gemination 0:t"
0:t <=> [.#.|#] Cns:* Vow t _  :a (Dummy:) %>  :a ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow t _  :e (Dummy:) %>  :e ;
        [.#.|#] Cns:* Vow t _  :i (Dummy:) %>  :i ;
        [.#.|#] Cns:* Vow t _  :u (Dummy:) %>  :u ;
        [.#.|#] Cns:* Vow t _  :y (Dummy:) %>  :y ;
        [.#.|#] Cns:* Vow t _  :ä (Dummy:) %>  :ä ;
        [.#.|#] Cns:* Vow t _  :ö (Dummy:) %>  :ö ;
        [.#.|#] Cns:* Vow t _ a:  (Dummy:) %> (h) :a :a ;  ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow t _ e:  (Dummy:) %> h :e :e ;
        [.#.|#] Cns:* Vow t _ i:  (Dummy:) %> h :i :i ;
        [.#.|#] Cns:* Vow t _ o   (Dummy:) %> (h) :o ;
        [.#.|#] Cns:* Vow t _ o:  %^HMETA: %> h :o :o ;
        [.#.|#] Cns:* Vow t _ u:  (Dummy:) %> h :u :u ;
        [.#.|#] Cns:* Vow t _ y:  (Dummy:) %> h :y :y ;
        [.#.|#] Cns:* Vow t _ ä:  (Dummy:) %> h :ä :ä ;
        [.#.|#] Cns:* Vow t _ ö:  (Dummy:) %> h :ö :ö ;

!!€ pit0ä>^A
!!€ pittä>ä

"Gemination 0:v"
0:v <=> [.#.|#] Cns:* Vow v _  :a (Dummy:) %> (h)  :a ;   ! [:a|i2:|%^V:] ;
        [.#.|#] Cns:* Vow v _ a:  (Dummy:) %> (h) [:a|:i] [:a|:i] ;
        [.#.|#] Cns:* Vow v _  :e (Dummy:) %> (h) :e ;
        [.#.|#] Cns:* Vow v _ e:  (Dummy:) %>  h  :e :e ;
        [.#.|#] Cns:* Vow v _  :i (Dummy:) %> (h) :i ;
        [.#.|#] Cns:* Vow v _ i:  (Dummy:) %>  h  :i :i ;
        [.#.|#] Cns:* Vow v _ o   (Dummy:) %> (h) :o  ;
        [.#.|#] Cns:* Vow v _ o:  %^HMETA: %>  h  :o :o ;
        [.#.|#] Cns:* Vow v _  :u (Dummy:) %> (h) :u  ;
        [.#.|#] Cns:* Vow v _ u:  (Dummy:) %>  h  :u :u ;
        [.#.|#] Cns:* Vow v _  :y (Dummy:) %> (h) :y  ;
        [.#.|#] Cns:* Vow v _ y:  (Dummy:) %>  h  :y :y ;
        [.#.|#] Cns:* Vow v _  :ä (Dummy:) %> (h) :ä ;
        [.#.|#] Cns:* Vow v _ ä:  (Dummy:) %>  h  :ä :ä ;
        [.#.|#] Cns:* Vow v _  :ö (Dummy:) %> (h) :ö ;
        [.#.|#] Cns:* Vow v _ ö:  (Dummy:) %>  h  :ö :ö  ;

!!€ kuv0a^HMETA>hiin
!!€ kuvv00>hiin



!! ### Assimilation rules

"Alveolar assimilation for consonant stem l"
!! **@RULENAME@**
 %^N:l <=> l: %> _ ;

!!€ tul>^N^U
!!€ tul>lu

"Alveolar assimilation for consonant stem r"
!! **@RULENAME@**
%^N:r <=> r: %> _ ;

"Alveolar assimilation for consonant stem s in infinitives"
!! **@RULENAME@**
%^N:t <=> s %> _ [%^A:|%^U: [.#.|%>]] ; ! kusta, pestä, kustu, pesty

!!€ häris>^N^A
!!€ häris>tä

"Alveolar assimilation for consonant stem s in participles"
!! **@RULENAME@**
%^N:s <=> s %> _ %^U: t ; ! kussut, pessyt
          s %> _ e ;      ! kusseet (?)

	  
"j:0 in front of i"
!! **@RULENAME@**
j:0 <=> l _ e: %^E2I:0 ;



!! ## Vowel change rules: a - ä - e - i - o - ö - u - y

!! ###  a rules

"a:0"
a:0 <=>                                          _ %^HMETA: %> ;  ! before metathesis h
        [.#.|#] Cns:* RoundedVowel+ Cns:+ (:Cns) _ (%^WG:)  %> [i2:|i3:|j] ;
        [.#.|#] Cns    a                         _          %> [i2:|i3:|i4:] ; ! saada, maa

!!€ kuva>i2ss^A
!!€ kuv0>issa

!!€ kuv0a^HMETA>h^V^Vn
!!€ kuvv00>haan

!!€ saa>i3t
!!€ sa0>it

"a:e"
a:e <=> _ %^AE: ;


"a:o when nonrounded root vowel and before i"
a:o <=> [.#.|#] Cns:* UnroundedVowel:+ ([u|i|e|a]) Cns:+ (:Cns) (i) _ (%^WG:) %> [i2:|i3:|j] ;
        [.#.|#] Cns:* UnroundedVowel:+ ([o|u])     Cns:+ (:Cns) (i) _ (%^WG:) %> [i2:|i3:|j] ; ! see fkv
      [.#.|#] Cns:*            Vow:+    Cns:+  Vow Cns:+ (:Cns) (i) _ (%^WG:) %> [i2:|i3:|j] ;

!      [.#.|#] Cns:* Vow:+             Cns:+  Vow Cns:+            _ (%^WG:) %> [i2:|i3:|j] ; 	
!a:o <=> [.#.|#|#] Cns:* UnroundedVowel:+ ([u|i|e|a]) Cns:+ (:Cns) _ (%^WG:) %> [i2:|j] ;
!        [.#.|#|#] Cns:* UnroundedVowel:+ ([o|u])     Cns:+ (:Cns) _ (%^WG:) %> [i2:|j] ; ! see fkv
!        [.#.|#|#] Cns:* RoundedVowel:+    Cns:+  Vow Cns:+ (:Cns) _ (%^WG:) %> [i2:|j] ; 

!!€ laatia>i2tten
!!€ laatio>itten

!!€ fankila>i2ss^A
!!€ fankilo>issa

!! ###  ä rules

"ä:0"
ä:0 <=>         Cns:* Vow:+ Cns:+ _ (WeakGrade)  %> [i2:|i3:|j] ; ! before i
                                  _ %^HMETA:            ; ! before metathesis h
         [.#.|#] Cns    ä         _          %> [i2:|i3:|i5:]     ; ! jäädä : jäi

!!€ sytyttä^WG>i3n
!!€ sytyt000>in

!!€ päivä>i2ss^A
!!€ päiv0>issä

!!€ pitä>i3
!!€ pit0>i

!!€ jää>i3n
!!€ jä0>in

"ä:e"
ä:e <=> _ %^AE: %^WG: %> t h  ;



!! ###  e rules

"e:0"
e:0 <=>       _ h %> e ; ! säkheen  deletion before h in e stems
              _ %^HMETA: ;
!     k: s _ [.#.|#|t] ; ! look at this
[.#.|#:] Cns:* Vow:+ [l|n|r|t] _ %> t [%^A:|%^U:]; ! tulta, tultu
[.#.|#:] Cns:* Vow:+ [l|n|r] t _ %> t [%^A:|%^U:]; ! tulta, tultu
         Cns: _ (WeakGrade) %> [i2:|i3:|i4:] ;
 
!!€ tule^WG>i2ss^A
!!€ tul00>issa


"e:i"
e:i <=>[.#.|#:] Cns:* Vow:+ Cns:+ _ %^E2I: ;

!!€ ove^E2I
!!€ ovi0



!! ###  i rules

"i:0"
i:0 <=>  Vow (:Cns) Cns _ %^HMETA: ;               ! before metathesis h
                  Cns   _ [e|i] %> [i2:|i3:|i4:] ; ! tie, pii, viedä
          Cns [a|o|ö|ä] _       %> [i2:|i3:|i4:] ; ! hai, koi, naida, puida, vasikoida, käpelöidä
                        _ %^HMETA: ;               ! keppi : kepphiin
	           Cns: _ (WeakGrade) %>  [i2:|i3:] ; ! sopia : sopi, oppia : oppi 
                   ! tarkista: xxx "i to e in plural" i:e <=> Cns: _ ([Dummy: - %^HMETA:]) %> i2: ;


!!€ sii0li^HMETA>h^V^Vn
!!€ siihl00>0iin

!!€ hai>i2t
!!€ ha0>it

!!€ vasikoi>i3n
!!€ vasiko0>in

!!€ vie>i3n
!!€ v0e>in

!!€ oppi>i3
!!€ opp0>i

"i:j"
i:j <=> Vow _ k:0 :Vow ; ! pojan
        Vow _ k:0 Vow:0 %^WG: %> :i ; ! pojissa

"i2:j"
i2:j <=> :Vow (Dummy:) %> _ :Vow ;

!!€ sana>i2^A
!!€ sano>ja




!! ###  o rules

"o:0"
o:0 <=> _ %^HMETA: ;  ! deletion before metathesis h"

!!€ poro^HMETA>h^V^Vn
!!€ por00>hoon

!! ###  ö rules
"ö:0"
ö:0 <=> [Cns|.#.|#] Vow (Cns) Cns _ %^HMETA: ; !  deletion before metathesis h

!!€ hölmö^HMETA>h^V^Vn
!!€ hölm00>höön


!! ###  u rules

"u:0"
u:0 <=> [Cns|.#.|#] Vow (Cns) Cns _ %^HMETA: ;           ! before metathesis h
        [Cns|.#.|#]               _ [o|u] %> [i2:|i3:|i4:] ; ! tuoda : toi, luu : luissa

!!€ tuo>i3n
!!€ t0o>in



!! ###  y rules
"y:0"
y:0 <=> [Cns|.#.|#] Vow (Cns) Cns _ %^HMETA: ;         ! deletion before metathesis h"
        [Cns|.#.|#]               _ [y|ö]  (%^WG:) %> [i2:|i3:|i4:] ; ! x syy, myydä, syödä,
                                  _ %^HMETA: ;

!!€ syy>i2ss^A
!!€ s0y>issä





!! ### Vowel copying rules


"a copying for h metathesis"
%^V:a <=> a: %^HMETA: %> h:    _ :a ;
          a: %^HMETA: %> h: :a _    ;
          a           %>       _ [n|t|%>|.#.] ;

!!€ hinta^HMETA>h^V^Vn
!!€ hint00>haan

"o copying for h metathesis"
%^V:o <=> o: %^HMETA: %> h:    _ [:i|:o] ;
          o: %^HMETA: %> h: :o _  ;
          o           %>       _ [n|t|%>|.#.] ;

!!€ talo^HMETA>h^V^Vn
!!€ tal00>hoon

"i copying for h metathesis"
%^V:i <=> i: %^HMETA: %> h:    _ :i ;
          i: %^HMETA: %> h: :i _    ;
         :i           %>       _ [n|t|%>|.#.] ;

!!€ silli^HMETA>h^V^Vn
!!€ sill00>hiin


"ä copying for h metathesis"
%^V:ä <=> ä: %^HMETA: %> h:    _ :ä ;
          ä: %^HMETA: %> h: :ä _    ;
          ä           %>       _ [n|t|%>|.#.] ;
          
"e copying for h metathesis"
%^V:e <=> e: %^HMETA: %> h:    _ :e ;
          e: %^HMETA: %> h: :e _    ;
           :e         %>       _ [n|t|%>|.#.] ;
          
"ö copying for h metathesis"
%^V:ö <=> ö: %^HMETA: %> h:    _ :ö ;
          ö: %^HMETA: %> h: :ö _    ;
          ö           %>       _ [n|t|%>|.#.] ;
          
"y copying for h metathesis"
%^V:y <=> y: %^HMETA: %> h:    _ :y ;
          y: %^HMETA: %> h: :y _    ;
          y           %>       _ [n|t|%>|.#.] ;
          
"u copying for h metathesis"
%^V:u <=> u: %^HMETA: %> h:    _ :u ;
          u: %^HMETA: %> h: :u _    ;
          u           %>       _ [n|t|%>|.#.] ;




!! ###  Vowel harmony rule
	
"Back harmony"
!! **@RULENAME@** 
 Vx:Vy <=> BackVowel: NonFront:* (:NonFront) NonFront:* %> NonFront:* ([ NonFront: | :NonFront ]) NonFront:* (%>) NonFront:* _ ;  
         where Vx in ( %^A %^O %^U )
               Vy in (   a   o   u )
         matched ;

!!€ nais>i2lt^A
!!€ nais>ilta

!!€ puhu>^A
!!€ puhu>a

!!€ hinkka^WG>t^A
!!€ hink0a0>ta

!!$ katto^WG
!!$ katto0

!!€ keskus>t^A
!!€ keskus>ta	



! san0a>i2ss^A
! sanno>issa ! hmm
! san0a^HMETA>h^Vin
! sann00>hoin
